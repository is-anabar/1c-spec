# Билет 1 (занятие 45)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задачи следует учесть возможность наличия проводок, сформированных с помощью данного документа.

Компания занимается торговлей продуктами питания. Учет товаров ведется в разрезе сроков годности. Под сроком годности принимается календарная дата, до которой товар годен к употреблению. На один и тот же товар с разными сроками годности при его поступлении может указываться разная цена. Например, на товар со сроком годности 10 января 2010 цена может быть ниже, чем на товар со сроком годности 30 января 2010. Товар с одинаковым сроком годности может поступать разными документами и по разной цене. Возможна ситуация когда в одном документе один и тот же товар поступает с разными сроками годности (и по разной цене). 

Учет товаров в разрезе складов не ведется.

Документ «Приходная накладная» реализует следующую проводку:
- Дт «Товары» - Кт «Поставщики» на количество и сумму закупаемого товара.

Продажа товара регистрируется документом «Расходная накладная». При продаже срок годности не указывается. В первую очередь списывается товар с наименьшим календарным сроком годности. Себестоимость определяется как средняя по товару по всем срокам годности. Т.е. например если 1 пачка йогурта со сроком годности 10.01.2010 поступила по цене 90 рублей и еще 1 пачка того же йогурта, но со сроком годности 30.01.2010 поступила по цене 110 рублей, то при списании себестоимость одной пачки данного йогурта равна 100 рублей.

Документ «Расходная накладная» реализует следующие проводки:
- Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму в продажных ценах.

При проведении документа анализируется наличие этого товара в организации. Если товара не достаточно документ не проводится.
Необходимо создать отчет, выдающий данные о количественном и суммовом остатке товаров.

![report](/2%20-%20БУ/media/ticket-1-report.png)

Сумма для срока годности должна получаться расчетным образом как средняя цена по данному товару умноженная на количество с данным сроком. Средняя цена рассчитывается как общая сумма данного товара поделенная на общее количество данного товара.


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Настройка Виды Субконто

Добавляем:
- Номенклатура
- Срок годности (дата)


## Настройка плана счетов

<!-- настройка 
0 - общее\05 как заполнить регистр бухгалтерии.md
Остатки ведутся по какому-то признаку, а себестоимость - по всей компании -->

### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Количественный
- Признаки учета субконто:
	- Суммовой

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура 
		- `устанавлиеваем` признак учета субконто
	- Сроки годности 
		- `не устанавлиеваем` признак учета субконто

Так как в отчете только данные по остаткам, счет `ПрибылиУбытки` можно не настраивать.


## Настройка регистра бухгалтерии

Необходимо выполнить [базовые настройки](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

Настройка будет похожа на пункт [Остатки ведутся по какому-то признаку, а себестоимость - по всей компании](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md).

Итоговые настройки:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма
		- признак учета субконто: `Суммовой`

## Поступление товаров

Для формирования движений можно воспользоваться конструктором движений.

## Списание товаров

### Установка флагов 

Очищаем движения (если были) и устанавливаем флаг на запись:
```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Алгоритм запроса будет примерно следующий:
- группировка табличной части по номенклатуре - избавляемся от дублей строк - `втТовары`
- получение информации об остатках:
	- достаем информацию о том, какую номенклатуру, сколько потребовали и за сколько продали в документе
	- достаем из `Управленческий.Остатки` информацию по общим остаткам (первое соедниение):
		- номенклатура
		- количество остаток общее
		- сумма остаток общее
	- достаем из `Управленческий.Остатки` информацию по общим остаткам (второе соединение):
		- номенклатура
		- срок годности
		- количество по сроку годности
	- итоги по:
		- количество (из документа)
		- количество остаток общее (для проверки отрицательных остатков)
		- сумма остаток общее (для средней цены)


```1c
ВЫБРАТЬ
	// группировка табличной части
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// данные по общим остаткам - выводятся только для номенклатуры
	ОстаткиОбщие.Субконто1 КАК НоменклатураСубконтоОбщее,
	ОстаткиОбщие.КоличествоОстаток КАК КоличествоОстатокОбщее,
	ОстаткиОбщие.СуммаОстаток КАК СуммаОстатокОбщее,
	// данные по остаткам по срокам годности - для пар "номенклатура + срок годности"
	ОстаткиПоСроку.Субконто1 КАК НоменклатураСубконтоПоСрокам,
	ОстаткиПоСроку.Субконто2 КАК СрокСубконтоПоСрокам,
	ОстаткиПоСроку.КоличествоОстаток КАК КоличествоОстатокПоСрокам
ИЗ
	втТовары КАК втТовары
		// соединение с общими остатками
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконтоОбщее, // тут список только с номенклатурой
				Субконто1 В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиОбщие
		ПО втТовары.Номенклатура = ОстаткиОбщие.Субконто1
		// соединения для остатков по парам "номенклатура + срок годности"
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконтоПоСрокам, // тут список с номенклатурой и сроками
				Субконто1 В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиПоСроку
		ПО втТовары.Номенклатура = ОстаткиПоСроку.Субконто1

УПОРЯДОЧИТЬ ПО
	СрокСубконтоПоСрокам
ИТОГИ
	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма),
	МАКСИМУМ(КоличествоОстатокОбщее),
	МАКСИМУМ(СуммаОстатокОбщее)
ПО
	Номенклатура
```

### Формирование движений

```1c
ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
Пока ВыборкаНоменклатура.Следующий() Цикл
	// проверка, что в целом товара хватает
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстатокОбщее;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
		ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// запоминаем, сколько потребовали по документу	
	КоличествоКСписанию = ВыборкаНоменклатура.Количество; 
	// так как мы списываем среднюю сумму, нужно определить, сколько мы будем списывать
	// - если мы списываем номенклатуру полностью, то списываем всю сумму
	// - если списываем только часть, то часть
	// это нужно, чтобы не пропали копейки при расчете себестоимости
	Если ВыборкаНоменклатура.Количество = ВыборкаНоменклатура.КоличествоОстатокОбщее Тогда
		СуммаКСписанию = ВыборкаНоменклатура.СуммаОстатокОбщее;
	Иначе
		СуммаКСписанию = Окр(ВыборкаНоменклатура.Количество * 
			ВыборкаНоменклатура.СуммаОстатокОбщее / ВыборкаНоменклатура.КоличествоОстатокОбщее, 2);
	КонецЕсли;
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И КоличествоКСписанию > 0 Цикл
		// определяем количество, которое спишем у текущего срока годности
		Списываем = Мин(КоличествоКСписанию, Выборка.КоличествоОстатокПоСрокам);

		// рассчитываем себестоимость в зависимости 
		// от полного списания текущией строки		
		Если Списываем = КоличествоКСписанию Тогда
			Себестоимость = СуммаКСписанию;
		Иначе
			Себестоимость = Списываем * ВыборкаНоменклатура.СуммаОстатокОбщее / ВыборкаНоменклатура.КоличествоОстатокОбщее; 
		КонецЕсли;

		// обновляем информацию о том, какое количество и сумму
		// осталось списать	
		КоличествоКСписанию = КоличествоКСписанию - Списываем;
		СуммаКСписанию = СуммаКСписанию - Себестоимость;
			
		// списание остатков
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		// дт 
		Движение.СчетДт = Счета.ПрибылиУбытки;
		// кт
		Движение.СчетКт = Счета.Товары;
		Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[Субконто.СрокиГодности] = Выборка.СрокСубконтоПоСрокам;
		Движение.КоличествоКт = Списываем;
		// общее 
		Движение.Сумма = Себестоимость;
	КонецЦикла; 
		
	// продажи
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата; 
	// дт
	Движение.СчетДт = Счета.Покупатели;
	// кт 
	Движение.СчетКт = Счета.ПрибылиУбытки;
	// общее 
	Движение.Сумма = ВыборкаНоменклатура.Сумма;
КонецЦикла;
```

### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- 1 элемента блокировки; хоть у нас `2 левых соединения`, но они используют один и тот же отбор при соединении
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- блокировка 1: 
		- счет
		- список номенклатуры; можно достать из документа


```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	ОстаткиОбщие.Субконто1 КАК Номенклатура,
	ВЫБОР
		КОГДА ОстаткиОбщие.КоличествоОстаток = 0
			ТОГДА 0
		ИНАЧЕ ОстаткиОбщие.СуммаОстаток / ОстаткиОбщие.КоличествоОстаток
	КОНЕЦ * ОстаткиПоСроку.КоличествоОстаток КАК СредняяСуммаПоСроку,
	ОстаткиПоСроку.Субконто1 КАК НоменклатураСубконтоПоСроку, // вспомогательное
	ОстаткиПоСроку.Субконто2 КАК СрокГодности, // вспомогательное
	ОстаткиПоСроку.КоличествоОстаток КАК КоличествоОстатокПоСроку
ИЗ
	РегистрБухгалтерии.Управленческий.Остатки(&Период, Счет = &СчетТовары, &ВидыСубконтоОбщие, ) КАК ОстаткиОбщие
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(&Период, Счет = &СчетТовары, &ВидыСубконтоПоСрокам, ) КАК ОстаткиПоСроку
		ПО ОстаткиОбщие.Субконто1 = ОстаткиПоСроку.Субконто1
```

Для вывода отчета необходимо сделать:
- Добавить в ресурсы поля:
	- СредняяСуммаПоСроку
	- КоличествоОстатокПоСроку
- Вывести отчет:
	- (группировка) Номенлатура
		- (группировка) Срок годности. Вывести ресурсы в выбранных полях

	



