# Билет 12 (занятие 54)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Организация занимается сборкой и продажей персональных компьютеров. Сборка готовых компьютеров происходит из отдельных комплектующих. Учет комплектующих ведется на счете «Материалы», а готовых изделий на счете «Товары». Сборка и хранение всех материалов происходят в головном офисе, а готовая продукция развозится на разные склады, поэтому складской учет ведется только по счету «Товары».

Продажа отражается документом «Расходная накладная», причем продаваться могут как готовые компьютеры; так и отдельные комплектующие, которые указываются в одной табличной части. В документе обязательно должен быть указан склад, с которого будет произведена отгрузка готовых компьютеров.

При проведении документ «Расходная накладная» формирует проводки:
- Дт «Прибыли и убытки» - Кт «Товары»  (на количество и себестоимость компьютеров; себестоимость рассчитывается как средняя по номенклатурной позиции на складе)
- Дт «Прибыли и убытки» - Кт «Материалы» (на количество и себестоимость комплектующих; себестоимость рассчитывается как средняя но номенклатурной позиции)
- Дт «Покупатели» - Кт «Прибыли и убытки» (на сумму в продажи)

Необходимо создать отчет о продажах товаров.

Продажи товаров за период с 01.01.2010 по 31.01.2010

|Товар	|Количество	|Себестоимость	|Сумма продажи|
|------|-----------|---------------|------------|
|Монитор |NEC	|5	|100 000	|210 000|
|Комп. «Чайник»	|2	|37 500	|50 000|


## Примечание

Для упрощения решения задачи можно считать, что компьютеры приходят от поставщиков.

> Можно сделать отдельный документ для сборки, но:
> - об этом не просили в задаче
> - нужно делать алгоритм списания с разделением количества компьютеров на пропорциональные части - лень


## Виды номенклатуры

Настроить виды номенклатуры можно по [инструкции](оу%20общее.md#filter-items)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Настройка Виды Субконто

### Склады

Создаем справочник `Склады`.

### Типы субконто

Добавляем:
- Номенклатура
- Склад


## Настройка плана счетов

Настройка будет выполняться по схеме [Простой учет остатков товаров](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md#простой-учет-остатков-товаров)


### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Количественный

### Настройка счетов

Материалы:
- Количественный: `да`
- Субконто:
	- Номенклатура

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Склад

Прибыли убытки:
- Количественный: `нет`
- Субконто: 
	- Номенклатура: `только обороты`



## Настройка регистра бухгалтерии

В итоге должно быть:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма:
		- `Балансовый`

## Приходная накладная

Добавляем реквизит `Склад`.


Для проведения необходимо:
- разделить номенклатуру на детали и комплекты
- детали записать на счета Материалы
- комплекты - на Товары

```1c
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Деталь КАК ЭтоДеталь
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Деталь
```

Записываем движения:
```1c
Пока Выборка.Следующий() Цикл
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Если Выборка.ЭтоДеталь Тогда
		Движение.СчетДт = ПланыСчетов.Управленческий.Материалы;
	Иначе
		Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
	КонецЕсли;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетали.Номенклатура;
	Движение.КоличествоДт = ВыборкаДетали.Количество;
	// кт
	Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
	// общее
	Движение.Сумма = ВыборкаДетали.Сумма;
КонецЦикла;
```

## Расходная накладная


### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Управленческий.Записать(); 

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Блокировка

Чтобы правильно установить блокировку, нужно разделить детали и комплекты по разным результатам запроса. Сделать это можно с помощью пакета запросов.

Данные по временной таблице пригодятся нам позже - для проверки отстатков.

```1c
// используем, чтобы сохранить временные таблицы
МВТ = Новый МенеджерВременныхТаблиц;
	
Запрос = Новый Запрос;
// устанавливаем, чтобы запомнить втТовары
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	// группировка табличной части
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Деталь КАК ЭтоДеталь
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Деталь
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// детали
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура
	|ИЗ
	|	втТовары КАК втТовары
	|ГДЕ
	|	втТовары.ЭтоДеталь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// комплекты
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура
	|ИЗ
	|	втТовары КАК втТовары
	|ГДЕ
	|	НЕ втТовары.ЭтоДеталь";
	
Запрос.УстановитьПараметр("Деталь", Перечисления.ВидыНоменклатуры.Деталь);
Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
РезультатЗапроса = Запрос.ВыполнитьПакет();
РезультатДетали = РезультатЗапроса[1];
РезультатКомплекты = РезультатЗапроса[2];

// БЛОКИРОВКУ УДОБНЕЙ ЗАПОЛНИТЬ 
// ПОСЛЕ НАПИСАНИЯ ЗАПРОСА НА СПИСАНИЕ ДЕТАЛЕЙ
Блокировка = Новый БлокировкаДанных;
// детали
// здесь требуется заблокировать только список номенклатуры
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Материалы);
ЭлементБлокировки.ИсточникДанных = РезультатДетали;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
// комплекты
// здесь требуется заблокировать список номенклатуры и склад
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
ЭлементБлокировки.ИсточникДанных = РезультатКомплекты;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
ЭлементБлокировки.УстановитьЗначение(Субконто.Склады, Склад);
Блокировка.Заблокировать();
```

### Запрос

```1c
Запрос = Новый Запрос;
// устанавливаем, чтобы получить данные из прошлого запроса
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
		// данные из документа
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	втТовары.Количество КАК Количество,
	|	втТовары.Сумма КАК Сумма,
	// данные из остатков
	|	ВЫБОР
	|		КОГДА втТовары.ЭтоДеталь
	|			ТОГДА ЕСТЬNULL(ОстаткиДетали.КоличествоОстаток, 0)
	|		ИНАЧЕ ЕСТЬNULL(ОстаткиКомплекты.КоличествоОстаток, 0)
	|	КОНЕЦ КАК КоличествоОстаток,
	|	ВЫБОР
	|		КОГДА втТовары.ЭтоДеталь
	|			ТОГДА ЕСТЬNULL(ОстаткиДетали.СуммаОстаток, 0)
	|		ИНАЧЕ ЕСТЬNULL(ОстаткиКомплекты.СуммаОстаток, 0)
	|	КОНЕЦ КАК СуммаОстаток,
	|	втТовары.ЭтоДеталь КАК ЭтоДеталь,
	|	ОстаткиКомплекты.Субконто2 КАК Склад
	|ИЗ
	|	втТовары КАК втТовары
			// данные по деталям
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&МоментВремени,
	|				Счет = &СчетМатериалы,
	|				&ВидыСубконтоДетали, // здесь субконто: номенклатура
	|				Субконто1 В
	|					(ВЫБРАТЬ
	|						втТовары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						втТовары КАК втТовары
	|					ГДЕ // соединяемся только по деталям
	|						втТовары.ЭтоДеталь)) КАК ОстаткиДетали
	|		ПО втТовары.Номенклатура = ОстаткиДетали.Субконто1
			// данные по комплектам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&МоментВремени,
	|				Счет = &СчетТовары,
	|				&ВидыСубконтоТовары, // здесь субконто: номенклатура, склад
	|				Субконто1 В
	|						(ВЫБРАТЬ
	|							втТовары.Номенклатура КАК Номенклатура
	|						ИЗ
	|							втТовары КАК втТовары
	|						ГДЕ // соединяемся только по комплектам
	|							НЕ втТовары.ЭтоДеталь)
	|					И Субконто2 = &Склад) КАК ОстаткиКомплекты
	|		ПО втТовары.Номенклатура = ОстаткиКомплекты.Субконто1";
```

### Формирование движений

```1c
Выборка = РезультатЗапроса.Выбрать();
Пока Выборка.Следующий() Цикл
	// проверка, что хватает номенклатуры
	Разница = Выборка.Количество - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("БУ: Недостаточно товара %1 в количестве %2",
		Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить(); 
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли; 

	// проверка, списываем мы всю номенклатуру или нет	
	Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
		Себестоимость = Выборка.СуммаОстаток;
	Иначе
		Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
	КонецЕсли;

	// списание
	// 	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт 
	Движение.СчетДт = Счета.ПрибылиУбытки;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
	// кт 
	Если Выборка.ЭтоДеталь Тогда
		Движение.СчетКт = Счета.Материалы;
	Иначе
		Движение.СчетКт = Счета.Товары;
		Движение.СубконтоКт[Субконто.Склады] = Выборка.Склад;
	КонецЕсли;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.КоличествоКт = Выборка.Количество;
	// общее
	Движение.Сумма = Себестоимость;

	// продажи
	// 	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.Покупатели;
	// кт
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	// общее
	Движение.Сумма = Выборка.Сумма;
КонецЦикла;
```

### Отчет

```1c
ВЫБРАТЬ
	УправленческийОбороты.Субконто1 КАК Номенклатура,
	УправленческийОбороты.КоличествоКорОборот КАК Количество,
	УправленческийОбороты.СуммаОборотДт КАК Себестоимотсть,
	УправленческийОбороты.СуммаОборотКт КАК Продажа
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, , Счет = &СчетПрибылиУбытки, &ВидыСубконто, , КорСчет В (&СчетМатериалы, &СчетТовары, &СчетПокупатели), ) КАК УправленческийОбороты
```
