# Билет 16 (занятие 49)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Компания занимается оптовой торговлей. Поступление товара отражается документом «Приходная накладная», продажа – «Расходная накладная».

Документ «Приходная накладная» реализует следующие проводки:
- Дт «Товары» - Кт «Поставщики» на количество и сумму закупаемого товара.

Списание себестоимости товаров должно быть организовано по партиям, в зависимости от текущего значения принятого на этот год в учетной политике метода списания себестоимости (FIFO или LIFO). Еще раз подчеркивается  - учетная политика действует год.  На следующий год метод списания может смениться.

Документ «Расходная накладная» реализует следующие проводки:
- Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости;
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму  в продажных ценах.

В силу особенности номенклатуры, периодически товар портится. Весь товар и плохой и испорченный учитывается на счете «Товары», но при продаже необходимо отгружать только неиспорченный товар. Факт порчи товара определяется и отражается пользователем вручную самостоятельно. В дальнейшем испорченный товар списывается посредством документа «Операция» со следующей проводкой:
- Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости

Необходимо построить отчет по продажам товаров  за период.

Продажи с 01.01.2014 по 31.03.2014 

|Номенклатура	|Себест-ть продаж	|Продажа кол./сумма	|Списано кол./сумма|
|---------------|-------------------|-------------------|------------------|


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)

### Порча товаров

Данные по порче товаров будут вводится: Товары (хорошие) -> Товары (порча)


## Настройка Виды Субконто

### Состояние товаров

Создаем перечисление `Состояние товаров` со значениями:
- Хорошее
- Испорчен

### Типы субконто

Добавляем:
- Номенклатура
- Партии (приходная накладная)
- Состояния товаров

## Настройка плана счетов

Настройка будет выполняться по схеме [простого учета остатков товаров](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md#простой-учет-остатков-товаров)


### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Партия
	- Состояние (для контроля остатков хороших товаров)

Прибыли убытки:
- Количественный: `нет`
- Субконто: 
	- Номенклатура: `только обороты`
	- Состояние: `только обороты` (для отчета) 

> Т.к. в отчете 2 корсчета, и только у одного из них - Товары - будет состояние, то:
> - при перестановке субконто в предприятии отчет сломается
> - при программном указании кор. субконто отчет тоже сломается


## Настройка регистра бухгалтерии

В итоге должно быть:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма:
		- `Балансовый`



## Приходная накладная

Движения можно сформировать с помощью конструктора. В качестве состояния записываем `Хорошее`.



## Расходная накладная

### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Управленческий.Записать(); 

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	// группируем табличную часть
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// данные по остаткам
	УправленческийОстатки.Субконто2 КАК Партия,
	УправленческийОстатки.Субконто3 КАК СостояниеТовара,
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				Субконто1 В // отбор по номенклатуре
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
							// только хорошие товары
					И Субконто3 = &СостояниеХорошее) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1

УПОРЯДОЧИТЬ ПО
	ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Документ.ПриходнаяНакладная).МоментВремени
ИТОГИ
	// для проверки остатков
	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма), // для продажи
	СУММА(КоличествоОстаток) // для проверки остатков
ПО
	Номенклатура
```

### Формирование движений

```1c
ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
Пока ВыборкаНоменклатура.Следующий() Цикл
	// проверка остатков
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
	Если Разница > 0 Тогда 
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;

	//	запоминаем, сколько требуется списать
	ОсталосьСписать = ВыборкаНоменклатура.Количество;
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
		// определяем, сколько списываем из текущей партии
		Списываем = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
		ОсталосьСписать = ОсталосьСписать - Списываем;

		// опрделеяем себестоимость в зависимости от того,
		// полностью списываем партию или нет	
		Если Списываем = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе
			Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
		КонецЕсли; 

		// списание	
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		Движение.СчетДт = Счета.ПрибылиУбытки;
		Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоДт[Субконто.СостоянияТоваров] = СостояниеХорошее;
		Движение.СчетКт = Счета.Товары;
		Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[Субконто.Партии] = Выборка.Партия;
		Движение.СубконтоКт[Субконто.СостоянияТоваров] = СостояниеХорошее; 
		Движение.КоличествоКт = Списываем;
		Движение.Сумма = Себестоимость;
			
	КонецЦикла; 

	// продажи	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	Движение.СчетДт = Счета.Покупатели;
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
	Движение.СубконтоКт[Субконто.СостоянияТоваров] = СостояниеХорошее;
	Движение.Сумма = ВыборкаНоменклатура.Сумма;
КонецЦикла;
```

### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- по счету 
	- списку номенклатуры
	- по состоянию товаров

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
// отбор по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
// отбор по статусу
ЭлементБлокировки.УстановитьЗначение(Субконто.СостоянияТоваров, СостояниеХорошее);
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	УправленческийОбороты.Субконто1 КАК Номенклатура,
	// УправленческийОбороты.Субконто2 - состояние
	ВЫБОР
		КОГДА УправленческийОбороты.Субконто2 = &СостояниеХорошее
			ТОГДА УправленческийОбороты.КоличествоКорОборот
		ИНАЧЕ 0
	КОНЕЦ КАК КоличествоПродажа,
	УправленческийОбороты.СуммаОборотКт КАК СуммаПродажа,
	ВЫБОР
		КОГДА УправленческийОбороты.Субконто2 = &СостояниеИспорчено
			ТОГДА УправленческийОбороты.КоличествоКорОборот
		ИНАЧЕ 0
	КОНЕЦ КАК КоличествоСписано,
	ВЫБОР
		КОГДА УправленческийОбороты.Субконто2 = &СостояниеИспорчено
			ТОГДА УправленческийОбороты.СуммаОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК СуммаСписано,
	ВЫБОР
		КОГДА УправленческийОбороты.Субконто2 = &СостояниеХорошее
			ТОГДА УправленческийОбороты.СуммаОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК Себестоимость
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(, , , Счет = &СчетПрибылиУбытки, &ВидыСубконто, , КорСчет В (&СписокКорСчетов), ) КАК УправленческийОбороты
```

Чтобы получить нужный отчет:
- Переносим все числовые поля в `ресурсы`
- Выводим отчет:
	- (группировка) Номенклатура
- Выбранные поля: все ресурсы
