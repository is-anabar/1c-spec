# Билет 2 (занятие 48)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задачи следует учесть возможность наличия проводок, сформированных с помощью данного документа.

Необходимо реализовать учет дополнительных затрат, связанных с рекламой продаваемого товара.
Факт передачи товара со склада (отгрузки) покупателю регистрируется документом «Расходная накладная». Данный документ формирует следующие проводки:
- Дт «Прибыли и убытки» - Кт «Товары» на сумму себестоимости.
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму в продажных ценах

Себестоимость определяется как средняя для каждой номенклатурной позиции по всей компании.

Кроме этого в документе «Расходная накладная» заполняется дополнительный реквизит «Проект», т.е. все продажи осуществляются в разрезе проектов (пустым данный реквизит быть не может). При проведении документа (расходная накладная) анализируется наличие этого товара на указанном в шапке документа складе. Если товара недостаточно документ не проводится.

Дополнительные затраты на продажу вводятся в систему с помощью документа «Затраты». В табличной части этого документа указывается проект, и сумма, затраченная на затраты в рамках данного проекта. Документы вводятся по мере поступления затрат в течение дня. Считается, что все затраты должны быть отнесены в счет продаж текущего дня. Контролировать наличие продаж по данному проекту при проведении документа «Затраты» не нужно. 

При проведении документ «Затраты» формируют следующие проводки:
- Дт «Прибыли и убытки» - Кт «Общехозяйственные затраты» на сумму затрат

Необходимо построить отчет о продажах с учетом затрат.

![report](/2%20-%20БУ/media/ticket-2-report.png)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)

## Настройка Виды Субконто

Создаем справочники:
- Склады
- Проекты

Добавляем:
- Номенклатура
- Склады
- Проекты


## Настройка плана счетов

Настройка будет похожа на пункт [Остатки ведутся по какому-то признаку, а себестоимость - по всей компании](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md).

### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Количественный
- Признаки учета субконто:
	- Суммовой

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура 
		- `устанавлиеваем` признак учета субконто
	- Склады 
		- `не устанавлиеваем` признак учета субконто

Прибыли убытки:
- Количественный: `нет`
- Субконто:
	- Номенклатура 
		- `только обороты` 
		- `устанавлиеваем` признак учета субконто
	- Склады 
		- `только обороты` 
		- `устанавлиеваем` признак учета субконто

## Настройка регистра бухгалтерии

Итоговые настройки:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма
		- признак учета субконто: `Суммовой`

## Поступление товаров

Добавляем реквизит `Склад`

Для формирования движений можно воспользоваться конструктором движений. 


## Списание товаров

### Установка флагов 

Очищаем движения (если были) и устанавливаем флаг на запись:
```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Алгоритм запроса будет примерно следующий:
- группировка табличной части по номенклатуре - избавляемся от дублей строк - `втТовары`
- получение информации об остатках:
	- достаем информацию о том, какую номенклатуру, сколько потребовали и за сколько продали в документе
	- достаем из `Управленческий.Остатки` информацию по остаткам на складе (первое соедниение):
		- склад
		- количество на складе
	- достаем из `Управленческий.Остатки` информацию по общим остаткам (второе соединение):
		- количество общее
		- сумма общее

```1c
ВЫБРАТЬ
	// группировка табличной части
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

/////////////////////////////////////////////////

ВЫБРАТЬ
	// данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// данные по остаткам на складе
	// достаем склад, чтобы правильно сработало получение количества
	ОстаткиПоСкладу.Субконто2 КАК Склад,
	ЕСТЬNULL(ОстаткиПоСкладу.КоличествоОстаток, 0) КАК КоличествоОстатокСклад,
	// данные по общим остаткам
	ЕСТЬNULL(ОстаткиОбщие.КоличествоОстаток, 0) КАК КоличествоОстатокОбщее,
	ЕСТЬNULL(ОстаткиОбщие.СуммаОстаток, 0) КАК СуммаОстатокОбщее
ИЗ
	втТовары КАК втТовары
		// соедиянемся с остатками по складу
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконтоСклад, // здесь субконто: номенклатура, склад
				Субконто1 В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					И Субконто2 = &Склад) КАК ОстаткиПоСкладу
		ПО втТовары.Номенклатура = ОстаткиПоСкладу.Субконто1
		// соединяемся по общим остаткам
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконтоОбщее, // здесь субконто: номенклатура
				Субконто1 В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиОбщие
		ПО втТовары.Номенклатура = ОстаткиОбщие.Субконто1
```

### Формирование движений

```1c
Пока Выборка.Следующий() Цикл
	// проверка, что товара хватает на складе
	Разница = Выборка.Количество - Выборка.КоличествоОстатокСклад;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 на складе в количестве %2",
		Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// проверяем, списываем мы всю номенклатуру или только часть	
	Если Выборка.Количество = Выборка.КоличествоОстатокОбщее Тогда
		Себестоимость = Выборка.СуммаОстатокОбщее
	Иначе 
		Себестоимость = Выборка.Количество * Выборка.СуммаОстатокОбщее / Выборка.КоличествоОстатокОбщее; 
	КонецЕсли; 

	// списание
	//	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт 
	Движение.СчетДт = Счета.ПрибылиУбытки;
	Движение.СубконтоДт[Субконто.Проекты] = Проект;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
	// кт
	Движение.СчетКт = Счета.Товары;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоКт[Субконто.Склады] = Выборка.Склад;
	Движение.КоличествоКт = Выборка.Количество;
	// общее
	Движение.Сумма = Себестоимость;  
		

	// продажа
	// 	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт 
	Движение.СчетДт = Счета.Покупатели;
	// кт
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Проекты] = Проект; 
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	// общее 
	Движение.Сумма = Выборка.Сумма;
КонецЦикла;
```


### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- 2 элемента блокировки по `2 левым соединениям`
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- блокировка 1: 
		- счет
		- список номенклатуры; можно достать из документа
		- склад; можно достать из реквизитов
	- блокировка 2: 
		- счет
		- список номенклатуры; можно достать из документа


```1c
Блокировка = Новый БлокировкаДанных;
// 1
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
ЭлементБлокировки.ИсточникДанных = РезультатЗапросаРазложенный;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
ЭлементБлокировки.УстановитьЗначение(Субконто.Склады, Склад); 
// 2
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
ЭлементБлокировки.ИсточникДанных = РезультатЗапросаРазложенный;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
Блокировка.Заблокировать()
```


## Отчет

### Запрос

Берем данные по оборотам `Прибыли убытки`. Разделяем себестоимость и затраты.

```1c
ВЫБРАТЬ
	ВЫРАЗИТЬ(УправленческийОбороты.Субконто1 КАК Справочник.Проекты) КАК Проект,
	ВЫРАЗИТЬ(УправленческийОбороты.Субконто2 КАК Справочник.Номенклатура) КАК Номенклатура,
	УправленческийОбороты.КоличествоКорОборот КАК Количество,
	ВЫБОР
		КОГДА УправленческийОбороты.КорСчет = &СчетТовары
			ТОГДА УправленческийОбороты.СуммаОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК Себестоимость,
	ВЫБОР
		КОГДА УправленческийОбороты.КорСчет = &СчетЗатраты
			ТОГДА УправленческийОбороты.СуммаОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК Затраты,
	УправленческийОбороты.СуммаОборотКт КАК Продажа
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(, , , Счет = &СчетПрибылиУбытки, &ВидыСубконто, , КорСчет В (&СчетТовары, &СчетПокупатели, &СчетЗатраты), ) КАК УправленческийОбороты
```

### Оформление

Чтобы оформить отчет, как надо, создаем:
- (группировка) Проект
	- (группировка) Номенклатура `только иерархия`

Добавляем в ресурсы:
- Количество - сумма по `Номенклатура иерархия`
- Себестоимость -//-
- Продажа -//-
- Затраты - без группировки по суммам; оставляем по умолчанию

> Чтобы выставить номенклатуру `Только иерархия`, необходимо:
> - создать группировку Номенклатура
> - с помощью редактирования изменить тип группировки

