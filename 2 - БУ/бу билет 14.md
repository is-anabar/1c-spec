# Билет 14 (занятие 57)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Необходимо организовать возможность учета сборки комплектов. Сами комплекты определяются в справочнике «Номенклатура» наравне с обычными товарами, услугами. В системе также должна храниться информация о составе комплекта (какие номенклатурные позиции и в каком количестве входят в данный комплект по умолчанию). В состав комплекта не могут входить другие комплекты.

Операция сборки комплекта оформляется в системе с помощью документа «Сборка». В шапке этого документа указывается комплект, склад с которого происходит списание и склад на который приходуется готовая продукция. В табличной части указывается перечень комплектующих для изготовления комплекта. Состав комплекта может изменяться пользователем (меняются комплектующие, их количество). В том случае, когда фактически указанный состав на сборку отличается (по количеству или составу) от справочного состава, то считается, что это нестандартная сборка.

Документ «Сборка» реализует следующие проводки:
- Дт «Основное производство» - Кт «Материалы» на списываемое количество комплектующих и их себестоимость;
- Дт «Товары» - Кт «Основное производство» на собранное количество комплектов и сумму списанных материалов.

Документ не должен проводиться, если соответствующих комплектующих не оказалось в компании. Учет остатков ведется в разрезе складов, себестоимость определяется как средняя по номенклатурной позиции на складе.

Необходимо создать отчет о собранных комплектах.

Сборка комплектов за период с 01.01.2010 но 31.01.2010

|Комплект	|Собрано	|Стандарт	|Нестандарт|
|----------|------------|---------|---|
|Комплект1	|10	|8	|2|
|Комплект2	|16	|12	|4|


## Анализ

Информацию для отчета можно получить со счета `Товары` (т.к. там указывается набор и количество наборов).

Чтобы разделить стандартные и нестандартные наборы, необходимо завести отдельное субконто на счете `Товары`, которое будет хранить информацию о виде набора.


## Виды номенклатуры

Настроить виды номенклатуры можно по [инструкции](оу%20общее.md#filter-items)

У нас будут следующие виды:
- Деталь
- Набор (комплект)
- Услуга


### Настройка номенклатуры

Добавляем реквизит `Тип`

### Регистр сведений для комплектов

Создаем регистр сведений `СоставКомплектов`:
- непериодический
- измерения:
	- Деталь: устанавливаем `связь по типу` Отбор.Тип(Деталь)
	- Комплект: устанавливаем `связь по типу` Отбор.Тип(Комплект)
- ресурсы:
	- Количество


## Настройка Виды Субконто

### Склады

Создаем справочник `Склады` и перечисление `ВидыНаборов` (стандарт и нестандарт)

### Типы субконто

Добавляем:
- Номенклатура
- Склад
- Виды наборов


## Настройка плана счетов

Настройка будет выполняться по схеме [простого учета остатков товаров](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md#простой-учет-остатков-товаров)

### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный

### Настройка счетов

Материалы:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Склад

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Склад
	- Вид набора


## Настройка регистра бухгалтерии

В итоге должно быть:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма:
		- `Балансовый`


## Сборка

Создаем документ:
- Реквизиты:
	- Набор (номенклатура)
	- Количество наборов
	- Склад-отправитель
	- Склад-получатель


При прводении будем выполнять:
1. Стандартное списание товаров
2. Определение, является ли комплект стандартным

Обе действия будут работать со списком деталей из табличной части, поэтому необходимо будет воспользоваться `менеджером времнных таблиц`

### Флаги

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Управленческий.Записать(); 

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

```1c
// данные временной таблицы пригодятся позже,
// при определении, является ли набор стандартным
МВТ = Новый МенеджерВременныхТаблиц;
	
Запрос = Новый Запрос;
// подключаем к запросу, чтобы запомнить временную таблицу
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	СборкаОсновная.Деталь КАК Деталь,
		// в таблице указываем количество на 1 комплект;
		// для опрделения стандарт или нет
	|	СУММА(СборкаОсновная.Количество) КАК Количество,
		// итоговое количество деталей - для проверки остатков
	|	СУММА(СборкаОсновная.Количество * &КоличестоНаборов) КАК КоличествоДеталей
	|ПОМЕСТИТЬ втДетали
	|ИЗ
	|	Документ.Сборка.Основная КАК СборкаОсновная
	|ГДЕ
	|	СборкаОсновная.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СборкаОсновная.Деталь
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Деталь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
		// данные из документа
	|	втДетали.Деталь КАК Деталь,
	|	втДетали.Деталь.Представление КАК ДетальПредставление,
	|	втДетали.КоличествоДеталей КАК КоличествоДеталей,
		// данные по остаткам
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	|ИЗ
	|	втДетали КАК втДетали
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&МоментВремени,
	|				Счет = &СчетМатериалы,
	|				&ВидыСубконто,
	|				Субконто1 В // отбор по деталям
	|						(ВЫБРАТЬ
	|							втДетали.Деталь КАК Деталь
	|						ИЗ
	|							втДетали КАК втДетали)
						// отбор по складу
	|					И Субконто2 = &СкладОтправитель) КАК УправленческийОстатки
	|		ПО втДетали.Деталь = УправленческийОстатки.Субконто1";
```

### Проверка набора

Для проверки:
- получаем данные по текущему набору из `СоставНаборов`
- с помощью `полного соединения` объединяемся с `втДетали` (виртаульная таблица из запроса на проверку остатков)
	- выводим любую информацию для тех записей, у которых количество в документе не совпадает с составом в регистре

> Полное соединение используем, т.к. может быть ситуация, когда в составе набора 3 детали, а мы указали в сборке только 2. С помощью полного соединения информация о таких деталях не пропадает

```1c
ЭтоСтандартныйНабор = Ложь;
	
Запрос = Новый Запрос;
// получаем данные по деталям из запроса по остаткам - втДетали 
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоставНаборов.Деталь КАК Деталь,
	|	СоставНаборов.Количество КАК Количество
	|ПОМЕСТИТЬ втСоставНабора
	|ИЗ
	|	РегистрСведений.СоставНаборов КАК СоставНаборов
	|ГДЕ
	|	СоставНаборов.Набор = &Набор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Деталь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК Запись // выводим любую информацию - она не важна
	|ИЗ
	|	втДетали КАК втДетали	// полное соединение
	|		ПОЛНОЕ СОЕДИНЕНИЕ втСоставНабора КАК втСоставНабора
	|		ПО втДетали.Деталь = втСоставНабора.Деталь
	|ГДЕ
		// т.к. соединение полное, то проверку на null нужно делать для обеих сторон.
	|	ЕСТЬNULL(втДетали.Количество, 0) <> ЕСТЬNULL(втСоставНабора.Количество, 0)";
	
Запрос.УстановитьПараметр("Набор", Набор);
	
РезультатЗапроса = Запрос.Выполнить();
// если набор пустой, то мы собираем стандартный набор
Если РезультатЗапроса.Пустой() Тогда
	ЭтоСтандартныйНабор = Истина;
КонецЕсли;
	
Возврат ЭтоСтандартныйНабор;
```

### Формирование движений

```1c
// для рассчета суммы комплекта по деталям
ИтоговаяСуммаКомплекта = 0; 
ЭтоСтандартныйНабор = ЭтоСтандартныйНабор(Набор, МВТ);
	
Пока Выборка.Следующий() Цикл
	// проверка остатков на складе
	Разница = Выборка.КоличествоДеталей - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда 
		Отказ = Истина;  
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно деталей %1 в количестве %2.",
			Выборка.ДетальПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// определение себестоимости в зависимости от того, 
	// списываем все детали со склада или нет	
	Если Выборка.КоличествоДеталей = Выборка.КоличествоОстаток Тогда
		Себестоимость = Выборка.СуммаОстаток;
	Иначе
		Себестоимость = Выборка.КоличествоДеталей * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
	КонецЕсли;
		
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт 
	Движение.СчетДт = Счета.ОсновноеПроизводство;
	// кт 
	Движение.СчетКт = Счета.Материалы;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Деталь;
	Движение.СубконтоКт[Субконто.Склады] = СкладОтправитель;
	Движение.КоличествоКт = Выборка.КоличествоДеталей; 
	// общее
	Движение.Сумма = Себестоимость;

	// обновляем стоимость комплекта	
	ИтоговаяСуммаКомплекта = ИтоговаяСуммаКомплекта + Движение.Сумма;
	КонецЦикла;  
	
ВидНабора = ?(ЭтоСтандартныйНабор, Перечисления.ВидыНаборов.Стандарт, Перечисления.ВидыНаборов.Нестандарт);
	
Движение = Движения.Управленческий.Добавить();
Движение.Период = Дата;
// дт
Движение.СчетДт = Счета.Товары;
Движение.СубконтоДт[Субконто.Номенклатура] = Набор;
Движение.СубконтоДт[Субконто.Склады] = СкладПолучатель;
Движение.СубконтоДт[Субконто.ВидыНаборов] = ВидНабора;
Движение.КоличествоДт = КоличествоНаборов;
// кт
Движение.СчетКт = Счета.ОсновноеПроизводство;
// общее
Движение.Сумма = ИтоговаяСуммаКомплекта;
```

### Блокировка

### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

Блокируем данные по остакам (для левого соединения):
- по счету
- по складу
- по списку деталей

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Материалы);
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = Основная;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Деталь");
// блокировка по складу
ЭлементБлокировки.УстановитьЗначение(Субконто.Склады, СкладОтправитель);
Блокировка.Заблокировать();
```


## Отчет

```1c
ВЫБРАТЬ
	УправленческийОборотыДтКт.СубконтоДт1 КАК Набор,
	УправленческийОборотыДтКт.КоличествоОборотДт КАК Количество,
	ВЫБОР
		КОГДА УправленческийОборотыДтКт.СубконтоДт3 = &Стандарт
			ТОГДА УправленческийОборотыДтКт.КоличествоОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК КоличествоСтандарт,
	ВЫБОР
		КОГДА УправленческийОборотыДтКт.СубконтоДт3 = &Нестандарт
			ТОГДА УправленческийОборотыДтКт.КоличествоОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК КоличествоНестандарт
ИЗ
	РегистрБухгалтерии.Управленческий.ОборотыДтКт(&НачалоПериода, &КонецПериода, , СчетДт = &СчетТовары, &ВидыСубконтоТовары, СчетКт = &СчетОсновноеПроизводство, , ) КАК УправленческийОборотыДтКт
```


Чтобы получить нужный отчет, необходимо:
- Перенести в ресурсы:
	- Количество
	- КоличествоСтандарт
	- КоличествоНестандарт
- Создать отчет:
	- (группировка) Набор
- Перенести все ресурсы в выбранные поля
