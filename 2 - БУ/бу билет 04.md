# Билет 4 (занятия 53)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Необходимо реализовать возможность ведения учета товаров в разрезе организаций, мест хранения и партий товаров. Подразумевается, что для каждой организации баланс будет формировать отдельно (каждая организация – это отдельное юридическое лицо). Склады у организаций «общие» (привязки склада к конкретной организации нет, но товар может принадлежать только одной организации). Под партией товара понимается документ, регистрирующий его поступление.

Документ «Приходная накладная» реализует следующую проводку:
- Дт «Товары» - Km «Поставщики» на количество и сумму закупаемого товара.

В системе необходимо реализовать документ, который осуществлял бы перемещение товара между собственными организациями (с возможным изменением склада компании). Фактически подобное перемещение будет регистрировать сразу две операции: продажа товара поставщиком покупателю и покупка покупателем товара у поставщика. Регистрация этих операций будет осуществляться документом «КупляПродажа». В шапке документа (при его заполнении) указывается организация-покупатель, организация-поставщик, склад организации-покупателя и склад организации-поставщика.

Документ «КупляПродажа» реализует следующие проводки:
По организации-поставщику:
- Дт «Прибыли и убытки» - Km «Товары» на количество сумму себестоимости. Себестоимость товара рассчитывается для каждой организации в разрезе партий и складов по каждой номенклатурной позиции.
- Дт «Покупатели» - Km «Прибыли и убытки» на сумму в продажных ценах.

По организации-покупателю: 
- Дт «Товары» - Km «Поставщики» на сумму закупаемого товара.

Необходимо сформировать отчет позволяющий получать информацию о суммах продаж перемещаемого между организациями товара.

![report](/2%20-%20БУ/media/ticket-4-report.png)


## Анализ


### Организация как измерение

В каждой проводке будет организация, которая получает (для приходной накладной) или отправляет (для купли-продажи) на счет `Товары` номенклатуру. По условию у нас уже есть как минимум 3 субконто для счета `Товары`:
- Номенклатура
- Склады
- Партии

Нельзя впихнуть 4ым субконто организации. В данной случае, т.к. организация есть в каждой проводке, мы добавляем ее в `измерения`.

### Организация как субконт

Информацию об отчете можно получить с помощью проводки `Прибыли убытки - Продажи`. 


Из измерении `организация` можно получить организацию-отправителя. Чтобы получить информацию об организации-получателе, необходимо добавить в один из счетов субконто `Организация` (оборотный).

Если добавить на счет `Прибыли убытки`, то сумма будет больше актуальной:
- Товары - Прибыли убытки (сумма по себестоимости)
- Прибыли убытки - Продажи (сумма по продаже)

Поэтому необходимо добавить `оборотный` субконто на счет `Продажи`


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Настройка Виды Субконто

Создаем справочники:
- Организации
- Склады


### Типы субконто

Добавляем:
- Номенклатура
- Склад
- Партия
	- Приходная накладная
	- Купля-продажа
- Организация (вспомогательный - для отчета)


## Настройка плана счетов

Настройка будет выполняться по схеме [Простой учет остатков товаров](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md#простой-учет-остатков-товаров)


### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Склад
	- Партии

Покупатели:
- Количественный: `нет`
- Субконто: 
	- Организация: `только обороты`


## Настройка регистра бухгалтерии

В итоге должно быть:
- Измерения:
	- Организация
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма:
		- `Балансовый`


## Приходная накладаная

Добавляем реквизиты:
- Организация
- Склад


Движения можно сделать с помощью конструктора движений.


## Расходная накладная

Добавляем реквизиты:
- Организация-отправитель
- Организация-получатель
- Склад-отправитель
- Склад-получатель


### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Управленческий.Записать(); 

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```


### Запрос

```1c
ВЫБРАТЬ
	// группировка табличной части
	КупляПродажаСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(КупляПродажаСписокНоменклатуры.Количество) КАК Количество,
	СУММА(КупляПродажаСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.КупляПродажа.СписокНоменклатуры КАК КупляПродажаСписокНоменклатуры
ГДЕ
	КупляПродажаСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	КупляПродажаСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// данные по остаткам
	УправленческийОстатки.Субконто3 КАК Партия,
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				Организация = &ОрганизацияОтправитель // блокировка по организации
					И Субконто1 В // по номенклатуре
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					И Субконто2 = &СкладОтправитель // по складу
			) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1
ИТОГИ
	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма),
	СУММА(КоличествоОстаток)
ПО
	Номенклатура
```

### Формирование движений


```1c
Пока ВыборкаНоменклатура.Следующий() Цикл
	// проверка, что на складе хватает номенклатуры
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("На складе-отправителе недостаточно товара %1 в количестве %2",
			ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить(); 
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// запоминаем, сколько требуется списать номенклатуры	
	ТребуетсяСписать = ВыборкаНоменклатура.Количество;
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
		// определяем, сколько можем списать с текущей партии
		Списываем = Мин(ТребуетсяСписать, Выборка.КоличествоОстаток);
		// обновляем требуемое количество
		ТребуетсяСписать = ТребуетсяСписать - Списываем;

		// рассчитываем себестоимость в зависимости от того, 
		// списываем всю номенклатуру из текущей партии или только часть	
		Если Списываем = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе 
			Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
		КонецЕсли; 

		// списание
		// отдаем организация-отправитель	
		//
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		Движение.Организация = ОрганизацияОтправитель;
		// дт
		Движение.СчетДт = Счета.ПрибылиУбытки;
		// кт 
		Движение.СчетКт = Счета.Товары;
		Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[Субконто.Склады] = СкладОтправитель;
		Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Партия;
		Движение.КоличествоКт = Списываем;
		// общее 
		Движение.Сумма = Себестоимость;
	КонецЦикла;

	// продажа
	// продает организация-отправитель
	// полкупает организация-получатель
	//	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	Движение.Организация = ОрганизацияОтправитель; // отправитель как измерение
	// дт
	Движение.СчетДт = Счета.Покупатели;
	// покупатель как субконто (справочное)
	Движение.СубконтоДт[Субконто.Организация] = ОрганизацияПолучатель;
    // кт
	Движение.СчетКт = Счета.ПрибылиУбытки;
	// общее
	Движение.Сумма = ВыборкаНоменклатура.Сумма;

	// покупка
	// покупает организация-получатель
	// 	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата; 
	Движение.Организация = ОрганизацияПолучатель;
	// дт
	Движение.СчетДт = Счета.Товары;
	Движение.СубконтоДт[Субконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
	Движение.СубконтоДт[Субконто.Склады] = СкладПолучатель;
	Движение.СубконтоДт[Субконто.Партии] = Ссылка; 
	Движение.КоличествоДт = ВыборкаНоменклатура.Количество;
	// кт
	Движение.СчетКт = Счета.Поставщики;
	// общее
	Движение.Сумма = ВыборкаНоменклатура.Сумма;
КонецЦикла;
```

### Блокировка



Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- счет
	- организация; берем из реквизитов - организация-отправитель
	- склад; берем из реквизитов - склад-отправитель
	- номенклатура; можно взять из ТЧ


```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары); 
// по организации
ЭлементБлокировки.УстановитьЗначение("Организация", ОрганизацияОтправитель);
// по складу
ЭлементБлокировки.УстановитьЗначение(Субконто.Склады, СкладОтправитель);
// по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
Блокировка.Заблокировать();
```

## Отчет

Чтобы получить отчет, нужно:
1. Соединить справочник Организации (отправители) с Организации (получатели)
2. Для пар отправитель-получатель достать сумму из оборотов

```1c
ВЫБРАТЬ
	Отправители.Ссылка КАК Отправитель,
	Получатели.Ссылка КАК Получатели
ПОМЕСТИТЬ втПересечения
ИЗ	// соединяем справочник с самим собой для получения всех пар
	Справочник.Организации КАК Отправители
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Получатели
		ПО (ИСТИНА)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втПересечения.Отправитель КАК Отправитель,
	втПересечения.Получатели КАК Получатели,
	УправленческийОборотыДтКт.СуммаОборот КАК Сумма
ИЗ
	втПересечения КАК втПересечения
		// берем ОборотыДтКт, т.к. указана конкретная проводка
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(
			&НачалоПериода, 
			&КонецПериода, 
			, 
			СчетДт = &СчетПокупатели, 
			&ВидыСубконто, 
			СчетКт = &СчетПрибылиУбытки, 
			, 
		) КАК УправленческийОборотыДтКт
		// соединяемся по отправителю
		ПО втПересечения.Отправитель = УправленческийОборотыДтКт.Организация 
			// по получателю
			И втПересечения.Получатели = УправленческийОборотыДтКт.СубконтоДт1
```

Для вывода отчета:
- На вкладке реквизиты переносим:
	- Сумма
- Таблица
	- Строки 
		- (группировка) Отправитель
	- Колонки
		- (группировка) Получатель
- Выбранные поля:
	- Сумма
