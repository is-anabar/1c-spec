# Билет 5 (занятие 47)

Необходимо реализовать возможность закупки и продажи редких товаров. Поступление таких товаров осуществляется документом «Приходная накладная». В документе поступления каждая единица товара оформляется отдельной строкой (с количеством равным 1). Каждой позиции закупаемого товара присваивается уникальный инвентарный номер (считается, что за уникальностью следит пользователь, автоматизировать получение уникальных инвентарных номеров в рамках задачи не требуется). Одним документом может оформляться поступление нескольких одинаковых товаров, но с разными инвентарными номерами.

Документ «Приходная накладная» реализует следующую проводку:
- Дт «Товары» - Kт «Поставщики» на сумму закупаемого товара

Продажа товара регистрируется документом «Расходная накладная». При продаже инвентарный номер вводится в табличную часть документа вручную. При проведении документа должен производиться контроль наличия указанного в документе товара (по указанному инвентарному номеру). Себестоимость списываемого товара определяется как средняя по номенклатурной позиции по всем ее инвентарным номерам.

Документ «Расходная накладная» реализует следующие проводки:
- Дт «Прибыли и убытки» - Kт «Товары» на сумму себестоимости;
- Дт «Покупатели» - Kт «Прибыли и убытки» на сумму в продажных ценах.

По данным бухгалтерского учета необходимо сформировать отчет, который за указанный интервал дат показывал бы данные о проданном товаре.

Продажи за период с 01 01.2010 по 31.01.2010
|Товар	|Инв. номер	|Себестоимость	|Продажа|
|-------|-----------|---------------|-------|
|Скрипка	|0001	|120 000	|200 000|
|Барабан	|0002	|51 000	|70 000|
|Барабан	|5008	|51 000	|75 890|


## Настройка Виды Субконто

### Типы субконто

Добавляем:
- Номенклатура
- Инвентарный номер (строка)


## Настройка плана счетов

<!-- настройка 
0 - общее\05 как заполнить регистр бухгалтерии.md
Остатки ведутся по какому-то признаку, а себестоимость - по всей компании -->

### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Количественный
- Признаки учета субконто:
    - Суммовой

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура 
        - `устанавлиеваем` признак учета субконто
	- Инвентарные номера 
        - `не устанавлиеваем` признак учета субконто

Прибыли убытки (используется как аналог регистра для продаж):
- Количественный: `нет`
- Субконто:
	- Номенклатура 
        - только обороты 
        - `устанавлиеваем` признак учета субконто
	- Инвентарные номера 
        - только обороты 
        - `не устанавлиеваем` признак учета субконто


## Настройка регистра бухгалтерии

Необходимо выполнить [базовые настройки](/0%20-%20общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

Дополнительно:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
    - Сумма
        - признак учета субконто: `Суммовой`


### Списание товаров


1. Очищаем движения (если были) и устанавливаем флаг на запись:
	```1c
	// очищаем движения - записываем пустые
	Движения.Управленческий.Записывать = Истина;
	Движения.Записать();
	
	// устанавливаем флажок, чтобы новые движения записывались
	Движения.Управленческий.Записывать = Истина;
	```

2. Формируем запрос

	Алгоритм запроса будет примерно следующий:
	- группировка табличной части по номенклатуре - избавляемся от дублей строк - `втТовары`
	- получение информации об остатках:
		- достаем информацию о том, какую номенклатуру, сколько потребовали и за сколько продали в документе
		- достаем из `Управленческий.Остатки` склад, партию, остаток на складах, сумму товаров, соединяясь по:
			- отбор в виртуальной таблице: список номенклатуры + склад
			- левое соединение по номенклатуре
		- итоги по количеству (сколько требования), остатку (сколько есть всего на складе) и сумму (для проводки по продаже)
		- сортируем в зависимости от учетной политики

	Итоговый запрос примерно такой: 
	```1c
	// группировка ТЧ
	ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.ИнвентарныйНомер КАК ИнвентарныйНомер,
	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество,
	РасходнаяНакладнаяСписокНоменклатуры.Сумма КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
    // из-за наличия субконто выводится остаток по номенклатуре
    // здесь будет 1 или 0 (исходя из условия)
	ЕСТЬNULL(УправленческийОстаткиПоИнвНомеру.Субконто2, 0) КАК ИнвНомер,
	ЕСТЬNULL(УправленческийОстаткиПоИнвНомеру.КоличествоОстаток, 0) КАК КоличествоОстатокПоИнвНомеру,
    // получаем общий остаток и общую сумму
	ЕСТЬNULL(УправленческийОстаткиОбщие.КоличествоОстаток, 0) КАК КоличествоОстатокОбщее,
	ЕСТЬNULL(УправленческийОстаткиОбщие.СуммаОстаток, 0) КАК СуммаОстатокОбщее
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				Субконто1 В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК УправленческийОстаткиОбщие
		ПО втТовары.Номенклатура = УправленческийОстаткиОбщие.Субконто1
        // соединяемся по инвентарному номеру
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				(Субконто1, Субконто2) В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура,
						втТовары.ИнвентарныйНомер КАК ИнвентарныйНомер
					ИЗ
						втТовары КАК втТовары)) КАК УправленческийОстаткиПоИнвНомеру
		ПО втТовары.Номенклатура = УправленческийОстаткиПоИнвНомеру.Субконто1
	```

	Параметры для запроса такие:
	```1c
	// установка видов субконто - обязательно, иначе снимут баллы
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныеНомера);
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	// остальные - неинтересные
	```

3. Списание товаров

	```1c
	Пока Выборка.Следующий() Цикл 
        // проверка, что есть номенклатура с текущий инв. номером
		Разница = Выборка.Количество - Выборка.КоличествоОстатокПоИнвНомеру; 
		Если Разница > 0 Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			Выборка.НоменклатураПредставление, Разница);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли; 
		
        // здесь нет проверки на последнее списание, 
        // т.к. Выборка.Количество = 1, и списываем мы тоже 1
		Себестоимость = Выборка.Количество * Выборка.СуммаОстатокОбщее / Выборка.КоличествоОстатокОбщее;
		
		// списание товара
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата; 
		// дт
		Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки; 
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныйНомер] = Выборка.ИнвНомер;
		// кт
		Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныйНомер] = Выборка.ИнвНомер;
		Движение.КоличествоКт = Выборка.Количество;  
		// общее 
		Движение.Сумма = Себестоимость;	
		
		// продажа
		// списание товара
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата; 
		// дт
		Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		// кт
		Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныйНомер] = Выборка.ИнвНомер; 
		// общее
		Движение.Сумма = Выборка.Сумма;
	КонецЦикла;
	```

5. Устанавливаем блокировку

	Блокировка устанавливается `ПЕРЕД` запросом. 

	На основе запроса делаем блокировку:
	- 2 элемента блокировки, т.к. `2 левых соединения`
	- объект блокировки: регистр бухгалтерии `Управленческий`
	- отборы блокировки:
		- блокировка 1: 
			- счет
			- список номенклатуры; можно достать из документа
		- блокировка 2: 
			- счет
			- список номенклатуры; можно достать из документа
			- список инвентарных номеров; можно достать из документа

	```1c
	// блокировка 
	Блокировка = Новый БлокировкаДанных; 
	// общие остатки
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный; 
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	// остатки по инв. номеру
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.ИнвентарныйНомер, "ИнвентарныйНомер");
	Блокировка.Заблокировать();
	```


## Отчет

```1c
ВЫБРАТЬ
	УправленческийОбороты.Субконто1 КАК Номенклатура,
	УправленческийОбороты.СуммаОборотДт КАК Себестоимость,
	УправленческийОбороты.СуммаОборотКт КАК СуммаПродажи,
	УправленческийОбороты.КоличествоКорОборот КАК Количество
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, , Счет = &СчетПрибылиУбытки, &ВидыСубконто, , КорСчет В (&СпискоКорСчетов), ) КАК УправленческийОбороты
```

Параметры:
```1c
&ВидыСубконто - Номенклатура
&СпискоКорСчетов - Товары, Покупатели
```




