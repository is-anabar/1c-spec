# Билет дополнительный 3

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задачи следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная Накладная», продажа - «Расходная накладная».

Учет товаров ведется в разрезе партий и сроков годности. У каждого товара указывается максимальный срок годности, который не может подвергаться изменению, например, максимальный срок годности сосисок составляет 10 дней. Сам срок годности отслеживается от момента поступления товара. Например, если сосиски поступили 1 января, то через 10 дней – с 11 января они считаются просроченными 
 
Документ «Расходная накладная» реализует следующие проводки:
Дт «Товары» - Кт «Поставщики» на количество и сумму закупаемого товара.
 
Списание себестоимости товара должно быть организованно по партиям, по методу FIFO, или по Средней. Кроме того, должен происходить контроль срока годности, если срок годности больше даты документа, то данный товар продаваться не может.
Значение учетной политики может меняться не чаще, чем 1 раз в год. Следует считать, что пользователь вручную самостоятельно корректно будет устанавливать учетную политику на соответствующий период.

Про проведении документа необходимо использовать метод, актуальный на момент проведения документа.

В пользовательском режиме необходимо реализовать два примера ввода и проведения документов:
- Переход с «партийного» учета на «среднюю»
- Переход со «средней» себестоимости на использования метода FIFO
 
Документ «Расходная накладная» реализует следующие проводки:
- Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму в продажных ценах.

Товар с истекшим сроком годности списывается с помощью документа «Операция» проводкой:
- Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости

Необходимо построить отчет по продажам товаров за период.

![report](/2%20-%20БУ/media/ticket-extra-3-report.png)


*Существует еще одна разновидность этой задачи. Отличия следующие: в документе «Приходная накладная» сразу указывается срок годности для поступающего товара, методы списания – FIFO и LIFO.



# Вариант 1 (FIFO и средняя)

## Анализ

Здесь не нужно создавать признак учета субконто для отделения партий и сроков годности, т.к. не может быть ситуации, вроде
- Сосиски №5 - Партия 1 - до 20.11.2025 - 15 шт.
- Сосиски №5 - Партия 1 - до 23.11.2025 - 15 шт.

Для одинаковой номенклатуры будет одинаковый срок годности в рамках одной партии, так как он зависит от даты документа (непосредственно партии).

Срок годности здесь скорее выступает как вспомогательная информация.


## Учетная политика

Инструкция по созданию учетной политики [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Настройка Виды Субконто


### Типы субконто

Добавляем:
- Номенклатура
- Партии (приходная накладная)
- Сроки годности


## Настройка плана счетов

<!-- настройка 
0 - общее\05 как заполнить регистр бухгалтерии.md
Остатки ведутся по одному признаку, а себестоимость рассчитывается по другому признаку -->


В анализе говорилось о том, почему здесь можно использовать [просто учет остатков](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md#простой-учет-остатков-товаров).

### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный

### Настройка счетов

Товары (используется как аналог регистра по остаткам):
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Партия
	- Срок годности


Прибыли убытки (используется как аналог регистра для продаж):
- Количественный: `нет`
- Субконто:
	- Номенклатура
		- только обороты
	- Срок годности (для того, чтобы различать продажу и списание негодных продуктов)
		- только обороты


## Настройка регистра бухгалтерии

Необходимо выполнить [базовые настройки](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

В итоге должно быть:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма (доработки):
		- `Балансовый`

## Приходная накладная

### Учетная политика

Необходимо проверить, что пользователь задал учетную политику

### Формирование движений

Предварительно необходимо рассчитать сроки годности для каждого товара. Лучше всего это сделать в запросе:

```1c
ВЫБРАТЬ
	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
	МАКСИМУМ(
		ДОБАВИТЬКДАТЕ(
			&КонецДня, // можно и начало дня - без разницы
			ДЕНЬ, 
			ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура.СрокГодностиДней
		)
	) КАК СрокГодности
ИЗ
	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
ГДЕ
	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура
```

Далее можно просто записывать движения.


## Расходная накладная

### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```


### Запрос

```1c
// группируем данные из документа
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// получаем данные по остаткам
	УправленческийОстатки.Субконто2 КАК Партия,
	УправленческийОстатки.Субконто3 КАК СрокГодности,
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				Субконто1 В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
						// остатки с учетом срока годности
					И (ВЫРАЗИТЬ(Субконто3 КАК ДАТА)) > &НачалоДня) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1
ИТОГИ
	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма),
	СУММА(КоличествоОстаток)
ПО
	Номенклатура
```

### Формирование движений

```1c
ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
Пока ВыборкаНоменклатура.Следующий() Цикл
	// проверка остаток по сроку годности
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 по срокам годности.",
			ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда 
		Продолжить;
	КонецЕсли;

	// запоминаем, сколько номенклатуры нужно в=списать	
	ТребуетсяСписать = ВыборкаНоменклатура.Количество;
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
		Списываем = Мин(ТребуетсяСписать, Выборка.КоличествоОстаток);
		ТребуетсяСписать = ТребуетсяСписать - Списываем;

		// определяем, мы списываем партию полностью или нет	
		Если Списываем = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе
			Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток; 
		КонецЕсли;
			
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		// дт
		Движение.СчетДт = Счета.ПрибылиУбытки;
		Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
		// не заполняем срок годности - его заполняем только
		// в документе Операция при списании просроченных
		// кт
		Движение.СчетКт = Счета.Товары;
		Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[Субконто.Партии] = Выборка.Партия;
		Движение.СубконтоКт[Субконто.СрокиГодности] = Выборка.СрокГодности;
		Движение.КоличествоКт = Списываем;
		// общее 
		Движение.Сумма = Себестоимость;
	КонецЦикла; 
		
	// продажи
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.Покупатели;
	// кт
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
	// общее
	Движение.Сумма = ВыборкаНоменклатура.Сумма;
КонецЦикла;
```


### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

Блокируем данные по остакам (для левого соединения):
- по счету
- по списку номенклатуры
- по периоду (сроки годности)

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
// блокировка по периоду
ЭлементБлокировки.УстановитьЗначение("Период", Новый Диапазон( , НачалоДня));
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлеентБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	УправленческийОбороты.Счет КАК Счет,
	УправленческийОбороты.Субконто1 КАК Номенклатура,
	ВЫБОР
		КОГДА НЕ (ВЫРАЗИТЬ(УправленческийОбороты.Субконто2 КАК ДАТА)) = &ПустаяДата
			ТОГДА УправленческийОбороты.КоличествоКорОборот
		ИНАЧЕ 0
	КОНЕЦ КАК КоличествоСписано,
	ВЫБОР
		КОГДА НЕ (ВЫРАЗИТЬ(УправленческийОбороты.Субконто2 КАК ДАТА)) = &ПустаяДата
			ТОГДА УправленческийОбороты.СуммаОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК СуммаСписано,
	ВЫБОР
		КОГДА (ВЫРАЗИТЬ(УправленческийОбороты.Субконто2 КАК ДАТА)) = &ПустаяДата
			ТОГДА УправленческийОбороты.СуммаОборотДт
		ИНАЧЕ 0
	КОНЕЦ КАК Себестоимость,
	ВЫБОР
		КОГДА (ВЫРАЗИТЬ(УправленческийОбороты.Субконто2 КАК ДАТА)) = &ПустаяДата
			ТОГДА УправленческийОбороты.КоличествоКорОборот
		ИНАЧЕ 0
	КОНЕЦ КАК КоличествоПродано,
	УправленческийОбороты.СуммаОборотКт КАК СуммаПродано
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, , Счет = &СчетПрибылиУбытки, &ВидыСубконто, , КорСчет В (&СчетТовары, &СчетПокупатели), ) КАК УправленческийОбороты
```
