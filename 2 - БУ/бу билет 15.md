# Билет 15 (занятия 42-43, 44)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность вести проводки с произвольной корреспонденцией счетов. При решении задачи следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Требуется реализовать возможность ведения учета товаров в разрезе мест хранения и партий товаров. Под партией товара понимается документ, регистрирующий его (товара) поступление.

В документе «Приходная накладная» склад указывается в табличной части для каждой строки и формируются следующие проводки:
- `Дт «Товары» - Кт «Поставщики» на количество и сумму закупаемого товара`

Продажа товара производится со склада и регистрируется документом «Расходная накладная», cклад указывается в реквизите самого документа. При заполнении документа партия товара не указывается. При проведении производится проверка достаточности количества на указанном складе продаваемого товара.

Документ «Расходная накладная» реализует следующие проводки:
- `Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости`
- `Дт «Покупатели» - Кт «Прибыли и убытки» на сумму в продажных ценах`

При продаже товара списание партий этого товара производится либо по дисциплине LIFO («последним пришел, первым ушел»), либо FIFO («первым пришел, первым ушел»). Какая из дисциплин будет использоваться, определяется в начале года и в течение года не меняется.

Себестоимость товара рассчитывается в разрезе партий на складе для данного товара. 

Используя план видов характеристик необходимо предоставить возможность пользователю добавить произвольное количество дополнительных характеристик к каждому товару (материал, цвет, страна-производитель и т.д.) 

Необходимо построить отчет о продажах товаров. Дополнительно данные в отчете должны быть сгруппированы по выбранной характеристике.

![report](/2%20-%20БУ/media/ticket-15-report.png)


## Учетная политика

Можно прочитать [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md)


## Свойства объектов

Можно прочитать [здесь](/0%20-%20общее/03%20как%20добавить%20характеристики%20объектов.md)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)



## Настройка Виды Субконто

### Склады

Создаем справочник `Склады`.

### Типы субконто

Добавляем:
- Номенклатура
- Склад
- Партия: Приходная накладная


## Настройка плана счетов

<!-- настройка 
0 - общее\05 как заполнить регистр бухгалтерии.md
Простой учет остатков товаров -->

### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный

### Настройка счетов

Товары (используется как аналог регистра по остаткам):
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Склад
	- Партия

Прибыли убытки (используется как аналог регистра для продаж):
- Количественный: `нет`
- Субконто:
	- Номенклатура: `только обороты`


## Настройка регистра бухгалтерии

Необходимо выполнить [базовые настройки](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

Дополнительно:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`


## Приходная накладная

Добавляем реквизит `Склад`.

Для формирования движений можно воспользоваться конструктором движений.

## Расходная накладная

Добавляем реквизит `Склад`.

### Списание товаров

1. Определеяем учетную политику

	С помощью запроса получаем учетную политику. Если она не установлена, то отказываемся от проведения.
	```1c
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетнаяПолитикиСрезПоследних.УчетнаяПолитика КАК УчетнаяПолитика
		|ИЗ
		|	РегистрСведений.УчетнаяПолитики.СрезПоследних(&МоментВремени, ) КАК УчетнаяПолитикиСрезПоследних";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	РезультатЗапроса = Запрос.Выполнить(); 
	
	Если РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задана учетная политика.";
		Сообщение.Сообщить();  
	КонецЕсли;
	
	// будет только одна запись, поэтому можно без
	// Пока Выборка.Следующий()...
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	УчетнаяПолитика = Выборка.УчетнаяПолитика;
	```

2. Очищаем движения (если были) и устанавливаем флаг на запись:
	```1c
	// очищаем движения - записываем пустые
	Движения.Управленческий.Записывать = Истина;
	Движения.Записать();
	
	// устанавливаем флажок, чтобы новые движения записывались
	Движения.Управленческий.Записывать = Истина;
	```

3. Формируем запрос

	Алгоритм запроса будет примерно следующий:
	- группировка табличной части по номенклатуре - избавляемся от дублей строк - `втТовары`
	- получение информации об остатках:
		- достаем информацию о том, какую номенклатуру, сколько потребовали и за сколько продали в документе
		- достаем из `Управленческий.Остатки` склад, партию, остаток на складах, сумму товаров, соединяясь по:
			- отбор в виртуальной таблице: список номенклатуры + склад
			- левое соединение по номенклатуре
		- итоги по количеству (сколько требования), остатку (сколько есть всего на складе) и сумму (для проводки по продаже)
		- сортируем в зависимости от учетной политики

	Итоговый запрос примерно такой: 
	```1c
	// группировка ТЧ
	ВЫБРАТЬ
		РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		// сумма пойдет в проводку с информацией о продажах
		СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	ПОМЕСТИТЬ втТовары
	ИЗ
		Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	ГДЕ
		РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

	СГРУППИРОВАТЬ ПО
		РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

	ИНДЕКСИРОВАТЬ ПО
		Номенклатура
	;

	////////////////////////////////////////////////////////////////////////////////
	ВЫБРАТЬ
		// информация из документа
		втТовары.Номенклатура КАК Номенклатура,
		втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		втТовары.Количество КАК Количество,
		втТовары.Сумма КАК Сумма,
		// информация из регистра бухгалтерии
		//используется для проводки-списания
		УправленческийОстатки.Субконто2 КАК Склад,
		УправленческийОстатки.Субконто3 КАК Партия,
		ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	ИЗ
		втТовары КАК втТовары
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
					&МоментВремени,
					Счет = &СчетТовары,
					&ВидыСубконто,
					Субконто1 В
							(ВЫБРАТЬ
								втТовары.Номенклатура КАК Номенклатура
							ИЗ
								втТовары КАК втТовары)
						И Субконто2 = &Склад) КАК УправленческийОстатки
			ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1

	// т.к. обращаемся к полю субконто через точку, нужно выразить тип явно
	УПОРЯДОЧИТЬ ПО
		ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени
	ИТОГИ
		// количество и сумма - информация их документа
		МАКСИМУМ(Количество),
		МАКСИМУМ(Сумма),
		// количество остаток - количество в партиях
		СУММА(КоличествоОстаток)
	ПО
		Номенклатура
	```

	Параметры для хапроса такие:
	```1c
	// сортировка в зависимости от учетной политики
	Если УчетнаяПолитика = Перечисления.УчетнаяПолитика.ЛИФО Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, 
		"ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени",
		"ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени УБЫВ");
	КонецЕсли;
	
	// установка видов субконто - обязательно, иначе снимут баллы
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Партии);
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	// остальные - неинтересные
	```

4. Списание товаров

	```1c
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Разница = ВыборкаНоменклатура.КоличествоОстаток - ВыборкаНоменклатура.Количество;
		Если Разница < 0 Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("На счете Товары для номенклатуры %1 недостаточно товара в количестве %2",
				ВыборкаНоменклатура.НоменклатураПредставление,
				-Разница);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоСписываем = ВыборкаНоменклатура.Количество;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И КоличествоСписываем > 0 Цикл
			// сколько списываем - всё из партии или только часть
			Списываем = Мин(КоличествоСписываем, Выборка.КоличествоОстаток);
			
			// проверка на последнее списание
			Если Списываем = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СуммаОстаток;
			Иначе 
				Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
			КонецЕсли;
			
			КоличествоСписываем = КоличествоСписываем - Списываем;
			
			// списываем товары
			//
			Движение = Движения.Управленческий.Добавить();
			// общая часть
			Движение.Период = Дата;
			Движение.Сумма = Себестоимость;
			// дт
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки; 
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			// кт
			Движение.СчетКт = ПланыСчетов.Управленческий.Товары; 
			Движение.КоличествоКт = Списываем;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Выборка.Партия;
		КонецЦикла;  
		
		// записываем продажу
		//
		Движение = Движения.Управленческий.Добавить();
		// общая часть
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаНоменклатура.Сумма;	
		// дт
		Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		// кт
		Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;	
	КонецЦикла;
	```

5. Устанавливаем блокировку

	Блокировка устанавливается `ПЕРЕД` запросом. 

	На основе запроса делаем блокировку:
	- объект блокировки: регистр бухгалтерии `Управленческий`
	- отборы блокировки:
		- счет
		- склад
		- список номенклатуры; можно достать из документа

	```1c
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий"); 
	ЭлементБлокировки.Режим= РежимБлокировкиДанных.Исключительный; 
	// обязательно блокируем счет
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары); 
	// блокируем склад, т.к. он используется в отборе оборотов
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
	// блокируем список номенклатуры, т.к. он используется в отборе оборотов
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	```


## Отчет

```1c
ВЫБРАТЬ
	УправленческийОбороты.Субконто1 КАК Номенклатура,
	УправленческийОбороты.СуммаОборотДт КАК Себестоимость,
	УправленческийОбороты.СуммаОборотКт КАК СуммаПродажи,
	СвойстваНоменклатуры.Значение КАК Значение,
	УправленческийОбороты.КоличествоКорОборот КАК Количество
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, , Счет = &СчетПрибылиУбытки, &ВидыСубконто, , КорСчет В (&СпискоКорСчетов), ) КАК УправленческийОбороты
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваНоменклатуры КАК СвойстваНоменклатуры
		// отбор находится в левом соединении, чтобы номенклатура без
		// установленного свойства выводилась с пустым значением
		ПО УправленческийОбороты.Субконто1 = СвойстваНоменклатуры.Номенклатура
			И (СвойстваНоменклатуры.Свойство = &Свойство)
```

Параметры:
```1c
&ВидыСубконто - Номенклатура
&СпискоКорСчетов - Товары, Покупатели
```