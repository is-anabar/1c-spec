# Билет 6 (занятие 59)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность вести проводки с произвольной корреспонденцией счетов. При решении задачи следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Требуется реализовать возможность ведения учета товаров в разрезе мест хранения и партий товаров. Под партией товара понимается документ, регистрирующий его (товара) поступление.

В документе «Приходная накладная» склад указывается в табличной части для каждой строки и формируются следующие проводки:

- Дт «Товары» - Кт «Поставщики» на количество и сумму закупаемого товара.

Продажа товара производится со склада и регистрируется документом «Расходная накладная»

Склад указывается в реквизите самого документа. При заполнении документа партия товара не указывается. При проведении производится проверка достаточности количества на указанном складе продаваемого товара.

Документ «Расходная накладная» реализует следующие проводки:
- Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости;
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму в продажных ценах.

При продаже товара списание партий этого товара производится либо по дисциплине LIFO («последним пришел, первым ушел»), либо FIFO («первым пришел, первым ушел»). Какая из дисциплин будет использоваться, определяется в начале года и в течение года не меняется.

Себестоимость товара рассчитывается в разрезе партий поступления этого товара.

Необходимо построить отчет о товарах на складах.

![report](/2%20-%20БУ/media/ticket-6-report.png)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)

## Учетная политика

Инструкция по созданию учетной политики [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md)


## Настройка Виды Субконто

### Склады

Создаем справочник `Склады`.

### Типы субконто

Добавляем:
- Номенклатура
- Склады
- Партии (приходная накладная)



## Настройка плана счетов

<!-- настройка 
0 - общее\05 как заполнить регистр бухгалтерии.md
Остатки ведутся по одному признаку, а себестоимость рассчитывается по другому признаку -->


По условию:
- остатки считаем в разрезе складов
- себестоимость считаем в разрезе партий

Чтобы правильно организовать учет, нужно добавить `признаки учета субконто`

### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный
- Признаки учета субконто
    - Количественный
    - КоличественныйСклад
    - Суммовой

### Настройка счетов

Товары (используется как аналог регистра по остаткам):
- Количественный: `да`
- Субконто:
	- Номенклатура
        - количественный, количественный склад, суммовой
	- Склад
        - количественный склад
    - Партия
        - количественный, суммовой



Прибыли убытки (используется как аналог регистра для продаж):
- Количественный: `нет`
- Субконто:
	- Номенклатура: `только обороты`
        - суммовой 

## Настройка регистра бухгалтерии

Необходимо выполнить [базовые настройки](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

Дополнительно:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
        - признак учета субконто: `Количественный`
    - КоличествоСклад:
		- `Небалансовый`
        - признак учета субконто: `КоличественныйСклад`
    - Сумма (доработки):
		- `Балансовый`
        - признак учета субконто: `Суммовой`


При получении из регистра бухгалтерии сведений по:
- Номенклатура + КоличествоСклад - данные для расчета остатков `по складам`
- Номенклатура + Количество + Сумма - данные для расчета себестоимости `по партиям`

## Приходная накладная

Добавляем реквизит `Склад`.

Для формирования движений можно воспользоваться конструктором движений.

Не забываем заполнять как `Количество`, так и `КоличествоСклад`.


## Расходная накладная

Добавляем реквизит `Склад`.

### Проверка учетной политики

Сначала проверяем, что на этот год задана учетная политика.


### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

## Формируем запрос

В запросе необходимо получить информацию по:
- остаткам по складам
- количеству и сумме номенклатуры по партиям

Сделать это необходимо через 2 левых запроса.

```1c
// группируем табличную часть
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
    // остатки по складу: выводим склад и количество
	УправленческийОстаткиПоСкладу.Субконто2 КАК Склад,
	ЕСТЬNULL(УправленческийОстаткиПоСкладу.КоличествоСкладОстаток, 0) КАК КоличествоСкладОстаток,
    // остатки по партиям: выводим партию, остатки и сумму по партии
	УправленческийОстаткиПоПартиям.Субконто3 КАК Партия,
	ЕСТЬNULL(УправленческийОстаткиПоПартиям.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстаткиПоПартиям.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
        // данные по складу
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконтоСклады,
				Субконто1 В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					И Субконто2 = &Склад) КАК УправленческийОстаткиПоСкладу
		ПО втТовары.Номенклатура = УправленческийОстаткиПоСкладу.Субконто1
        // данные по партиям
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконтоПартии,
				Субконто1 В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК УправленческийОстаткиПоПартиям
		ПО втТовары.Номенклатура = УправленческийОстаткиПоПартиям.Субконто1

// сортировка для политики учета
УПОРЯДОЧИТЬ ПО
	ВЫРАЗИТЬ(УправленческийОстаткиПоПартиям.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени
ИТОГИ
	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма),
	МАКСИМУМ(КоличествоСкладОстаток)
ПО
	Номенклатура
```


Изменяем текст в зависимости от учетной политики:
```1c
// Порядок документов меняется в зависимости от учетной политики	
Если УчетнаяПолитика = Перечисления.УчетнаяПолитика.ЛИФО Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
	"ВЫРАЗИТЬ(УправленческийОстаткиПоПартиям.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремени",
	"ВЫРАЗИТЬ(УправленческийОстаткиПоПартиям.Субконто3 КАК Документ.ПриходнаяНакладная).МоментВремениУБЫВ");
КонецЕсли;
```

Также для левых соединений разные списки номенклатуры.

Для получения данных по партиям достаточно 2 субконто, а для получения партий - 3

```1c
ВидыСубконтоСклады = Новый Массив; 
ВидыСубконтоСклады.Добавить(Субконто.Номенклатура);
ВидыСубконтоСклады.Добавить(Субконто.Склады);
	
ВидыСубконтоПартии = Новый Массив; 
ВидыСубконтоПартии.Добавить(Субконто.Номенклатура);
ВидыСубконтоПартии.Добавить(Субконто.Склады);
ВидыСубконтоПартии.Добавить(Субконто.Партии);
```

### Формирование движений

```1c
Пока ВыборкаНоменклатура.Следующий() Цикл
	// проверка, что хватает на складе	
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоСкладОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
		ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

    // запоминаем, сколько номенклатуры требуется в документе	
	КоличествоКСписанию = ВыборкаНоменклатура.Количество;
		
	Выборка = ВыборкаНоменклатура.Выбрать();
    // проверяем, что есть что списывать
	Пока Выборка.Следующий() И КоличествоКСписанию > 0 Цикл
        // определяем, сколько списать с текущей партии
		Списываем = Мин(Выборка.КоличествоОстаток, КоличествоКСписанию);
		КоличествоКСписанию = КоличествоКСписанию - Списываем;

        // проверка на последнее списание	
		Если Списываем = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе 
			Себестоимость = Списываем *  Выборка.СуммаОстаток / Выборка.КоличествоОстаток; 
		КонецЕсли;
			
		// списание
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		// дт 
		Движение.СчетДт = Счета.ПрибылиУбытки;
		Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
		// кт
		Движение.СчетКт = Счета.Товары;
		Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[Субконто.Склады] = Выборка.Склад;
		Движение.СубконтоКт[Субконто.Партии] = Выборка.Партия;
		Движение.КоличествоКт = Списываем;
		Движение.КоличествоСкладКт = Списываем; // списываем со склада тоже
		// общее
		Движение.Сумма = Себестоимость;
	КонецЦикла; 	
	// продажа 
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата; 
	// дт
	Движение.СчетДт = Счета.Покупатели;
	// кт 
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура; 
	// общее
	Движение.Сумма = ВыборкаНоменклатура.Сумма;
КонецЦикла
```


## Отчет

```1c
ВЫБРАТЬ
	ВЫРАЗИТЬ(УправленческийОстаткиНоменклатураСклад.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
	ВЫРАЗИТЬ(УправленческийОстаткиНоменклатураСклад.Субконто2 КАК Справочник.Склады) КАК Склад,
	УправленческийОстаткиНоменклатураСклад.КоличествоСкладОстаток КАК Количество,
	УправленческийОстаткиНоменклатураСклад.СуммаОстаток КАК Сумма
ИЗ
	РегистрБухгалтерии.Управленческий.Остатки(&Период, Счет = &СчетТовары, &ВидыСубконто, ) КАК УправленческийОстаткиНоменклатураСклад
```


Однако в чистом виде будет следующий вывод:

|Номенклатура|Склад|Количество|Сумма|
|---|---|---|---|
|Ручка|Склад|9 | |
|Ручка| | |18,00|


Чтобы в отчете все хорошо легло:
- На вкладке `Ресурсы` добавляем Количество и Сумму
- Создаем иерархию вывода:
    - (группировка) Номенклатура
        - (группировка) Склад
            - (выбранные поля) Количество, Сумма