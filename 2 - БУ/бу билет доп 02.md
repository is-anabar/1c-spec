# Билет 2 дополнительный (Возврат товаров)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Компания занимается оптовой торговлей. Поступление товара отражается документом «Приходная накладная», продажа – «Расходная накладная».

Документ «Приходная накладная» реализует следующие проводки:
- Дт «Товары» - Кт «Поставщики» на количество и сумму закупаемого товара.
 
Списание себестоимости товаров должно быть организовано по партиям, в зависимости от текущего значения принятого на этот год в учетной политике метода списания себестоимости (FIFO или LIFO). Еще раз подчеркивается  - учетная политика действует год.  На следующий год метод списания может смениться.

Документ «Расходная накладная» реализует следующие проводки:
- Дт «Прибыли и убытки» - Кт «Товары» на количество и сумму себестоимости;
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму  в продажных ценах.
 
В том случае, когда какой-либо товар не устраивает покупателя, покупатель может вернуть этот товар, если с момента продажи прошло не более чем 3 дня. Возврат товара отражается документом «Приходная накладная» с соответствующим признаком и указанием документа, по которому производится возврат. В том случае, когда с момента возврата прошло более 3-х дней, товар к возврату не принимается и документ не проводится. Себестоимость принятого товара считается как средняя списанная себестоимость этого товара из документа «Расходная накладная», по которой происходит возврат.
			
Необходимо построить отчет по продажам товаров  за период

![report](/2%20-%20БУ/media/ticket-extra-2-report.png)




## Учетная политика

Можно прочитать [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Настройка Виды Субконто

### Типы субконто

Добавляем:
- Номенклатура
- Партии (приходная накладная)
- Списания (расходная накладная)
	- будет полезно при написании запроса на возврат товаров


## Настройка плана счетов

<!-- настройка 
0 - общее\05 как заполнить регистр бухгалтерии.md
Простой учет остатков товаров -->

Настройка похожа на [настройку простого списания товаров](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)


### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Количественный

### Настройка счетов

Товары (используется как аналог регистра по остаткам):
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Партии

Прибыли убытки (используется как аналог регистра для продаж):
- Количественный: `нет`
- Субконто:
	- Номенклатура: `только обороты`
    - Списания: `только обороты`


## Настройка регистра бухгалтерии

Необходимо выполнить [базовые настройки](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

В итоге получаем:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма
		- `Балансовый`


## Приходная накладная


### Обычное поступление

Для формирования движений можно воспользоваться конструктором движений.

### Возврат товаров

Добавляем реквизиты:
- ЭтоВозврат (булево)
- РасходнаяНакладная


### Проверка, что товар возвращается в срок

Создаем функцию, которая вычисляет, сколько дней прошло с момента покупки до момента возврата.

```1c
// получаем ДатаПокупки - дата расходной накладной -
// с помощью запроса
	
ДатаПокупки = НачалоДня(Выборка.Дата);  
ДатаВозврата = НачалоДня(Дата);
	
СекундВСутках = 24 * 60 * 60;
РазностьДат = Окр((ДатаВозврата - ДатаПокупки) / СекундВСутках, 1);
	
Возврат РазностьДат;
```

При проведении проверяем, чтобы прошло не более 3 дней.


### Флаги

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
	
Движения.Управленческий.Записать();

// устанавливаем флаг, что движения нужно записать	
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Чтобы получить информацию, какие товары были проданы, воспользуемся проводкой `Дт Прибыли убытки - Кт Товары` (из расходной накладной). В ней будет указана сумма и себестоимость, которую мы списали со счета `Товары`.

```1c
ВЫБРАТЬ
	// группировка табличной части
	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
ГДЕ
	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// информация из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	// информация из проводок - сколько и по какой цене было отдано со счета товары
	ЕСТЬNULL(УправленческийОборотыДтКт.КоличествоОборотКт, 0) КАК КоличествоОборотКт,
	ЕСТЬNULL(УправленческийОборотыДтКт.СуммаОборот, 0) КАК СуммаОборот
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(
				,
				,
				,
				СчетДт = &СчетПрибылиУбытки,
				&ВидыСубконтоПрибылиУбытки,
				СчетКт = &СчетТовары,
				&ВидыСубконтоТовары,
				// устанавливаем отбор по номенклатуре
				СубконтоКт1 В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					// устанавливаем отбор по документу-списанию
					И СубконтоДт2 = &РасходнаяНакладная) КАК УправленческийОборотыДтКт
		ПО втТовары.Номенклатура = УправленческийОборотыДтКт.СубконтоКт1
```


### Движения


```1c
Пока Выборка.Следующий() Цикл
	// проверка, что возвращаем не больше, чем покупали
	Разница = Выборка.Количество - Выборка.КоличествоОборотКт;
	Если Разница > 0 Тогда
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("В документе-списании недостаточно товара %1 в количестве %2",
		Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли; 
	
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// проверка, что возвращаем товар полностью - для решения проблемы копеек	
	Если Выборка.Количество = Выборка.КоличествоОборотКт Тогда
		СебестоимостьВозврата = Выборка.СуммаОборот;
	Иначе
		СебестоимостьВозврата = Выборка.Количество * Выборка.СуммаОборот / Выборка.КоличествоОборотКт; 
	КонецЕсли;

	// записываем движение	
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.Товары;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоДт[Субконто.Партии] = Ссылка; 
	Движение.КоличествоДт = Выборка.Количество;
	// кт 
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоКт[Субконто.Списания] = РасходнаяНакладная; // документ-списание
	// общее
	Движение.Сумма = СебестоимостьВозврата;
КонецЦикла;
```

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- счет: `ПрибылиУбытки`
	- список номенклатуры; можно достать из документа
	- списание; можно взять из реквизита `РасходнаяНакладная`

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.ПрибылиУбытки);
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
// блокировка по документу-списанию
ЭлементБлокировки.УстановитьЗначение(Субконто.Списания, РасходнаяНакладная);
Блокировка.Заблокировать();
```





## Расходная накладная

### Учетная политика

Проверяем, что указана учетаня политика


### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Получаем данные для списания по партиям.

```1c
ВЫБРАТЬ
	// группировка табличной части
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// информация из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// информация об остатках
	УправленческийОстатки.Субконто2 КАК Партия,
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				Субконто1 В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1

// упорядочиваем - для учетной политики
// использовем ВЫРАЗИТЬ(), т.к. субконто имеют составные типы
УПОРЯДОЧИТЬ ПО
	ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Документ.ПриходнаяНакладная).МоментВремени
ИТОГИ
	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма),
	СУММА(КоличествоОстаток)
ПО
	Номенклатура
```


### Формирование движений

```1c
// исправляем текст запроса в зависимости от учетной политкии
Если УчетнаяПолитика = Перечисления.УчетнаяПолитика.ЛИФО Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
	"ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Документ.ПриходнаяНакладная).МоментВремени",
	"ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Документ.ПриходнаяНакладная).МоментВремени УБЫВ");
КонецЕсли;

	
РезультатЗапроса = Запрос.Выполнить();	
ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
Пока ВыборкаНоменклатура.Следующий() Цикл
	
	// проверка, что номенклатуры в целом хватает
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
		ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли; 
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;
	
	// запоминаем, сколько всего требуется списать
	ТребуетсяСписать = ВыборкаНоменклатура.Количество;
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
		// определяем, сколько спишем из текущей партии
		Списываем = Мин(Выборка.КоличествоОстаток, ТребуетсяСписать); 
		ТребуетсяСписать = ТребуетсяСписать - Списываем;

		// проверяем, все ли списываем с партии	
		Если Списываем = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе
			Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток; 
		КонецЕсли; 
			
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		// дт
		Движение.СчетДт = Счета.ПрибылиУбытки;
		Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоДт[Субконто.Списания] = Ссылка;
		// кт
		Движение.СчетКт = Счета.Товары;
		Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[Субконто.Партии] = Выборка.Партия; 
		Движение.КоличествоКт = Списываем;
		// общее 
		Движение.Сумма = Себестоимость;
	КонецЦикла;
		
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата; 
	// дт
	Движение.СчетДт = Счета.Покупатели;
	// кт
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоДт[Субконто.Списания] = Ссылка;
	// общее
	Движение.Сумма = ВыборкаНоменклатура.Сумма;
КонецЦикла;
```


### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- счет: `Товары`
	- список номенклатуры; можно достать из документа

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
Блокировка.Заблокировать();
```

### Отчет

Из запроса по оборотам к счету `Товары` можно достать всю информацию

```1c
ВЫБРАТЬ
	УправленческийОбороты.Субконто1 КАК Номенклатура,
	// Товары - Поставщики
	// со стороны дебета - себестоимость
	ВЫБОР
		КОГДА УправленческийОбороты.КорСчет = &СчетПоставщики
			ТОГДА УправленческийОбороты.СуммаОборотДт
	КОНЕЦ КАК Себестоимость,
	// Прибыли убытки - Товары
	// со стороны кредита - продажа
	ВЫБОР
		КОГДА УправленческийОбороты.КорСчет = &СчетПрибылиУбытки
			ТОГДА УправленческийОбороты.КоличествоОборотКт
	КОНЕЦ КАК КоличествоПродажа,
	ВЫБОР
		КОГДА УправленческийОбороты.КорСчет = &СчетПрибылиУбытки
			ТОГДА УправленческийОбороты.СуммаОборотКт
	КОНЕЦ КАК СуммаПродажа,
	// Товары - Прибыли убытки
	// со стороны дебета - возвраты
	ВЫБОР
		КОГДА УправленческийОбороты.КорСчет = &СчетПрибылиУбытки
			ТОГДА УправленческийОбороты.КоличествоОборотДт
	КОНЕЦ КАК КоличествоВозврат,
	ВЫБОР
		КОГДА УправленческийОбороты.КорСчет = &СчетПрибылиУбытки
			ТОГДА УправленческийОбороты.СуммаОборотДт
	КОНЕЦ КАК СуммаВозврат
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, , Счет = &СчетТовары, &ВидыСубконто, , КорСчет В (&СчетПоставщики, &СчетПрибылиУбытки), ) КАК УправленческийОбороты
```




			
