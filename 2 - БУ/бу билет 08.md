# Билет 8 (занятие 46)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задачи следует учитывать возможность наличия проводок, формированных с помощью данного документа.

Необходимо реализовать возможность закупки и продажи редких товаров. Поступление таких товаров осуществляется документом «Приходная накладная». В документе поступления каждая единица товара оформляется отдельной строкой с количеством равным 1. Каждой позиции закупаемого товара присваивается уникальный инвентарный номер (считается, что за уникальностью следит пользователь, автоматизировать получение уникальных инвентарных номеров в рамках задачи не требуется). Одним документом может оформляться поступление нескольких одинаковых товаров, но с разными инвентарными номерами.

Документ «Приходная накладная» реализует следующую проводку:
- Дт «Товары» - Km «Поставщики» на сумму закупаемого товара.

Продажа товара регистрируется документом «Расходная накладная». При продаже инвентарный номер вводится в табличную часть документа вручную. При проведении документа должен производиться контроль наличия указанного в документе товара (по указанному инвентарному номеру). Себестоимость списываемого товара определяется по каждой номенклатурной позиции для каждого ее инвентарного номера. При списании себестоимости следует учесть, что она может быть скорректирована пользователем документом «Операция» и не совпадать с закупочной ценой.

Документ «Расходная накладная» реализует следующие проводки:
- Дт «Прибыли и убытки» - Km «Товары» на сумму себестоимости;
- Дт «Покупатели» - Km «Прибыли и убытки» на сумму в продажных ценах.

Также в документе «Расходная накладная» указывается сотрудник, осуществивший продажу. Для сотрудников нужна возможность указывать различные свойства – пол, образование, наличие прав и т.п. 

Необходимо сформировать отчет, который за указанный интервал дат показывал бы данные о продажах товара сотрудниками.

![report](/2%20-%20БУ/media/ticket-8-report.png)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Настройка Виды Субконто

### Склады

Создаем справочник `Склады`.

### Типы субконто

Добавляем:
- Номенклатура
- Инвентарный номер (строка)
- Сотрудник


## Настройка плана счетов

Настройка будет выполняться по схеме [простой учет остатков товаров](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md#простой-учет-остатков-товаров)


### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Инв. номер

Прибыли убытки:
- Количественный: `нет`
- Субконто: 
	- Номенклатура: `только обороты`
	- Инв. номер: `только обороты`
	- Сотрудник: `только обороты`

## Настройка регистра бухгалтерии

В итоге должно быть:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
	- Сумма:
		- `Балансовый`


## Приходная накладная

Делаем движения с помощью конструктора.


## Расходная накладная

### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.ИнвентарныйНомер КАК ИнвентарныйНомер,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.ИнвентарныйНомер

ИНДЕКСИРОВАТЬ ПО
	Номенклатура,
	ИнвентарныйНомер
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.ИнвентарныйНомер КАК ИнвентарныйНомер,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				(Субконто1, Субконто2) В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура,
						втТовары.ИнвентарныйНомер КАК ИнвентарныйНомер
					ИЗ
						втТовары КАК втТовары)) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1
			И втТовары.ИнвентарныйНомер = УправленческийОстатки.Субконто2
```

### Формирование движений

```1c
Выборка = РезультатЗапроса.Выбрать();
Пока Выборка.Следующий() Цикл  
	Разница = Выборка.Количество - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 с(инв. номер %2) в количестве %3",
			Выборка.НоменклатураПредставление, Выборка.ИнвентарныйНомер, Разница);
		Сообщение.Сообщить(); 
	КонецЕсли; 
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;
		
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	Движение.СчетДт = Счета.ПрибылиУбытки;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоДт[Субконто.ИнвентарныеНомера] = Выборка.ИнвентарныйНомер;
	Движение.СубконтоДт[Субконто.Сотрудники] = Сотрудник;
	Движение.СчетКт = Счета.Товары;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоКт[Субконто.ИнвентарныеНомера] = Выборка.ИнвентарныйНомер;
	Движение.КоличествоКт = Выборка.Количество;
	Движение.Сумма = Выборка.СуммаОстаток; 
		
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	Движение.СчетДт = Счета.Покупатели;
	Движение.СчетКт = Счета.ПрибылиУбытки;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоКт[Субконто.ИнвентарныеНомера] = Выборка.ИнвентарныйНомер; 
	Движение.СубконтоКт[Субконто.Сотрудники] = Сотрудник;
	Движение.Сумма = Выборка.Сумма;
КонецЦикла;
```


### Блокировка

Ставим ДО запроса.

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.ИнвентарныеНомера, "ИнвентарныйНомер");
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	ВЫРАЗИТЬ(УправленческийОбороты.Субконто1 КАК Справочник.Номенклатура).Представление КАК Номенклатура,
	ВЫРАЗИТЬ(УправленческийОбороты.Субконто2 КАК СТРОКА(6)) КАК ИнвентарныйНомер,
	УправленческийОбороты.Субконто3 КАК Сотрудник,
	УправленческийОбороты.СуммаОборотДт КАК Стоимость,
	УправленческийОбороты.СуммаОборотКт КАК Продажа,
	СвойстваСотрудников.Значение КАК Свойство
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(, , , Счет = &СчетПрибылиУбытки, &ВидыСубконто, , КорСчет В (&СписокКорСчетов), ) КАК УправленческийОбороты
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваСотрудников КАК СвойстваСотрудников
		ПО УправленческийОбороты.Субконто3 = СвойстваСотрудников.Сотрудник
			И (СвойстваСотрудников.Свойство = &Свойство)
```
