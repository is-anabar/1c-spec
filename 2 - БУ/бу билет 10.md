# Билет 10 (занятия 50, 51)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Необходимо организовать учет перемещений товаров. Складской учет ведется в рамках одной организации в разрезе складов. Склады территориально удалены друг от друга. Перемещение из одного склада на другой может идти несколько дней.

Факт отправления регистрируется в системе с помощью документа «Отправление». В шапке документа указывается склад. В табличной части указывается перемещаемый товар и его количество. Документ делает следующие проводки:
- Дт «Товары в пути» - Кт «Товары» в указанном количестве. 

Себестоимость определяется как средняя по товару в разрезе склада.

Если на складе отправителе товар в нужном количестве отсутствует, документ не проводится.

Факт прибытия товара на склад получатель отражается документом «Прибытие». В документе указываются склад получатель, документ отправки, а в табличной части указывается перечень прибывших товаров и их количество. Товар может теряться в дороге или приходить по частям (табличная часть может подвергаться корректировке), но в результате корректировки недопустимо прибытие товара, который не отправляли. Документ реализует следующие проводки:
- Дт «Товары» - Кт «Товары в пути» па количество и стоимость прибывшего товара

Стоимость одной единицы товара остается неизменной в процессе перемещения и соответствует себестоимости одной единицы отправленного товара.

Потери товара необходимо регистрировать вручную с помощью документа "Операция":
- Дт «Прибыли Убытки» - Кт «Товары в пути» на количество и стоимость потерянного товара.

Необходимо получать отчет о потерях товара.

![report](/2%20-%20БУ/media/ticket-10-report.png)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)

## Настройка Виды Субконто

### Склады

Создаем справочник `Склады`.

### Типы субконто

Добавляем:
- Номенклатура
- Склад
- Отправления


## Настройка плана счетов

<!-- настройка 
0 - общее\05 как заполнить регистр бухгалтерии.md
Простой учет остатков товаров -->

### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Количественный

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Склад

Товары в пути:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Отправления

Прибыли убытки:
- Количественный: `нет`
- Субконто: можно не заполнять


## Настройка регистра бухгалтерии

Необходимо выполнить [базовые настройки](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

Дополнительно:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`





## Отправление

Добавляем реквизит `Склад`.

Создаем табличную часть `Основная` с реквизитами:
- Номенклатура
- Количество


Для правильного проведения документа необходимо:
- Получить запросом остатки товаров по складу
- Если хватает, то списать и отправить


### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Получаем данные для проверки остатков по счету `Товары`

```1c
// группируем табличную часть
ВЫБРАТЬ
	ОтправлениеОсновная.Номенклатура КАК Номенклатура,
	СУММА(ОтправлениеОсновная.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.Отправление.Основная КАК ОтправлениеОсновная
ГДЕ
	ОтправлениеОсновная.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	ОтправлениеОсновная.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	// остатки на складе-отправителе по счету товары
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				Субконто1 В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					И Субконто2 = &СкладОтправитель) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1
```

### Движения

```1c
Пока Выборка.Следующий() Цикл 
	// проверка, что на складе хватает
	Разница = Выборка.Количество - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("На складе-отправителе недостаточно товара %1 в количестве %2",
		Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// проверка последнего списания	
	Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
		Себестоимость = Выборка.СуммаОстаток;
	Иначе
		Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
	КонецЕсли;
		
	// списание со склада-отправителя и отправка в дорогу
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.ТоварыВПути;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоДт[Субконто.Отправления] = Ссылка;
	Движение.КоличествоДт = Выборка.Количество;
	// кт
	Движение.СчетКт = Счета.Товары;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоКт[Субконто.Склады] = СкладОтправитель;
	Движение.КоличествоКт = Выборка.Количество;
	// общее  
	Движение.Сумма = Себестоимость;		
КонецЦикла;
```

### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

Блокируем данные по остакам (для левого соединения):
- по счету
- по списку номенклатуры
- по складу

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
// блокировка по списку номенклатура
ЭлементБлокировки.ИсточникДанных = Основная;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
// блокировка по складу
ЭлементБлокировки.УстановитьЗначение(Субконто.Склады, СкладОтправитель);
Блокировка.Заблокировать();
```



## Прибытие

Добавляем реквизиты
- `Склад`
- `Отправление`

Создаем табличную часть `Основная` с реквизитами:
- Номенклатура
- Количество


Для правильного проведения документа необходимо:
- Получить запросом остатки товаров по отправлению
- Если хватает, то записать на склад




### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Получаем данные для проверки остатков по счету `ТоварыВПути`

```1c
// группируем табличную часть
ВЫБРАТЬ
	ПрибытиеОсновная.Номенклатура КАК Номенклатура,
	СУММА(ПрибытиеОсновная.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.Прибытие.Основная КАК ПрибытиеОсновная
ГДЕ
	ПрибытиеОсновная.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	ПрибытиеОсновная.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
    // данные номенклатуры, которая была отправлена
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТоварыВПути,
				&ВидыСубконто,
				Субконто1 В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					И Субконто2 = &Отправление) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1
```


### Движения

```1c
Пока Выборка.Следующий() Цикл 
    // проверка на отрицательные остатки
	Разница = Выборка.Количество - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Было отправлено недостаточно товара %1 в количестве %2",
		Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

    // проверка последнего списания	
	Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
		Себестоимость = Выборка.СуммаОстаток;
	Иначе
		Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
	КонецЕсли;
		
	// списание со отправления и запись на склад
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.Товары;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоДт[Субконто.Склады] = СкладПолучатель;
	Движение.КоличествоДт = Выборка.Количество;
	// кт
	Движение.СчетКт = Счета.ТоварыВПути;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоКт[Субконто.Отправления] = Отправление;
	Движение.КоличествоКт = Выборка.Количество;
	// общее  
	Движение.Сумма = Себестоимость;		
КонецЦикла;
```



### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

Блокируем данные по остакам (для левого соединения):
- по счету
- по списку номенклатуры
- по отправлению

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = Основная;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
// блокировка по 
ЭлементБлокировки.УстановитьЗначение(Субконто.Отправления, Отправление);
Блокировка.Заблокировать();
```


## Отчет

Несмотря на то, что отчет должен быть по прибытию, мы получаем последовательно данные:
- отправление
    - соединяем с получением
    - соединяем с потерей


```1c
ВЫБРАТЬ
	ВЫРАЗИТЬ(ОборотыОтправление.СубконтоДт1 КАК Справочник.Номенклатура) КАК Номенклатура,
	ВЫРАЗИТЬ(ОборотыПрибытие.СубконтоДт2 КАК Справочник.Склады) КАК Склад,
	ЕСТЬNULL(ОборотыОтправление.КоличествоОборотКт, 0) КАК КоличествоОтправлено,
	ЕСТЬNULL(ОборотыОтправление.СуммаОборот, 0) КАК СуммаОтправлено,
	ЕСТЬNULL(ОборотыПрибытие.КоличествоОборотКт, 0) КАК КоличествоПолучено,
	ЕСТЬNULL(ОборотыПрибытие.СуммаОборот, 0) КАК СуммаПолучено,
	ЕСТЬNULL(ОборотыПотеряно.КоличествоОборотКт, 0) КАК КоличествоПотеряно,
	ЕСТЬNULL(ОборотыПотеряно.СуммаОборот, 0) КАК СуммаПотеряно
ИЗ
	РегистрБухгалтерии.Управленческий.ОборотыДтКт(, &КонецПериода, , СчетДт = &СчетТоварыВПути, &ВидыСубконтоТоварыВПути, СчетКт = &СчетТовары, &ВидыСубконтоТовары, ) КАК ОборотыОтправление
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(, &КонецПериода, , СчетДт = &СчетТовары, &ВидыСубконтоТовары, СчетКт = &СчетТоварыВПути, &ВидыСубконтоТоварыВПути, ) КАК ОборотыПрибытие
        // соединение по номенклатуре
		ПО ОборотыОтправление.СубконтоДт1 = ОборотыПрибытие.СубконтоКт1
        // соединение по документу-отправления
			И ОборотыОтправление.СубконтоДт2 = ОборотыПрибытие.СубконтоКт2
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(, &КонецПериода, , СчетДт = &СчетПрибылиУбытки, , СчетКт = &СчетТоварыВПути, &ВидыСубконтоТоварыВПути, ) КАК ОборотыПотеряно
        // соединение по номенклатуре
		ПО ОборотыОтправление.СубконтоДт1 = ОборотыПотеряно.СубконтоКт1
        // соединение по документу-отправлению
            И ОборотыОтправление.СубконтоДт2 = ОборотыПрибытие.СубконтоКт2
```

Для проводок необходимо использовать:
- ВидыСубконтоТоварыВПути
- ВидыСубконтоТовары


> Отчет будет давать странные результаты, если будет ситуация:
> - Склад №1 отправил товары по документу №0001
> - Склад №2 получил часть товаров по документу №0001
> - Склад №3 получил часть товаров по документу №0001
>
> В такой случае в отчете для складов будет дублирование отправления