# Билет 11 (занятие 52)


Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Требуется организовать учет перемещений товаров. Складской учет ведется в рамках одной организации в разрезе складов. Склады территориально удалены друг от друга. Перемещение из одного склада на другой может идти несколько дней.
Факт отправления регистрируется в системе с помощью документа «Отправление». В шапке документа указывается склад - отправитель и ожидаемая дата прибытия товара. В табличной части указывается перемещаемый товар и его количество. Документ делает следующие проводки:
- Дт «Товары в пути» - Km «Товары» в указанном количестве. 

Себестоимость определяется как средняя по товару в разрезе склада.

Если на складе отправителе товар в нужном количестве отсутствует, документ не проводится.

Факт прибытия товара на склад получатель отражается документом «Прибытие». Документ вводится только на основании документа «Отправление». Считается, что отправляемый товар доходит до получателя в полном объеме, но дата фактического прибытия может отличаться от запланированной. Кроме того, в процессе передачи могут происходить потери. Подобного рода потери отражаются пользователем с помощью документа «Операция» до того, как будет оформлено прибытие товара. В документе «Прибытие» указывается только дата фактического прибытия товара и документ отправления.

Документ «Прибытие» реализует следующие проводки:
- Дт «Товары» - Km «Товары в пути»

Количество и стоимость соответствуют количеству и сумме отправленного товара.

Необходимо иметь возможность сформировать отчет о задержках перемещаемого товара. В отчет должны выводиться данные только о тех перемещениях, у которых прибытие произошло позже запланированного срока.

Задержки товара, поступившего за период с 01.01.2010 по 31.01.2010

|Отправитель	|Получатель	|Дата прибытия	|Задержка|
|---------------|-----------|---------------|--------|
|Основной	|Региональный 1	|15 января 2010	|2|
|Основной	|Региональный 2	|17 января 2010	|7|

Данные отчета должны содержать сведения только о тех товарах, которые прибыли в выбранном периоде. Задержка показывает количество дней просрочки относительно плановой даты.


## Настройка Виды Субконто

### Склады

Создаем справочник `Склады`.

### Типы субконто

Добавляем:
- Номенклатура
- Склад
- Отправления
- Дата проводки (для учета задержек товаров)


## Настройка плана счетов

Настройка похожа на [настройку простого списания товаров](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md)

### Общая настройка 

- Количество субконто: 3
- Признаки учета:
	- Количественный

### Настройка счетов

Товары:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Склад

Товары в пути:
- Количественный: `да`
- Субконто:
	- Номенклатура
	- Отправления
    - Дата проводки
        - ОБЯЗАТЕЛЬНО `оборотный`

> Если не выключить для даты проводки только обороты, то может быть следующая ситуация при получении товара:
> Остатки:
> - Сырок - 02.10 (отправление) - 5 штук
> - Сырок - 04.10 (прибытие) - -5 штук
> 
> Номенклатура зависнет из-за разных дат, хотя должна присаться полность по прибытию.


## Настройка регистра бухгалтерии

Итоговая настройка:
- Ресурсы:
	- Количество:
		- `Небалансовый`
		- признак учета: `Количественный`
    - Сумма


## Отправление

Добавляем реквизиты:
- Склад
- ОжидаемаяДатаПрибытия

Создаем табличную часть `Основная` с реквизитами:
- Номенклатура
- Количество


Для правильного проведения документа необходимо:
- Получить запросом остатки товаров по складу
- Если хватает, то списать и отправить


### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Получаем данные для проверки остатков по счету `Товары`

```1c
// группируем табличную часть
ВЫБРАТЬ
	ОтправлениеОсновная.Номенклатура КАК Номенклатура,
	СУММА(ОтправлениеОсновная.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.Отправление.Основная КАК ОтправлениеОсновная
ГДЕ
	ОтправлениеОсновная.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	ОтправлениеОсновная.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	// остатки на складе-отправителе по счету товары
	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
				&МоментВремени,
				Счет = &СчетТовары,
				&ВидыСубконто,
				Субконто1 В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					И Субконто2 = &СкладОтправитель) КАК УправленческийОстатки
		ПО втТовары.Номенклатура = УправленческийОстатки.Субконто1
```

### Движения

```1c
Пока Выборка.Следующий() Цикл 
	// проверка, что на складе хватает
	Разница = Выборка.Количество - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("На складе-отправителе недостаточно товара %1 в количестве %2",
		Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// проверка последнего списания	
	Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
		Себестоимость = Выборка.СуммаОстаток;
	Иначе
		Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
	КонецЕсли;
		
	// списание со склада-отправителя и отправка в дорогу
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.ТоварыВПути;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
    Движение.СубконтоДт[Субконто.ДатаПроводки] = ОжидаемаяДатаПрибытия;
	Движение.СубконтоДт[Субконто.Отправления] = Ссылка;
	Движение.КоличествоДт = Выборка.Количество;
	// кт
	Движение.СчетКт = Счета.Товары;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоКт[Субконто.Склады] = СкладОтправитель;
	Движение.КоличествоКт = Выборка.Количество;
	// общее  
	Движение.Сумма = Себестоимость;		
КонецЦикла;
```

### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

Блокируем данные по остакам (для левого соединения):
- по счету
- по списку номенклатуры
- по складу

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
// блокировка по списку номенклатура
ЭлементБлокировки.ИсточникДанных = Основная;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных(Субконто.Номенклатура, "Номенклатура");
// блокировка по складу
ЭлементБлокировки.УстановитьЗначение(Субконто.Склады, СкладОтправитель);
Блокировка.Заблокировать();
```


## Прибытие

Добавляем реквизиты
- ФактическаяДатаПрибытия
- СкладПолучатель
- ДокументОтправление

### Установка флагов записи движений

```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Получаем данные для проверки остатков по счету `ТоварыВПути`

```1c
ВЫБРАТЬ
	УправленческийОстатки.Субконто1 КАК Номенклатура,
	УправленческийОстатки.Субконто2 КАК Отправление,
	УправленческийОстатки.Субконто3 КАК ДатаПроводки,
	УправленческийОстатки.КоличествоОстаток КАК Количество,
	УправленческийОстатки.СуммаОстаток КАК Сумма
ИЗ
	РегистрБухгалтерии.Управленческий.Остатки(
		&ФактическаяДатаПрибытия, // берем на фактическую дату прибытия
		Счет = &СчетТоварыВПути, &ВидыСубконто, Субконто2 = &ДокументОтправления) КАК УправленческийОстатки
```

### Движения

```1c
Пока Выборка.Следующий() Цикл 
	Движение = Движения.Управленческий.Добавить(); 
	Движение.Период = Дата; 
	// дт
	Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
	Движение.СубконтоДт[Субконто.Номенклатура] = Выборка.Номенклатура;
	Движение.СубконтоДт[Субконто.Склады] = СкладПолучатель;
	Движение.КоличествоДт = Выборка.Количество;
		// кт
	Движение.СчетКт = ПланыСчетов.Управленческий.ТоварыВПути;
	Движение.СубконтоКт[Субконто.Номенклатура] = Выборка.Номенклатура; 
	Движение.СубконтоКт[Субконто.Отправления] = Выборка.Отправление;
	Движение.СубконтоКт[Субконто.ДатаПроводки] = ФактическаяДатаПрибытия;
	Движение.КоличествоКт = Выборка.Количество;		
	// общее
	Движение.Сумма = Выборка.Сумма;
КонецЦикла;
```

### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

Блокируем данные по остакам (для левого соединения):
- по счету
- по документу-отправлению

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Товары);
// блокировка по документу отправления
ЭлементБлокировки.УстановитьЗначение(Субконто.Отправления, ДокументОтправление);
Блокировка.Заблокировать();
```


## Отчет

Получаем данные с помощью двух соединений `ОборотыДтКт`:
- один для прибытия
- второй - для отправления

Чтобы соответствовать условию `Данные отчета должны содержать сведения только о тех товарах, которые прибыли в выбранном периоде`:
- не устанавливаем период отчета (т.к. это период регистратора)
- указываем в условии, что дата прибытия должна попадать в период

```1c
ВЫБРАТЬ РАЗЛИЧНЫЕ
	Отправление.СубконтоКт2 КАК Отправитель,
	Прибытие.СубконтоДт2 КАК Получатель,
	Прибытие.СубконтоКт3 КАК ДатаПрибытия,
	// рассчет разности дат
	РАЗНОСТЬДАТ(
		ВЫРАЗИТЬ(Отправление.СубконтоДт3 КАК ДАТА), // приводим к дате, иначе будет ругаться
		ВЫРАЗИТЬ(Прибытие.СубконтоКт3 КАК ДАТА), // аналогично
		ДЕНЬ
	) КАК Задержка
ИЗ
	РегистрБухгалтерии.Управленческий.ОборотыДтКт(
			, 
			, 
			, 
			СчетДт = &СчетТовары, 
			&ВидыСубконтоТовары, 
			СчетКт = &СчетТоварыВПути, 
			&ВидыСубконтоТоварыВПути, 
			// дата прибытия должна попасть в период отчета
			// приводим к дате, иначе будет ругаться
			ВЫРАЗИТЬ(СубконтоКт3 КАК ДАТА) МЕЖДУ &НачалоПериодаПрибытие И &КонецПериодаПрибытие) КАК Прибытие
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.ОборотыДтКт(, , , СчетДт = &СчетТоварыВПути, &ВидыСубконтоТоварыВПути, СчетКт = &СчетТовары, &ВидыСубконтоТовары, ) КАК Отправление
		ПО Прибытие.СубконтоКт1 = Отправление.СубконтоДт1
			И Прибытие.СубконтоКт2 = Отправление.СубконтоДт2
ГДЕ
	РАЗНОСТЬДАТ(ВЫРАЗИТЬ(Отправление.СубконтоДт3 КАК ДАТА), ВЫРАЗИТЬ(Прибытие.СубконтоКт3 КАК ДАТА), ДЕНЬ) > 0
```
