# Билет 7 (занятие 60)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задачи следует учитывать возможность наличия проводок, формированных с помощью данного документа.

Взаиморасчеты с покупателями ведутся в разрезе договоров с детализацией до документа отгрузки. С каждым контрагентом может быть заключено произвольное количество договоров. В каждом договоре указывается дата окончания действия этого договора. Задолженность покупателей возникает при проведении документа «Расходная накладная». В этом документе пользователем указывается сам покупатель («Контрагент») и договор (договор вместе с контрагентом указывается в шапке документа). Пользователю должно быть запрещено указывать не соответствующий контрагенту договор.

Взаиморасчеты с покупателями ведутся одновременно в рублях и евро. Курс евро указывается пользователем вручную в документе.

Документ «Расходная накладная» формирует следующую проводку:
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму продажи
	
Погашение задолженности (оплата переданного покупателю товара) регистрируется с помощью документа «Приход денег». В реквизитах документа указывается контрагент и сумма оплаты. Погашение задолженности происходит в первую очередь по тем договорам, у которых срок окончания действия самый ранний. В рамках каждого договора задолженность погашается в хронологическом порядке, начиная с самой первой недоплаченной накладной. Сумма оплаты не может превосходить сумму задолженности. В том случае, если сумма оплаты больше суммы общей задолженности, то документ не должен проводиться.

Контроль задолженностей должен производиться по рублевым суммам.

Незакрытые в евро задолженности, закрытые в рублях, закрываются пользователем вручную с помощью документа «Операция».

Документ «Приход денег» формирует следующую проводку:
- Дт «Касса» - Кт «Покупатели» на расчетную сумму оплат
	
    
Необходимо создать отчет по состоянию взаиморасчетов с покупателями.
![report](/2%20-%20БУ/media/ticket-7-report.png)


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Договоры

СОздаем справочник:
- Реквизиты: 
    - срок договора
- Владельцы:
    - контрагент


## Настройка Виды Субконто


### Типы субконто

Добавляем:
- Контрагенты
- Договоры
- Партии (расходная накладная)


## Настройка плана счетов

Для настройки нужно использовать:
- [учет в валюте](/2%20-%20БУ/общее/05%20как%20заполнить%20регистр%20бухгалтерии.md#учет-в-валюте)

Количество в билете не участвует.

### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Валютный

### Настройка счетов

Покупатели:
- Валютный: `да`
- Субконто:
	- Контрагенты
    - Договоры
    - Партии


## Настройка регистра бухгалтерии

Итого:
- Ресурсы:
	- СуммаРуб:
		- `Балансовый`
	- СуммаВал:
		- `Небалансовый`
		- признак учета: `Валютный`



## Расходная накладная

Добавляем реквизиты:
- Контрагент
- Договор
    - Связи параметров выбора: Отбор.Владелец(Контрагент)
- СуммаВал
- Курс

Движение можно сформировать с помощью конструктора.

## Приход денег

Создаем документ:
- Контрагент
- СуммаРуб
- Курс


### Флаги

```1c
Движения.Управленческий.Записывать = Истина;
Движения.Управленческий.Записать();	
Движения.Управленческий.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	УправленческийОстатки.Субконто2 КАК Договор,
	УправленческийОстатки.Субконто3 КАК Партия,
	УправленческийОстатки.СуммаВалОстаток КАК СуммаВалОстаток,
	УправленческийОстатки.СуммаРубОстаток КАК СуммаРубОстаток
ИЗ
	РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет = &СчетПокупатели, &ВидыСубконто, Субконто1 = &Контрагент) КАК УправленческийОстатки

УПОРЯДОЧИТЬ ПО
    // делать серез ВЫРАЗИТЬ(), чтобы не было лишних неявных соединений
	ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Справочник.Договоры).СрокДействия
    ВЫРАЗИТЬ(УправленческийОстатки.Субконто3 КАК Документ.РасходнаяНакладная).МоментВремени
ИТОГИ   // для контроля, что сумма оплаты не превышает сумму долга
	СУММА(СуммаРубОстаток)
ПО
	ОБЩИЕ
```

### Движения

```1c
РезультатЗапроса = Запрос.Выполнить();

// если результаты пустые, то нет долгов вообще	
Если РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "У контрагента нет долга.";
	Сообщение.Сообщить();  
		
	Возврат;
КонецЕсли;
	
ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
ВыборкаОбщийИтог.Следующий();		

// проверка, что сумма оплаты не больше суммы долгов	
Разница = СуммаРуб - ВыборкаОбщийИтог.СуммаРубОстаток;
Если Разница > 0 Тогда
	Отказ = Истина;
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Сумма оплаты превышает сумму долга на " + Разница;
	Сообщение.Сообщить(); 
		
	Возврат;
КонецЕсли; 

// запоминаем, сколько поступило	
ДоступноДляСписания = СуммаРуб;
	
Курс = ?(Курс = 0, 1, Курс);
		
Выборка = ВыборкаОбщийИтог.Выбрать();
// пока есть, что списывать
Пока Выборка.Следующий() И ДоступноДляСписания > 0 Цикл 
    // определяем сумму в рублях и в валюте
	СписываемРуб = Мин(Выборка.СуммаРубОстаток, ДоступноДляСписания);
	СписываемВал = Окр(СписываемРуб / Курс, 2);

    // если не покрывается евроцент, то не записываем	
	Если СписываемВал = 0 Тогда
		Продолжить;
	КонецЕсли; 
		
	ДоступноДляСписания = ДоступноДляСписания - СписываемРуб;
		
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.Касса;
	// кт
	Движение.СчетКт = Счета.Покупатели;
	Движение.СубконтоКт[Субконто.Контрагенты] = Контрагент; 
	Движение.СубконтоКт[Субконто.Договоры] = Выборка.Договор;
	Движение.СубконтоКт[Субконто.Партии] = Выборка.Партия;
	Движение.СуммаВалКт = СписываемВал;
	// общее 
	Движение.СуммаРуб = СписываемРуб;
КонецЦикла;
```

### Блокировка

Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

Блокируем данные по остакам:
- по счету
- по контрагенту

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Покупатели);
// блокировка по контрагенту
ЭлементБлокировки.УстановитьЗначение(Субконто.Контрагенты, Контрагент);
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	УправленческийОбороты.Субконто1 КАК Контрагент,
	УправленческийОбороты.Субконто2 КАК Договор,
	"Расх. накл. № " + ВЫРАЗИТЬ(УправленческийОбороты.Субконто3 КАК Документ.РасходнаяНакладная).Номер КАК Документ,
	УправленческийОбороты.СуммаРубОборотДт КАК ОтгрузкаРуб,
	УправленческийОбороты.СуммаВалОборотДт КАК ОтгрузкаВал,
	УправленческийОбороты.СуммаРубОборотКт КАК ОплатаРуб,
	УправленческийОбороты.СуммаВалОборотКт КАК ОплатаВал
ИЗ
	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, , Счет = &СчетПокупатели, &ВидыСубконто, , КорСчет В (&СписокКорСчетов), ) КАК УправленческийОбороты
```
