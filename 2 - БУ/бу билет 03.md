# Билет 3 (занятия 62-63, 64)

Необходимо создать документ «Операция», с помощью которого пользователь должен иметь возможность ввести проводки с произвольной корреспонденцией счетов. При решении задач следует учитывать возможность наличия проводок, сформированных с помощью данного документа.

Требуется организовать возможность ведения взаиморасчетов (счет «Покупатели») в разрезе контрагентов и договоров в валюте взаиморасчетов. С каждым контрагентом может быть заключено любое количество договоров (у каждого может быть своя валюта взаиморасчетов). Все взаиморасчеты по договору ведутся только в валюте, указанной в этом договоре, и рублевом эквиваленте.

Задолженность покупателей возникает при проведении документа «Расходная накладная». В этом документе пользователем указывается сам покупатель («Контрагент») и договор. Все суммы в документе указываются в валюте выбранного договора.

Документ «Расходная накладная» формирует следующую проводку:
- Дт «Покупатели» - Кт «Прибыли и убытки»
на сумму продажи в валюте взаиморасчетов и в рублях по курсу на дату документа

Оплата задолженности по отгрузке происходит документом «Приход денег», где указывается контрагент и сумма оплаты в рублях. Сумма должна автоматически распределяться на договоры в соответствии с максимальной рублевой задолженностью по договору (чем больше задолженность, тем скорее она должна быть оплачена). Следует считать, что авансов и переплат быть не может.

Документ «Приход денег» должен формировать следующие проводки:
- Дт «Касса» - Кт «Покупатели»

на расчетную сумму оплаты в валюте взаиморасчетов и в рублях по курсу на дату документа.
Необходимо реализовать обработку документ «Корректировка задолженности», которая бы в соответствии с изменившимся курсом валюты взаиморасчетов корректировала рублевую задолженность покупателя. Т.е. предположим, что при курсе валюты «Валютная» в 10 рублей покупателем был получен товар на 10 единиц «Валютная» (получается, по курсу это 100 рублей). Курс вырос до 12 рублей за единицу валюты. В соответствии с этим рублевая сумма задолженности покупателя должная возрасти на 20 рублей.

Обработка «Корректировка задолженности» должна формировать документ “Операция” со следующими проводками:
- Дт «Покупатели» - Кт «Прибыли и убытки» на сумму разницы (в случае увеличения рублевого долга покупателя)
- Дт «Прибыли и убытки» - Кт «Покупатели» на сумму разницы (в случае уменьшения рублевого долга покупателя)

Документ «Корректировка задолженности» «Операция» вводится как регламентный (один экземпляр корректирует все существующие задолженности).

Необходимо создать отчет о взаиморасчетах с покупателями по выбранной валюте за период с детализацией до документа.

![report](/2%20-%20БУ/media/ticket-3-report.png)


## Анализ

Во всех проводках участвует счет `Покупатели` и к нему всегда будет привязана валюта. В таком случае валюту можно сделать `измерением` регистра бухгалтерии.

При это можно настроить использование валюты (какие-то счета будут иметь валюту и валютную сумму, а какие-то - нет) с помощью `признака учета` из плана счетов. В этой задаче только `Покупатели` имеют в движениях валюту и валютную сумму. 


## Документ Операция 

Можно прочитать [здесь](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md)


## Настройка Виды Субконто

Создаем справочники `Договоры`
- Реквизиты:
	- валюта
- Прочие настройки:
	- на вкладке `Владельцы` указываем справочник `Контрагенты`

Добавляем:
- Контрагенты
- Договоры

Удаляем:
- Номенклатура


## Настройка плана счетов


### Общая настройка 

- Количество субконто: 2
- Признаки учета:
	- Валютный

### Настройка счетов

Покупатели:
- Валютный: `да`
- Субконто:
	- Контрагенты
	- Договоры 

Прибыли убытки:
- Валютный: `нет`
- Субконто: можно не настраивать


## Настройка регистра бухгалтерии

Итоговые настройки:
- Измерения:
	- Валюта
		- признак учета: `Валютный`
- Ресурсы:
	- Сумма
	- СуммаВал:
		- `Небалансовый`
		- признак учета: `Валютный`


## Расходная накладная

Добавляем реквизиты:
- Контрагент
- Договор
	- В настройках устанавливаем `Связи параметров выбора`: Отбор.Владелец(Контрагент)

### Проверка валюты договора

При проведении проверяем наличие валюты у договора (с помощью запроса)

### Проверка курса валюты

Если у договора есть валюта, то проверяем, что установлен курс

### Движения

Далее просто записываем движения. Можно воспользоваться конструктором движений.


## Приход денег

Создаем документ.

Реквизиты:
- Контрагент
- Сумма (в рублях)


### Установка флагов 

Очищаем движения (если были) и устанавливаем флаг на запись:
```1c
// очищаем движения - записываем пустые
Движения.Управленческий.Записывать = Истина;
Движения.Записать();

// устанавливаем флажок, чтобы новые движения записывались
Движения.Управленческий.Записывать = Истина;
```

### Запрос

Необходимо получить долги по договорам в валюте и долги в рублях `по актуальному курсу` (т.е. рассчитывать рублей долг на основе валютного * курс, а не брать напрямую рублевый)

```1c
ВЫБРАТЬ
	// получаем данные по долгам
	УправленческийОстатки.Субконто1 КАК Контрагент,
	УправленческийОстатки.Субконто2 КАК Договор,
	УправленческийОстатки.Валюта КАК Валюта,
	УправленческийОстатки.СуммаВалОстаток КАК СуммаВалОстаток
ПОМЕСТИТЬ втЗадолженности
ИЗ
	РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет = &СчетПокупатели, &ВидыСубконто, Субконто1 = &Контрагент) КАК УправленческийОстатки

ИНДЕКСИРОВАТЬ ПО
	Валюта
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втЗадолженности.Контрагент КАК Контрагент,
	втЗадолженности.Договор КАК Договор,
	втЗадолженности.Валюта КАК Валюта,
	втЗадолженности.Валюта.Представление КАК ВалютаПредставление,
	втЗадолженности.СуммаВалОстаток КАК СуммаВалОстаток,
	// получаем долг в рублях по курсу
	втЗадолженности.СуммаВалОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК СуммаРубОстаток,
	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс
ИЗ
	втЗадолженности КАК втЗадолженности
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
				&МоментВремени,
				Валюта В
					(ВЫБРАТЬ
						втЗадолженности.Валюта КАК Валюта
					ИЗ
						втЗадолженности КАК втЗадолженности)) КАК КурсыВалютСрезПоследних
		ПО втЗадолженности.Валюта = КурсыВалютСрезПоследних.Валюта

УПОРЯДОЧИТЬ ПО
	СуммаРубОстаток УБЫВ
ИТОГИ
	СУММА(СуммаРубОстаток) // для проверки, что оплата не превышает долг
	// это опционально, можно не делать 
ПО
	ОБЩИЕ
```

### Движения

```1c
ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
ВыборкаОбщийИтог.Следующий(); 

// проверка, что оплата не превышает долг	
Разница = Окр(СуммаОплатыРуб - ВыборкаОбщийИтог.СуммаДолгаРуб, 2);
Если Разница > 0 Тогда
	Отказ = Истина; 
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("БУ: оплата контрагента превышает долг на %1 рублей.",
		Разница);
	Сообщение.Сообщить();
		
	Возврат;
КонецЕсли;

// запоминаем, сколько денег еще доступно для списания	
ОсталосьСписать = СуммаОплатыРуб;
	
Выборка = ВыборкаОбщийИтог.Выбрать();
Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
	СписываемРуб = Мин(ОсталосьСписать, Выборка.СуммаДолгаРуб);
	// НЕ делаем проверку полного списания по договору,
	// так как у нас есть курсовая разница
	СписываемВал = Окр(СписываемРуб / Выборка.Курс, 2); // с округлением

	// если не покрывается даже цент, то не списываем долг	
	Если СписываемВал = 0 Тогда
		Продолжить;
	КонецЕсли;

	// обновляем информацию об оставшейся оплате	
	ОсталосьСписать = ОсталосьСписать - СписываемРуб;
		
	Движение = Движения.Управленческий.Добавить();
	Движение.Период = Дата;
	// дт
	Движение.СчетДт = Счета.Касса;
	// кт 
	Движение.СчетКт = Счета.Покупатели;
	Движение.СубконтоКт[Субконто.Контрагенты] = Контрагент;
	Движение.СубконтоКт[Субконто.Договоры] = Выборка.Договор;
	Движение.ВалютаКт = Выборка.Валюта; // не забываем про валюту и сумму валюты
	Движение.СуммаВалКт = СписываемВал; // для покупателей
	// общее 
	Движение.Сумма = СписываемРуб;
КонецЦикла;
```

### Блокировка

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- 1 элемент блокировки по `1 левому соединению`
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- блокировка 1: 
		- счет
		- контрагент; можно достать из реквизитов

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по счету
ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Покупатели);
// блокировка по контрагенту
ЭлементБлокировки.УстановитьЗначение(Субконто.Контрагенты, Контрагент);
Блокировка.Заблокировать(); 
```

## Обработка

Создаем обработку.

Создаем для нее форму:
- добавляем команду и выносим ее на форму

### Примечание

Здесь будет описан вариант с обработкой (есть еще вариация с документом).

При выполнении дальнейших действий потребуется блокировка регистра бухгалтерии. Чтобы не было ошибки
`Использование блокировки допустимо только внутри транзакции в режиме управляемых блокировок!`, необходимо код обрамить в конструкцию
```1c
НачатьТранзакцию();

Попытка
	// код...
	ЗафиксироватьТранзакцию();
Исключение
	ОтменитьТранзакцию();
КонецПопытки;
```

### Запрос

Запрос похож на запрос из `Приход денег`, только тут нет отбора по контрагентам.

Для вычисления, у кого есть курсовая разница сравним:
- Долг в рублях, записанный в движениях
- Долг в рублях, рассчитанный как `Долг в валюте * Курс` (с округлением)

Если эти значения не совпадают, то образовалась курсовая разница.

```1c
НачатьТранзакцию();
	
	Попытка
		
	// ЗДЕСЬ БУДЕТ БЛОКИРОВКА - позже напишем
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УправленческийОстатки.Субконто1 КАК Контрагент,
	|	УправленческийОстатки.Субконто2 КАК Договор,
	|	УправленческийОстатки.Валюта КАК Валюта,
	|	УправленческийОстатки.СуммаВалОстаток КАК СуммаВалОстаток,
	|	УправленческийОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ втДолги
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет = &СчетПокупатели, &ВидыСубконто, ) КАК УправленческийОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДолги.Контрагент КАК Контрагент,
	|	втДолги.Договор КАК Договор,
	|	втДолги.Валюта КАК Валюта,
	|	ВЫРАЗИТЬ(втДолги.СуммаВалОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК ЧИСЛО(15, 2)) КАК СуммаДолгаРуб,
	|	втДолги.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	втДолги КАК втДолги
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						втДолги.Валюта КАК Валюта
	|					ИЗ
	|						втДолги КАК втДолги)) КАК КурсыВалютСрезПоследних
	|		ПО втДолги.Валюта = КурсыВалютСрезПоследних.Валюта";

	// ВАЖНО:
	// Период - конец месяца
	// данны берем на начало следующего, чтобы не возникло ситуации:
	// - создали документ (дата - 31.10.2025 23:59:59, долг проверяем на 31.10.2025 23:59:59)
	// - пользователь еще раз нажал - получаем движения без учета созданного документа
	//		-> создался документ с дублирующими проводками
	Запрос.УстановитьПараметр("МоментВремени", Период + 1);

	// код с созданием документа
	// напишем позже
		
	ЗафиксироватьТранзакцию();
Исключение
	// код с сообщением пользователю
КонецПопытки;
```

### Движения

```1c
НачатьТранзакцию();
	
	Попытка

	// блокировка - напишем позже

	// запрос

	// создаем документ и заполняем начальными данными
	ДокументОбъект = Документы.Операция.СоздатьДокумент();
	ДокументОбъект.Дата = Период;
	ДокументОбъект.Комментарий = СтрШаблон("Корректировка курсовой разницы. Период - %1.",
		Формат(Период, "ДЛФ=DDT"));

	// создаем набор записей для документа	
	НаборЗаписей = РегистрыБухгалтерии.Управленческий.СоздатьНаборЗаписей();
		
	Пока Выборка.Следующий() Цикл
		// если рублевые долги (оригинальный и рассчитанный) совпадают,
		// то курсовая разница не появилась, обрабатывать это не нужно
		Разница = Выборка.СуммаОстаток - Выборка.СуммаДолгаРуб;
		Если Разница = 0 Тогда
			Продолжить;
		КонецЕсли;

		// добавляем движение	
		Движение = НаборЗаписей.Добавить();
		Движение.Период = Период; 
		// записываем движение в зависимости от курсовой разницы
		Если Разница < 0 Тогда
			// дт
			Движение.СчетДт = Счета.Покупатели;
			Движение.СубконтоДт[Субконто.Контрагенты] = Выборка.Контрагент;
			Движение.СубконтоДт[Субконто.Договоры] = Выборка.Договор;
			Движение.ВалютаДт = Выборка.Валюта; 
			// кт
			Движение.СчетКт = Счета.ПрибылиУбытки;
			// общее 
			Движение.Сумма = -Разница;
		Иначе 
			// дт
			Движение.СчетДт = Счета.ПрибылиУбытки;
			// кт 
			Движение.СчетКт = Счета.Покупатели;
			Движение.СубконтоКт[Субконто.Контрагенты] = Выборка.Контрагент;
			Движение.СубконтоКт[Субконто.Договоры] = Выборка.Договор;
			Движение.ВалютаКт = Выборка.Валюта;
			// общее 
			Движение.Сумма = Разница;			
		КонецЕсли;
	КонецЦикла; 

	// записываем документ	
	ДокументОбъект.Записать();

	// после записи можем установить отбор регистратора по ссылке	
	НаборЗаписей.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка); 
	НаборЗаписей.Записать(); // записываем движения
		
	ЗафиксироватьТранзакцию();
Исключение
	// код с сообщением пользователю
КонецПопытки;	
```

### Блокировка

На основе запроса делаем блокировку:
- 1 элемент блокировки по `1 левому соединению`
- объект блокировки: регистр бухгалтерии `Управленческий`
- отборы блокировки:
	- блокировка 1: 
		- счет

```1c
Попытка
		
	// блокировка
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Счет", Счета.Покупатели);
	Блокировка.Заблокировать(); 

	// далее запрос и движения

	ЗафиксироватьТранзакцию();
Исключение
	// код с сообщением пользователю
КонецПопытки;	
```

## Отчет

Обязательно указываем:
- Периодичность: `Регистратор`
	- ставим, чтобы получить поле Регистратор
- МетодДополнения: `Движения`
	- если оставить по умолчанию или поставить ДвиженияИГраницыПериода, то будет неверный отчет: у половины полей не будет регистратора


```1c
ВЫБРАТЬ
	УправленческийОстаткиИОбороты.Субконто1 КАК Контрагент,
	УправленческийОстаткиИОбороты.Субконто2 КАК Договор,
	// делаем ВЫРАЗИТЬ(), чтобы получить именно договор
	ВЫРАЗИТЬ(УправленческийОстаткиИОбороты.Субконто2 КАК Справочник.Договоры).Валюта КАК Валюта,
	УправленческийОстаткиИОбороты.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	УправленческийОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
	УправленческийОстаткиИОбороты.СуммаОборот КАК СуммаОборот,
	УправленческийОстаткиИОбороты.СуммаВалНачальныйОстаток КАК СуммаВалНачальныйОстаток,
	УправленческийОстаткиИОбороты.СуммаВалКонечныйОстаток КАК СуммаВалКонечныйОстаток,
	УправленческийОстаткиИОбороты.СуммаВалОборот КАК СуммаВалОборот,
	УправленческийОстаткиИОбороты.Регистратор КАК Документ
ИЗ
	РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, 
		Регистратор, // Периодичность
		Движения, 	// МетодДополнения
		Счет = &СчетПокупатели, &ВидыСубконто, ) КАК УправленческийОстаткиИОбороты
```