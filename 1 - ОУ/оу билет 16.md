# Билет 16 (занятие 34)

Компания занимается оптовой торговлей. Отгрузка товара осуществляется по предоплате. Оплата оформляется документом «Приход денег», а отпуск товара документом «Расходная накладная».

Для постоянных покупателей может быть предоставлен кредит на некоторый срок. Срок и размер кредита определяется для каждого контрагента индивидуально. В том случае, если сумма накладной меньше суммы предоставленного ему кредита, то может быть произведена отгрузка товаров без поступления денег. Далее, пока не превышен размер кредита, могут быть оформлены еще накладные, но только пока самая первая неоплаченная накладная не будет просрочена больше, чем на срок кредита. Например, если покупателю предоставлен кредит на 1000 и 5 дней, то взаимоотношения с ним выглядят следующим образом: никаких задолженностей нет, 1-го числа происходит отгрузка на сумму 600 и 2-го числа оплата на сумму 100, тогда 3-го числа можно произвести еще отгрузку, но на сумму не более 500, пока не превышена сумма кредита. 8-го числа отгрузок не может быть, поскольку превышен срок кредита. 

При поступлении денег гасятся задолженности по накладным, начиная с самой первой недоплаченной накладной. Авансов и переплат быть не может.


Складской учет товаров не ведется.

Сформировать отчет по взаиморасчетам с покупателем за произвольный период.

![report](/1%20-%20ОУ/media/ticket-16-report.png)

## Примечание

Здесь кривое условие. Судя по отчету, все-таки сначала записывается расходная накладная, а потом оплата.

Если наоборот, то условие про отсутсвие авансов не соблюдается.

## Доступные кредиты

Создаем регистр сведений для информации по доступным кредитам:
- Непериодический
- Измерения:
    - Контрагент
    - Доступен до
- Ресурсы
    - Сумма кредита


## Регистры накопления

### РН Взаиморасчеты

- Измерения:
    - Контрагент
    - Партия
- Ресурсы:
    - Сумма


## Расходная накладная

Несмотря на то, что здесь мы записываем приход, будет блокировка.

Связано это с тем, что приход:
- не всегда оформляется (есть условия отказа)
- доступность прихода зависит от долга; если он скореектируется, пока идет просчет документа, то получится ошибочная ситуация

### Флаги

```
Движения.Взаиморасчеты.Записывать = Истина;
Движения.Взаиморасчеты.Записать();
Движения.Взаиморасчеты.Записывать = Истина;
```

### Запрос

В запросе необходимо получить информацию:
- о долгах контрагента
- о том, какая сумма кредита доступна контрагенту

Доступный долг надо брать:
- актуальный на дату
- у которого срок годности не истек

```1c
// долги по контрагенту
ВЫБРАТЬ
	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
ИЗ
	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ ПЕРВЫЕ 1 // самая последняя запись
	ДоступныеКредитыСрезПоследних.СуммаКредита КАК СуммаКредита
ИЗ
	РегистрСведений.ДоступныеКредиты.СрезПоследних(
			&КонецДня, 
            // отбор по контрагенту
			Контрагент = &Контрагент 
                // только те записи, которые подходят по сроку
				И ДоступенДо > &КонецДня) КАК ДоступныеКредитыСрезПоследних

УПОРЯДОЧИТЬ ПО
	ДоступныеКредитыСрезПоследних.Период
```

### Формирование движений

```1c
// получаем результаты обоих запросов
РезультатЗапроса = Запрос.ВыполнитьПакет();
РЗДолги = РезультатЗапроса[0];
РЗДоступныйКредит = РезультатЗапроса[1];

// получение суммы долга	
Если РЗДолги.Пустой() Тогда
	ДолгКонтрагента = 0;
Иначе
	ВыборкаДолг = РЗДолги.Выбрать();
	ВыборкаДолг.Следующий();
	ДолгКонтрагента = ВыборкаДолг.СуммаОстаток;
КонецЕсли; 

// получение суммы доступного кредита	
Если РЗДоступныйКредит.Пустой() Тогда
	ДоступныйКредит = 0;
Иначе 
	ВыборкаКредит = РЗДоступныйКредит.Выбрать();
	ВыборкаКредит.Следующий();
	ДоступныйКредит = ВыборкаКредит.СуммаКредита;
КонецЕсли; 

// проверяем, можем ли провести накладную	
Если ДолгКонтрагента + СуммаПоДокументу > ДоступныйКредит Тогда
	Отказ = Истина;
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("Недостаточно денег: " + 
		"%2 (актуальный долг), %3 (доступный кредит)",
	СуммаПоДокументу, ДолгКонтрагента, ДоступныйКредит);
    Сообщение.Сообщить();
		
	Возврат
КонецЕсли; 

// если все в порядке, то записываем долг
Движение = Движения.Взаиморасчеты.ДобавитьПриход();
Движение.Период = Дата;
Движение.Контрагент = Контрагент;
Движение.Партия = Ссылка;
Движение.Сумма = СуммаПоДокументу;
```

### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `Взаиморасчеты`
- отборы блокировки:
	- контрагент

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по контрагенту
ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
Блокировка.Заблокировать();
```


## Приход денег

### Флаги

```
Движения.Взаиморасчеты.Записывать = Истина;
Движения.Взаиморасчеты.Записать();
Движения.Взаиморасчеты.Записывать = Истина;
```

### Запрос

Получаем долги по партиям.

С помощью общих итогов получаем общую сумму долга.

```1c
ВЫБРАТЬ
	ВзаиморасчетыОстатки.Партия КАК Партия,
	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
ИЗ
	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки

УПОРЯДОЧИТЬ ПО
	ВзаиморасчетыОстатки.Партия.МоментВремени
ИТОГИ
	СУММА(СуммаОстаток)
ПО
	ОБЩИЕ
```

### Формирование движений

```1c
// если нет записей, то у долга нет контрагентов
РезультатЗапроса = Запрос.Выполнить();
Если РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "У контрагента нет долга.";
	Сообщение.Сообщить();
		
	Возврат;
КонецЕсли;
	
ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
ВыборкаОбщийИтог.Следующий();		// Общий итог

// если сумма долга больше, что сумма оплаты, то не проводим
Разница = СуммаПредоплаты - ВыборкаОбщийИтог.СуммаОстаток;
Если Разница > 0 Тогда 
	Отказ = Истина;
		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Оплата превышает сумму долга на " + Разница;
	Сообщение.Сообщить();
		
	Возврат;
КОнецЕсли;

// запоминаем, сколько денег доступно	
ДоступноКСписанию = СуммаПредоплаты;
	
Выборка = ВыборкаОбщийИтог.Выбрать();
// пока есть, что списывать
Пока Выборка.Следующий() И ДоступноКСписанию > 0 Цикл
    // определяем, сколько списываем с текущей партии
	Списываем = Мин(ДоступноКСписанию, Выборка.СуммаОстаток);
	ДоступноКСписанию = ДоступноКСписанию - Списываем;

    // уменьшаем долг	
	Движение = Движения.Взаиморасчеты.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Партия = Выборка.Партия;
	Движение.Сумма = Списываем;
КонецЦикла;
```

### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `Взаиморасчеты`
- отборы блокировки:
	- контрагент

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	ВзаиморасчетыОстаткиИОбороты.Контрагент КАК Контрагент,
	ВзаиморасчетыОстаткиИОбороты.Партия КАК Партия,
	ВзаиморасчетыОстаткиИОбороты.СуммаНачальныйОстаток КАК НачальныйОстаток,
	ВзаиморасчетыОстаткиИОбороты.СуммаПриход КАК Отгрузка,
	ВзаиморасчетыОстаткиИОбороты.СуммаРасход КАК Оплата,
	ВзаиморасчетыОстаткиИОбороты.СуммаКонечныйОстаток КАК КонечныйОстаток
ИЗ
	РегистрНакопления.Взаиморасчеты.ОстаткиИОбороты КАК ВзаиморасчетыОстаткиИОбороты
```
