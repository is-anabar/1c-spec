# Билет 11 (занятие 25)

Организация занимается торговлей скоропортящимися продуктами. Доставка товара покупателю отражается документом «Расходная накладная», оплата покупателя - документом «Приход денег». При получении товара покупатель по каждой накладной составляет акт о фактическом поступлении товара, за вычетом испортившегося при транспортировке. Данная информация также указывается в документе «Расходная накладная».

Оплата покупателя поступает общей суммой по нескольким накладным (накладные указываются в табличной части, сумма в шапке). При проведении документа оплаты должны автоматически распределяться по накладным, начиная с самой ранней недооплаченной, причем оплаты должны распределяться с учетом фактической (реально получено согласно акту) задолженности покупателя. Следует считать, что переплат и авансов нет.

Необходимо создать отчеты о состоянии отгрузок товаров за период.

![report](/1%20-%20ОУ/media/ticket-11-report.png)


## Анализ

Здесь нет приходной накладной, поэтому хранить остатки по товарам не нужно. Но, чтобы получить отчет, нужно хранить информацию о номенклатуре, количестве и суммах. Для этого подойдет `оборотный` регистр накопления.

Для хранения долгов будет служить остаточный регистр.

## Регистры накопления

### РН Отгрузки

- Оборотный
- Измерения:
	- Номенклатура
- Ресурсы:
	- Количество отправлено
	- Сумма отправлено
	- Количество получено
	- Сумма получено

## РН Долги контрагентов

- Остаточный
- Измерения:
	- Контрагент
	- Партия (расходная накладная)
- Ресурсы:
	- Сумма


## Расходная накладная

Изменяем табличную часть, чтобы получилось:
- Номенклатура
- Количество отправлено
- Сумма отправлено
- Количество получено

### Флаги

```1c
// для записи движений
Движения.Отгрузки.Записывать = Истина;
Движения.ДолгиКонтрагентов.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.КоличествоОтправлено) КАК КоличествоОтправлено,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.СуммаОтправлено) КАК СуммаОтправлено,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.КоличествоПолучено) КАК КоличествоПолучено,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Представление КАК НоменклатураПредставление
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Представление
```

### Формирование движений

```1c
// для рассчета долга
СуммаИтого = 0;
	
Пока Выборка.Следующий() Цикл
	// проверка не обязательна
	Разница = Выборка.КоличествоПолучено - Выборка.КоличествоОтправлено;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Для товара %1 получено больше, чем отправлено, на %2 шт.",
			Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// рассчитываем себестоимость полученных товаров	
	Если Выборка.КоличествоОтправлено = Выборка.КоличествоПолучено Тогда
		Себестоимость = Выборка.СуммаОтправлено;
	Иначе 
		Себестоимость = Выборка.КоличествоПолучено * Выборка.СуммаОтправлено / Выборка.КоличествоОтправлено;
	КонецЕсли; 

	// записываем информацию 	
	Движение = Движения.Отгрузки.Добавить();
	Движение.Период = Дата;
	ЗаполнитьЗначенияСвойств(Движение, Выборка);
	Движение.СуммаПолучено = Себестоимость;

	// обновляем долг	
	СуммаИтого = СуммаИтого + Движение.СуммаПолучено;
КонецЦикла;

// увеличение долга контрагента	
Движение = Движения.ДолгиКонтрагентов.ДобавитьПриход();
Движение.Период = Дата;
Движение.Контрагент = Контрагент;
Движение.Партия = Ссылка;
Движение.Сумма = СуммаИтого
```


## Приход денег

Создаем документ:
- Реквизиты:
	- Контрагент
	- Сумма
- Табличные части:
	- Основная:
		- Партии

### Флаги

```1c
Движения.ДолгиКонтрагентов.Записывать = Истина;
Движения.ДолгиКонтрагентов.Записать();
Движения.ДолгиКонтрагентов.Записывать = Истина;
```

### Запрос

```1c
// получаем список партий из документа
ВЫБРАТЬ РАЗЛИЧНЫЕ
	ПриходДенегОсновная.Партия КАК Партия
ПОМЕСТИТЬ втСписокПартий
ИЗ
	Документ.ПриходДенег.Основная КАК ПриходДенегОсновная
ГДЕ
	ПриходДенегОсновная.Ссылка = &Ссылка

ИНДЕКСИРОВАТЬ ПО
	Партия // индексируем для отборов и соединений
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втСписокПартий.Партия КАК Партия,
	ЕСТЬNULL(ДолгиКонтрагентовОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втСписокПартий КАК втСписокПартий
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДолгиКонтрагентов.Остатки(
				&МоментВремени,
				Контрагент = &Контрагент // отбор по контрагенту
					И Партия В // отбор по партиям
						(ВЫБРАТЬ
							втСписокПартий.Партия КАК Партия
						ИЗ
							втСписокПартий КАК втСписокПартий)) КАК ДолгиКонтрагентовОстатки
		ПО втСписокПартий.Партия = ДолгиКонтрагентовОстатки.Партия

УПОРЯДОЧИТЬ ПО // сортируем от саммых ранних по времени
	ДолгиКонтрагентовОстатки.Партия.МоментВремени
```


### Формирование движений

```1c
ОсталосьОплаты = Сумма;

// пока осталась сумма к списанию	
Пока Выборка.Следующий() И ОсталосьОплаты > 0 Цикл
	// опрделяем, сколько можем покрыть для текущей партии
	СписываемДолг = Мин(Выборка.СуммаОстаток, ОсталосьОплаты);
	// обновляем оставшуюся сумму
	ОсталосьОплаты = ОсталосьОплаты - СписываемДолг;
		
	Движение = Движения.ДолгиКонтрагентов.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Партия = Выборка.Партия;
	Движение.Сумма = СписываемДолг;
КонецЦикла;
```

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ДолгиКонтрагентов`
- отборы блокировки:
	- контрагент; берем из реквизитов
	- список партий; можно достать из табличной части

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДолгиКонтрагентов");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по контрагенту
ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
// отбор по списку партий
ЭлементБлокировки.ИсточникДанных = Основная;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия", "Партия");
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	ОтгрузкиОбороты.Номенклатура КАК Номенклатура,
	ОтгрузкиОбороты.КоличествоОтправленоОборот КАК КоличествоОтправлено,
	ОтгрузкиОбороты.СуммаОтправленоОборот КАК СуммаОтправлено,
	ОтгрузкиОбороты.КоличествоПолученоОборот КАК КоличествоПолучено,
	ОтгрузкиОбороты.СуммаПолученоОборот КАК СуммаПолучено
ИЗ
	РегистрНакопления.Отгрузки.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ОтгрузкиОбороты
```
