# Билет 4 (занятие 14)

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа - «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Учет товаров ведется в разрезе складов. Каждый из складов имеет свой приоритет, который может меняться не чаще чем 1 раз в месяц.

При проведении расходной накладной необходимо в первую очередь контролировать хватает ли товара вообще. Если нет - выдавать соответствующее предупреждение с указанием количества нехватки и не позволять проводить документ.

Списание себестоимости товаров должно быть организовано по складам, в зависимости от текущего значения их приоритета и выбранного в документе склада. В первую очередь товар списывается со склада, указанного в шапке документа, далее со склада с минимальным приоритетом, потом со следующего склада с большим приоритетом и т.д. пока не спишется все необходимое количество.

Себестоимость товаров рассчитывается как средняя по складу.

Поступление товара происходит на один выбранный пользователем в документе «Приходная накладная» склад.

Необходимо построить отчет по движениям товаров за период по количеству и сумме.

![ticket 4 report](/1%20-%20ОУ/media/ticket-4-report.png)


## Товары и услуги

В справочнике `Номенклатура` добавляем реквизит `ВидНоменклатуры` - перечисление `Виды номенклатуры`.

Для документа `Приходная накладная` открываем настройки реквизита `Номенклатура` табличной части `СписокНоменклатуры`. В настройках указываем `Параметры выбора`:
- Отбор.ВидНоменклатуры: Товар


## Склады и приоритет

<!-- Похоже на учетную политику -->

### Справочник

Создаем справочник `Склады`

### Хранение приоритетов

Создаем регистр сведений `ПриоритетыСкладов`:
- периодичность: месяц (из условия)
- измерения:
	- склад
- ресурсы:
	- приоритет: число

## Настройка регистра накопления `ОстаткиНоменклатуры`

Добавляем измерение `Склад`.

## Приходная накладная

Добавляем реквизит `Склад`.

Для формирования движений можно воспользоваться конструктором движений.

## Расходная накладная

Добавляем реквизит `Склад`.

### Списание товаров

1. Очищаем движения (если были) и устанавливаем флаг на запись:
	```1c
	// очищаем движения - записываем пустые
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	// устанавливаем флажок, чтобы новые движения записывались
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	```

2. Формируем запрос
	Алгоритм запроса будет примерно следующий:
	- группировка табличной части по номенклатуре - избавляемся от дублей строк - `втТовары`
	- получение информации об остатках и приоритетах:
		- достаем информацию о том, какую номенклатуру и сколько потребовали в документе
		- достаем из `ОстаткиНоменклатуры.Остатки` склад, остаток на складах, сумму товаров, соединяясь по:
			- отбор в виртуальной таблице: по номенклатуре (т.к. нужны данные по всем складам)
			- левое соединение по номенклатуре
		- достаем из`ПриоритетСкладов` данные о приоритете, соединяясь с `ОстаткиНоменклатуры.Остатки` по:
			- склад
		- делаем итог по `Количество` и `КоличествоОстаток`

	Итоговый запрос примерно такой: 
	```1c
	// группировка ТЧ из документа
	ВЫБРАТЬ
		РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	ПОМЕСТИТЬ втТовары
	ИЗ
		Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	ГДЕ
		РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Товар

	СГРУППИРОВАТЬ ПО
		РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

	// индексируем по номенклатуре, т.к. будет выполняться соединение в дальнейшем
	ИНДЕКСИРОВАТЬ ПО
		Номенклатура
	;

	////////////////////////////////////////////////////////////////////////////////
	ВЫБРАТЬ
		// данные из документа
		втТовары.Номенклатура КАК Номенклатура,
		втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		втТовары.Количество КАК Количество,
		// данные из регистра накопления
		ОстаткиНоменклатурыОстатки.Склад КАК Склад,
		ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		// определение приоритета: 
		// - если склад из шапки, то значение самое маленькое
		// - если другой склад, то берем приоритет из регистра сведений
		ВЫБОР
			КОГДА ОстаткиНоменклатурыОстатки.Склад = &Склад
				ТОГДА -1
			ИНАЧЕ ЕСТЬNULL(ПриоритетСкладовСрезПоследних.Приоритет, 99999)
		КОНЕЦ КАК Приоритет
	ИЗ
		втТовары КАК втТовары
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
					&МоментВремени,
					Номенклатура В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПриоритетСкладов.СрезПоследних(&МоментВремени, ) КАК ПриоритетСкладовСрезПоследних
				ПО ОстаткиНоменклатурыОстатки.Склад = ПриоритетСкладовСрезПоследних.Склад
			ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура

	УПОРЯДОЧИТЬ ПО
		Приоритет
	// для того, чтобы определить, хватает ли товара в целом
	ИТОГИ
		// максимум, т.к. это данные из документа
		МАКСИМУМ(Количество),
		// сумма, т.к. количество по всем складам
		СУММА(КоличествоОстаток)
	ПО
		Номенклатура
	``` 

3. Списание товаров

	```1c
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		// сверяемся, что нам в целом хватает номенклатуры:
		// количество на всех скаладах должно быть больше, чем просят в документе
		Разница = ВыборкаНоменклатура.КоличествоОстаток - ВыборкаНоменклатура.Количество;
		Если Разница < 0 Тогда
			Отказ = Истина; 
			
			Сообщение = Новый СообщениеПользователю;
			// важно, чтобы была именно НоменклатураПредставление
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2.", 
				ВыборкаНоменклатура.НоменклатураПредставление,
				-Разница);
			Сообщение.Сообщить();
		КонецЕсли;	
		
		// не формируем движения, если был отказ
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		Выборка = ВыборкаНоменклатура.Выбрать();
		
		// запоминаем, сколько всего нужно списать по текущей номеклатуре
		КоличествоСписываем = ВыборкаНоменклатура.Количество; 
		
		// условие КоличествоСписываем > 0 будет работать в случаях, если:
		// списываем 10
		// всего у нас:
		//		- 5 штук в первой строке (спишем 5)
		//		- 7 штук во второй строке (спишем 5, больше в документе не требовали)
		//		- 3 штуки в третьей строке (здесь уже не нужно проверять, списали, сколько хотели)
		Пока Выборка.Следующий() И КоличествоСписываем > 0 Цикл
			// смотрим, сколько списываем с текущей строки:
			// 1. всего надо списать 30, остаток в текущей строке - 10. списываем 10
			// 2. всего надо списать 15, остаток в текущей строке - 20. списываем 15
			Списываем = Мин(КоличествоСписываем, Выборка.КоличествоОстаток);
			
			// расчет себестоимости с проверкой последнего списания
			Если Списываем = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СуммаОстаток;
			Иначе 
				Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток; 
			КонецЕсли; 
			
			// обновляем информацию о том, сколько нужно списать по текущей позиции
			КоличествоСписываем = КоличествоСписываем - Списываем;
			
			// добавляем движение
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Склад = Выборка.Склад;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Количество = Списываем;
			Движение.Сумма = Себестоимость;
		КонецЦикла;
	КонецЦикла;
	```


4. Устанавливаем блокировку

	Блокировка устанавливается `ПЕРЕД` запросом. 

	На основе запроса делаем блокировку:
	- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
	- отборы блокировки:
		- список номенклатуры; можно достать из документа

	```1c
	Блокировка = Новый БлокировкаДанных;
	// объект блокировки
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры"); 
	ЭлементБлокировки.Режим= РежимБлокировкиДанных.Исключительный;
	// отбор - данные из ТЧ документа 
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	```