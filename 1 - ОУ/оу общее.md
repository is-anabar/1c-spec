# Общие моменты по оперативному учету

- [Отбор по виду номенклатуры](#filter-items)
- [Cтруктура Расходной накладной (старая методика)](#consumption-old)
- [Cтруктура Расходной накладной (новая методика)](#consumption-new)



## <a name="filter-items"></a>Отбор по виду номенклатуры

Ситуация: в документе `Расходная накладная` можно указывать только `Товары`, а `Услуги` - нельзя.

### Настройка номенклатуры

Добавляем реквизит `ВидНоменклатуры`. Тип - перечисление `ВидыНоменклатуры`

### Настройка документа

Устанавливаем в ТЧ для номенклатуры `Параметры выбора` значение `Отбор.ВидНоменклатуры(Товар)`.


## <a name="consumption-old"></a>Cтруктура Расходной накладной (старая методика)


### Когда использовать старую методику

- В документе есть `расчет себестоимости`
- В документе `указывается только номенклатура`, но списывается по `номенклатура + партия/склад и пр.` (есть разделение списания по каким-либо критериям)

### Общий план

Списание имеет общую структуру:
- Установка флага `Записывать` для движений
- Блокировка
- Запрос
- Формирование движений

### Запись движений

```1c
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Записать();
	
Движения.ОстаткиНоменклатуры.Записывать = Истина;
```


### Блокировка

Блокировку лучше всего делать после запроса, но она имеет общую структуру:
- Блокируем регистр накопления, с которого `списываем` что-то
- Создаем `элементы блокировки` - **по одной на каждое левое соединение** с регистром накопления
	> С регистрами сведений блокировку делать не нужно
- В качестве отборов для элемента блокировки устанавливаем отборы виртуальной таблицы регистра накопления.

**Пример 1**. Запрос имеет следующий вид:
```1c
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Товар

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
	втТовары.Количество КАК Количество,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				Номенклатура В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары) И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
```

Здесь:
- Одно левое соединение с регистром `РегистрНакопления.ОстаткиНоменклатуры`
- В отборах виртуальной таблицы используются:
	- номенклатура из `СписокНоменклатуры` (СписокНоменклатуры группируется и становится `втТовары`)
	```1c
	Номенклатура В
		(ВЫБРАТЬ
			втТовары.Номенклатура КАК Номенклатура
		ИЗ
			втТовары КАК втТовары)
	```
	- склад 
	```1c
	Склад = &Склад
	```

Таким образом получаем блокировку:
```1c
Блокировка = Новый БлокировкаДанных;
// блокировка для левого соединения
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры"); 
ЭлементБлокировки.Режим= РежимБлокировкиДанных.Исключительный; 
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
// блокировка по складу
ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
Блокировка.Заблокировать();
```


**Пример 2**. Запрос имеет следующий вид:

<!-- ЗАПИСАТЬ ДЛЯ ДВУХ ЛЕВЫХ СОЕДИНЕНИЙ -->


### Запрос

Общая структура запроса следующая:
- Группируем ТЧ `Список номенклатуры`: избавляемся от дубликатов строк + создаем индексы
- Соединеняемся с регистром накопления (можно еще с регистром сведений)

Чаще всего будет схема: списываем с любых складов/партий.

Примерно запрос будет следующим:

```1c
// группировка ТЧ из документа
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

// индексируем по номенклатуре, т.к. будет выполняться соединение в дальнейшем
ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	// данные из регистра накопления
	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	ИЗ
		втТовары КАК втТовары
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
					&МоментВремени,
					Номенклатура В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки

	// для того, чтобы определить, хватает ли товара в целом
	ИТОГИ
		// максимум, т.к. это данные из документа
		МАКСИМУМ(Количество),
		// сумма, т.к. количество по всем складам
		СУММА(КоличествоОстаток)
	ПО
		Номенклатура
```


### Формирование движений

Общий алгоритм списания такой:
- Проверяем, что в остатках хватает номенклатуры
	- Если нет, то `Отказ`
- Определяем, сколько всего номенклатуры надо списать. Проходим по всем записям текущей номенклатуры:
	- Если есть, что списывать, то продолжаем
	- Проверяем на последнее списание для текущей строки
	- Обновляем количество номенклатуры, которое надо списать со следующих складов/партий
	- Формирование движений

```1c
// Выборка по итогам номенклатуры
ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

// Обход номенклатуры по складам/партиям	
Пока ВыборкаНоменклатура.Следующий() Цикл
	// сверяемся, что нам в целом хватает номенклатуры:
	// количество на всех скаладах должно быть больше, чем просят в документе
	Разница = ВыборкаНоменклатура.КоличествоОстаток - ВыборкаНоменклатура.Количество;
	Если Разница < 0 Тогда
		Отказ = Истина; 
			
		Сообщение = Новый СообщениеПользователю;
		// важно, чтобы была именно НоменклатураПредставление
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2.", 
			ВыборкаНоменклатура.НоменклатураПредставление,
			-Разница);
		Сообщение.Сообщить();
	КонецЕсли;	
		
	// не формируем движения, если был отказ
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	Выборка = ВыборкаНоменклатура.Выбрать();
		
	// запоминаем, сколько всего нужно списать по текущей номеклатуре
	КоличествоСписываем = ВыборкаНоменклатура.Количество; 
		
	// условие КоличествоСписываем > 0 будет работать в случаях, если:
	// списываем 10
	// всего у нас:
	//		- 5 штук в первой строке (спишем 5)
	//		- 7 штук во второй строке (спишем 5, больше в документе не требовали)
	//		- 3 штуки в третьей строке (здесь уже не нужно проверять, списали, сколько хотели)
	Пока Выборка.Следующий() И КоличествоСписываем > 0 Цикл
		// смотрим, сколько списываем с текущей строки:
		// 1. всего надо списать 30, остаток в текущей строке - 10. списываем 10
		// 2. всего надо списать 15, остаток в текущей строке - 20. списываем 15
		Списываем = Мин(КоличествоСписываем, Выборка.КоличествоОстаток);
			
		// расчет себестоимости с проверкой последнего списания в текущей строке
		// т.е. списываем строку полностью
		Если Списываем = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе 
			Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток; 
		КонецЕсли; 
			
		// обновляем информацию о том, сколько нужно списать по текущей позиции
		КоличествоСписываем = КоличествоСписываем - Списываем;
			
		// добавляем движение
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Склад = Выборка.Склад;
		Движение.Номенклатура = Выборка.Номенклатура;
		// Списываем столько, сколько доступно по текущему складу/партии
		Движение.Количество = Списываем;
		Движение.Сумма = Себестоимость;
	КонецЦикла;
КонецЦикла;
```

## <a name="consumption-new"></a>Cтруктура Расходной накладной (новая методика)


### Когда использовать старую методику

Можно сразу внести записи из табличной части по всем полям. Чаще всего такое бывает, когда:
- в документе указывается только номенклатура, и списывается только номеклатура (без распределния по партиям/скалада)
- когда нет расчета себестоимости
- для авансов и подобных денежных проверок


### Общий план

Списание имеет общую структуру:
- Запрос и запись данных
- Проверка на отрицательные остатки


### Запрос и запись данных

Для реализации неоходимо:
- Сформировать запрос 
- Записать движения

Запрос имеет следующую структуру:
- предварительно создаем `менеджер временных таблиц` и присоединяем его к запросу; он помогает `запоминать временные таблицы`
- группируем данные по ТЧ и записываем во временную таблицу (пригодится для других запросов)
- выводим данные

```1c	
// Необходимо для того, чтобы новые запросы знали
// о временных таблицах
МВТ = Новый МенеджерВременныхТаблиц;
	
Запрос = Новый Запрос; 
// Устанавливаем менеджер для "запоминания виртуальных таблиц"
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	// группируем для избавления от дублей
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Будет использовано для формирования списания остатков
	|// и блокировки при расчете себестоимости
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Количество КАК Количество
	|ИЗ
	|	втТовары КАК втТовары";

```

Записываем движения. Блокровки здесь напрямую нет, но обязательно надо указывать
`Движения.<Наименование>.БлокироватьДляИзменения = Истина;`

```1c	
// РезультатЗапросаТЧ будет использовано позже,
// для блокировки при расчете себестоимости
РезультатЗапросаТЧ = Запрос.Выполнить();	
Выборка = РезультатЗапросаТЧ.Выбрать();

// флаг
Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
Пока Выборка.Следующий() Цикл
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Номенклатура = Выборка.Номенклатура;
	Движение.Количество = Выборка.Количество;
КонецЦикла; 

// запись остатков	
Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
Движения.ОстаткиНоменклатуры.Записать();
```

### Проверка отрицательных остатков

После того, как списали номенклатуру, можно проверить остатки на отрицательные значения. Если они есть, то номенклатуры недостаточно, необходимо отменить проведение.

С помощью `менеджера временных таблиц` можно взять данные из временных таблиц другого запроса.

Очень важный момент: для проверки отрицательных остатков их надо брать на `Новый Граница(МоментВремени(), ВидГраницы.Включая)`.
- записываем движения мы на `МоментВремени()`
- а проверку, того, что мы списали, нужно делать ровно после `МоментВремени()`

```1c
Запрос = Новый Запрос;
// Используем МВТ, чтобы запрос знал о втТовары
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) * -1 КАК ОстатокНаСкладе
	|ИЗ
	|	втТовары КАК втТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|		&Граница,
	|		Номенклатура В
	|			(ВЫБРАТЬ
	|				втТовары.Номенклатура КАК Номенклатура
	|			ИЗ
	|				втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
	|	ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) < 0";

// Обязательно использовать
// Новый Граница(МоментВремени(), ВидГраницы.Включая)
// так как списание сформировано на МоментВремени(),
// а остатки необходимо получить сразу после этого момента
Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
Запрос.УстановитьПараметр("Склад", Склад);

// Если есть хоть одна запись, то номенклатуры недостаточно
// Необходимо отменить проведение	
РезультатЗапроса = Запрос.Выполнить();
Если НЕ РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
	
	Выборка = РезультатЗапроса.Выбрать();	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
		Выборка.НоменклатураПредставление, Выборка.ОстатокНаСкладе);
		Сообщение.Сообщить();	
	КонецЦикла; 
	
	Возврат;
КонецЕсли;
```


## Авансы


## Валюты


## блокировка


## запись движений

## общий алгоритм запроса
- группировка табличной части
- соединение с регистром

## использование итогов