# Билет 9 (занятие 21)

Организация занимается оптовой торговлей. Для описания товаров существуют различные характеристики, набор которых составляет свойство. Учет товаров ведется в разрезе свойств этих товаров, причем для каждого товара набор этих свойств может быть произвольный. Например, «ботинки черные 42 размера», «ботинки коричневые 42 размера» и «ботинки коричневые 44 размера утепленные» это один товар «ботинки», но с разными свойствами. «Черные 42 размера» будет являться свойством, а цвет «черный» и размер «42» являются характеристиками.

Поступление товаров отражается документом «Приходная накладная», продажа - «Расходная накладная». И в документе поступления, и в документе списания для каждого товара указывается его свойство.

Механизм свойств и характеристик товаров должен быть реализован с использованием плана видов характеристик.

Складской учет товаров не ведется.

Списание себестоимости товаров должно быть организовано в разрезе свойств.

Необходимо построить отчет о движениях товаров за период по выбранной характеристике в количественном и суммовом выражении.

![report](/1%20-%20ОУ/media/ticket-9-report.png)


## Свойства номенклатуры

Для хранения наборов свойств создаем справочник `Набор свойств`

Пример можно взять [здесь](/0%20-%20общее/03%20как%20добавить%20характеристики%20объектов.md). В качестве объекта для доп. свойств будет выступать `Набор свойств`.

## Регистры накопления

По условию учет по количестве и себестоимости ведется в разрезе номенклатура + набор свойств

### Остатки номенклатуры

- Измерения:
    - Номенклатура
    - Набор свойств
- Ресурсы:
    - Количество
    - Сумма

## Приходная накладная

В табличную часть необходимо добавить колонку `Набор свойств`.

Дляформирования проводок можно воспользоваться конструктором движений.


## Расходная накладная

В табличную часть необходимо добавить колонку `Набор свойств`.

Т.к. учет остатков и учет себестоимости ведется в одних и тех же разрезах, то контроль остатков и рассчет себестоимости будем делать в одной транзакции. Расчеты делаем с помощью `старой методики`.


### Установка флагов

```1c
// очищаем движения - записываем пустые
Движения.ОстаткиНоменклатуры.Записывать = Истина;	
Движения.Записать();	
Движения.ОстаткиНоменклатуры.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
// группировка табличной части
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.НаборСвойств КАК НаборСвойств,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.НаборСвойств

ИНДЕКСИРОВАТЬ ПО // индексируем оба, т.к. будем по ним делать отбор
	Номенклатура,
	НаборСвойств
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
    // данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.НаборСвойств КАК НаборСвойств,
	втТовары.НаборСвойств.Представление КАК НаборСвойствПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
    // данные по остаткам
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				(Номенклатура, НаборСвойств) В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура,
						втТовары.НаборСвойств КАК НаборСвойств
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
			И втТовары.НаборСвойств = ОстаткиНоменклатурыОстатки.НаборСвойств
```

### Формирование движений


```1c
Пока Выборка.Следующий() Цикл
    // проверка, что номенклатуры с заданным свойством хватает
	Разница = Выборка.Количество - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 со свойством %2 в количестве %3",
			Выборка.НоменклатураПредставление, Выборка.НаборСвойствПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

    // расчет зависит от того, списываем мы всю номенклатуру или нет	
	Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
		Себестоимость = Выборка.СуммаОстаток;
	Иначе
		Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток; 
	КонецЕсли;

    // расход	
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Движение.Период = Дата;
	ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, НаборСвойств, Количество");
	Движение.Сумма = Себестоимость;
	КонецЦикла;
```


### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
- отборы блокировки:
	- номенклатура; можно взять из ТЧ
    - набор свойств; можно взять из ТЧ

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по списку номенклатуры и набору свойств
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НаборСвойств", "НаборСвойств")
Блокировка.Заблокировать(); 
```

## Отчет

```1c
ВЫБРАТЬ
	ОстаткиНоменклатурыОстаткиИОбороты.Номенклатура КАК Номенклатура,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоНачальныйОстаток КАК НачальныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоПриход КАК Приход,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоРасход КАК Расход,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоКонечныйОстаток КАК КонечныйОстаток,
	Представление(ДополнительныеСведения.Значение) КАК Харка
ИЗ
	РегистрНакопления.ОстаткиНоменклатуры.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , ) КАК ОстаткиНоменклатурыОстаткиИОбороты
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		ПО ОстаткиНоменклатурыОстаткиИОбороты.НаборСвойств = ДополнительныеСведения.НаборСвойств
			И (ДополнительныеСведения.Свойство = &Свойство)
```



