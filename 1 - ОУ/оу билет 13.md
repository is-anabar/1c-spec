# Билет 13 (занятия 24, 27)

В компании используется оборудование, имеющее ограничения к эксплуатации. Для каждого устройства устанавливается срок годности и срок использования. Срок годности считается с момента приобретения оборудования и указывается в документе поступления, а срок эксплуатации с момента его ввода в эксплуатацию и указывается непосредственно для оборудования и не меняется. Поступление оборудования отражается документом «Приходная накладная». Через некоторое время оборудование вводится в эксплуатацию документом «Ввод в эксплуатацию».

При передаче оборудования в эксплуатацию в первую очередь передастся оборудование, у которого минимальный срок годности. В документе «Ввод в эксплуатацию» указывается перечень и количество передаваемого оборудования. При проведении должна происходить проверка на наличие оборудования и его срок годности. В том случае, если срок годности истек или оборудования недостаточно, документ не проводится и выдается соответствующее сообщение.

Раз в месяц формируется регламентный документ «Выбытие оборудования», который при проведении проверяет эксплуатируемое оборудование и, если оно негодно, то списывает его. При проверке определяется, вышел ли срок эксплуатации устройства с момента его ввода в эксплуатацию. Если срок эксплуатации истек или истек срок годности, то оборудование должно быть списано. Кроме того, этот же документ должен списывать еще не введенное в эксплуатацию оборудование, но срок годности, которого уже истек.

Учет оборудования в разрезе складов не ведется. 

Себестоимость оборудования рассчитывается как средняя.

Необходимо создать отчет о состоянии эксплуатируемого оборудования на выбранную дату.

Состояние оборудования в эксплуатации на 31.01.2010

|Оборудование	|Кол-во	|Сумма	|Оставшийся срок годн.	|Оставшийся срок эксп.|
|---------------|-------|-------|---------------|----|
|Кинокамера	|3	|3 000	|60	|20|
|Портсигар	|2  |400	|500	|520|


## Примечание

Здесь описано выполнение более сложного варианта: себестоимость номенклатуры рассчитывается как общая по всем срокам годности.

## Регистры накопления

Для хранения информации о прибывшем оборудовании необходимо завести остаточный регистр. Для того, чтобы правильно формировать списание (ввод в эксплуатацию), необходимо хранить информацию о сроках годности.

Так как учет цены ведется для всей номенклатуры, без учета сроков годности, необходимо завести отдельный регистр накопления для количества и цены прибывшей номенклатуры.

> Если хранить цену в остатках по сроку годности, то для каких=то номенклатур могут образоваться отрицательные суммы при нулевом количестве.
>
> Когда появятся отрицательные суммы:
> - Чайник - до 01.11.2025 - 1 штука - 150 рублей
> - Чайник - до 01.12.2025 - 1 штука - 250 рублей
>
> Средняя цена - 200 рублей.
> При списании одного чайника получаем:
> - Чайник - до 01.11.2025 - 0 штук - -50 рублей
> - Чайник - до 01.12.2025 - 1 штука - 250 рублей
>
> Аналогично может образоваться положительная сумма при нулевом количестве, если изначально было:
> - Чайник - до 01.11.2025 - 1 штука - 250 рублей
> - Чайник - до 01.12.2025 - 1 штука - 150 рублей

Для хранения информации об оборудовании в эксплуатации создаем отдельный регистр накопления. Чтобы правильно сформировать выбытие, а также получить данные по отчету, нужно хранить информацию дополнительно о сроках годности и эксплуатации, а также количестве и цене.

### РН Номенклатура по срокам годности

- Остаточный
- Измерения:
	- Номенклатура
	- Срок годности
- Ресурсы:
	- Количество

### РН Себестоимость номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
- Ресурсы:
	- Количество
	- Сумма

### РН Номенклатура в эксплуатации

- Остаточный
- Измерения:
	- Номенклатура
	- Срок годности
	- Срок эксплуатации
- Ресурсы:
	- Количество
	- Сумма



## Номенклатура

Добавляем  реквизит `Срок эксплуатации (в днях)`




## Приходная накладная

Добавляем в табличную часть реквизит `Срок годности`.

Документ делает приход по регистрам:
- Номенклатура по срокам годности
- Себестоимость номенклактуры

Движения можно сделать с помощью конструктора движений.

## Ввод в эксплуатацию

При вводе необходимо:
- Списать:
	- Номенклатура по срокам
	- Себестоимость
- Сделать приход:
	- Номенклатура в эксплуатации

### Флаги

```1c
// очищаем движения, которые делают расход
Движения.НоменклатураПоСрокамГодности.Записывать = Истина;
Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	
Движения.Записать(); 

// устанавливаем флаги на запись в регистры	
Движения.НоменклатураПоСрокамГодности.Записывать = Истина;
Движения.СебестоимостьНоменклатуры.Записывать = Истина;
Движения.НоменклатураВЭксплуатации.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	// получаем данные из документа
	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура КАК Номенклатура,
	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней КАК СрокЭксплуатацииДней,
	СУММА(ВводВЭксплуатациюСписокНоменклатуры.Количество) КАК Количество
ПОМЕСТИТЬ втОборудование
ИЗ
	Документ.ВводВЭксплуатацию.СписокНоменклатуры КАК ВводВЭксплуатациюСписокНоменклатуры
ГДЕ
	ВводВЭксплуатациюСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура,
	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатацииДней

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные из документа
	втОборудование.Номенклатура КАК Номенклатура,
	втОборудование.Номенклатура.Представление КАК НоменклатураПредставление,
	// расчета срока эксплуатации
	ДОБАВИТЬКДАТЕ(&НачалоДня, ДЕНЬ, втОборудование.СрокЭксплуатацииДней) КАК СрокЭксплуатации,
	втОборудование.Количество КАК Количество,
	// остаток по срокам годности
	ЕСТЬNULL(НоменклатураПоСрокамГодностиОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	НоменклатураПоСрокамГодностиОстатки.СрокГодности КАК СрокГодности,
	// данные для расчета себестоимости
	ЕСТЬNULL(СебестоимостьНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокОбщее,
	ЕСТЬNULL(СебестоимостьНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстатокОбщее
ИЗ
	втОборудование КАК втОборудование
		// остатки по сроку годности
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НоменклатураПоСрокамГодности.Остатки(
				&МоментВремени,
				Номенклатура В // отбор по номенклатуре
						(ВЫБРАТЬ
							втОборудование.Номенклатура КАК Номенклатура
						ИЗ
							втОборудование КАК втОборудование)
						// отбор по срокам годности
					И СрокГодности > &НачалоДня) КАК НоменклатураПоСрокамГодностиОстатки
		ПО втОборудование.Номенклатура = НоменклатураПоСрокамГодностиОстатки.Номенклатура
		// соединяемся для получения информации о сумме без учета сроков годности
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьНоменклатуры.Остатки(
				&МоментВремени,
				Номенклатура В // отбор по номенклатуре
					(ВЫБРАТЬ
						втОборудование.Номенклатура КАК Номенклатура
					ИЗ
						втОборудование КАК втОборудование)) КАК СебестоимостьНоменклатурыОстатки
		ПО втОборудование.Номенклатура = СебестоимостьНоменклатурыОстатки.Номенклатура

УПОРЯДОЧИТЬ ПО
	СрокГодности
ИТОГИ
	МАКСИМУМ(Количество), // для проверки остатков
	СУММА(КоличествоОстаток),
	МАКСИМУМ(КоличествоОстатокОбщее), // для расчета себестоимости
	МАКСИМУМ(СуммаОстатокОбщее)
ПО
	Номенклатура
```

### Формирование движений

```1c
ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
Пока ВыборкаНоменклатура.Следующий() Цикл
	// проверка, что хватает по срокам годности
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количество %2 по сроку годности",
			ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// запоминаем, сколько требуется списать	
	ТребуетсяСписать = ВыборкаНоменклатура.Количество;

	// вспомогательные переменные для расчета себестоимости	
	ОсталосьКоличество = ВыборкаНоменклатура.КоличествоОстатокОбщее;
	ОсталосьСумма = ВыборкаНоменклатура.СуммаОстатокОбщее;
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
		// определяем, сколько можем списать по текущему сроку годности
		Списываем = Мин(ТребуетсяСписать, Выборка.КоличествоОстаток);
		ТребуетсяСписать = ТребуетсяСписать - Списываем;

		// Здесь может быть ситуация, когда
		// - 11 штук (требуется) - 7 штук (подходит по сроку) - 11 штук (всего по номенклатуре)
		// 		в этой итерации спишем 7, всего осталось 11
		// - 11 штук (требуется) - 4 штук (подходит по сроку) - 11 штук (всего по номенклатуре)
		// 		в этой итерации спишем 4, всего осталось 4
		// Если мы списываем столько, сколько всего осталось, то это полное списание номенклатуры,
		// и требуется списать остатки по сумме - для решения проблемы копеек.
		// Если же на последней итерации всего осталось больше, чем списываем,
		// то это не полное списание номенклатуры
		Если Списываем = ОсталосьКоличество Тогда
			Себестоимость = ОсталосьСумма;
		Иначе
			Себестоимость = Списываем * ВыборкаНоменклатура.СуммаОстатокОбщее /  ВыборкаНоменклатура.КоличествоОстатокОбщее;
		КонецЕсли;

		// списываем номенклатуру по сроку	
		Движение = Движения.НоменклатураПоСрокамГодности.ДобавитьРасход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, СрокГодности");
		Движение.Количество = Списываем; 

		// списываем себестоимость	
		Движение = Движения.СебестоимостьНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура");
		Движение.Количество = Списываем;
		Движение.Сумма = Себестоимость;

		// вводим в эксплуатацию	
		Движение = Движения.НоменклатураВЭксплуатации.ДобавитьПриход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, СрокГодности, СрокЭксплуатации");
		Движение.Количество = Списываем;
		Движение.Сумма = Себестоимость;

		// обновляем оставшееся общее количество и сумму номенклатуры	
		ОсталосьКоличество = ОсталосьКоличество - Списываем;
		ОсталосьСумма = ОсталосьСумма - Движение.Сумма;
	КонецЦикла;
КонецЦикла;
```


### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объекты блокировки (по двум левым соединениям для расхода): 
	- регистр накопления `НоменклатураПоСрокамГодности`
	- регистр накопления `СебестоимостьНоменклатуры`
- отборы блокировки:
	- блокировка 1 (остатки по срокам):
		- номенклатура; можно взять из ТЧ
		- срок годности; по диапазону, ОТ начала дня и позже
	- блокировка 2 (себестоимость):
		- номенклатура; можно взять из ТЧ

```1c
Блокировка = Новый БлокировкаДанных;
// БЛОКИРОВКА 1
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НоменклатураПоСрокамГодности");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
// блокировка по диапазону: ОТ НачалоДня
ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(НачалоДня, )); 
// БЛОКИРОВКА 2
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СебестоимостьНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
Блокировка.Заблокировать();
```

## Выбытие оборудование

Возьмем, что документ делается в конце месяца. Чтобы при повторном проведении движения были такими же, записываем движения на начало следующего месяца.

Необходимо списать:
- Номенклатура по срокам + Себестоимость
- Номенклатура в эксплуатации


### Флаги

```1c
// очищаем движения, которые делают расход
Движения.НоменклатураПоСрокамГодности.Записывать = Истина;
Движения.СебестоимостьНоменклатуры.Записывать = Истина;
Движения.НоменклатураВЭксплуатации.Записывать = Истина;
	
Движения.Записать(); 

// устанавливаем флаги на запись в регистры	
Движения.НоменклатураПоСрокамГодности.Записывать = Истина;
Движения.СебестоимостьНоменклатуры.Записывать = Истина;
Движения.НоменклатураВЭксплуатации.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	НоменклатураПоСрокамГодностиОстатки.Номенклатура КАК Номенклатура,
	НоменклатураПоСрокамГодностиОстатки.СрокГодности КАК СрокГодности,
	НоменклатураПоСрокамГодностиОстатки.КоличествоОстаток КАК КоличествоОстаток,
	СебестоимостьНоменклатурыОстатки.КоличествоОстаток КАК КоличествоОстатокОбщее,
	СебестоимостьНоменклатурыОстатки.СуммаОстаток КАК СуммаОстатокОбщее
ИЗ
	РегистрНакопления.НоменклатураПоСрокамГодности.Остатки(&Период, СрокГодности < &КонецМесяца) КАК НоменклатураПоСрокамГодностиОстатки
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьНоменклатуры.Остатки(&КонецМесяца, ) КАК СебестоимостьНоменклатурыОстатки
		ПО НоменклатураПоСрокамГодностиОстатки.Номенклатура = СебестоимостьНоменклатурыОстатки.Номенклатура
ИТОГИ
	СУММА(КоличествоОстаток),
	МАКСИМУМ(КоличествоОстатокОбщее),
	МАКСИМУМ(СуммаОстатокОбщее)
ПО
	// берем итоги, т.к. могут быть записи:
	// Чайник - 01.10.2025 - 5 штук (по сроку) - 20 штук (всего) - 220 рублей (всего)
	// Чайник - 03.10.2025 - 7 штук (по сроку) - 20 штук (всего) - 220 рублей (всего)
	// С помощью итогов можно получить, сколько всего чайников надо списать
	Номенклатура

;

/////////////////////////////////////////////

ВЫБРАТЬ
	НоменклатураВЭксплуатацииОстатки.Номенклатура КАК Номенклатура,
	НоменклатураВЭксплуатацииОстатки.СрокГодности КАК СрокГодности,
	НоменклатураВЭксплуатацииОстатки.СрокЭксплуатации КАК СрокЭксплуатации,
	НоменклатураВЭксплуатацииОстатки.КоличествоОстаток КАК Количество,
	НоменклатураВЭксплуатацииОстатки.СуммаОстаток КАК Сумма
ИЗ
	РегистрНакопления.НоменклатураВЭксплуатации.Остатки(
			&КонецМесяца,
			СрокГодности < &КонецМесяца
				ИЛИ СрокЭксплуатации < &КонецМесяца) КАК НоменклатураВЭксплуатацииОстатки
```

### Формирование движений

Для списание по срокам годности:

```1c
Пока ВыборкаНоменклатура.Следующий() Цикл

	// вспомогательные переменные для расчета себестоимости		
	ОсталосьКоличество = ВыборкаНоменклатура.КоличествоОстатокОбщее;
	ОсталосьСумма = ВыборкаНоменклатура.СуммаОстатокОбщее;
	
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
		// аналогично вводу в эксплуатацию проверяем:
		// - это последнее списание для текущей номенклатуры
		// - списываем всю номенклатуру	
		Если Выборка.КоличествоОстаток = ОсталосьКоличество Тогда
			Себестоимость = ОсталосьСумма;
		Иначе
			Себестоимость = Выборка.КоличествоОстаток * ВыборкаНоменклатура.СуммаОстатокОбщее / ВыборкаНоменклатура.КоличествоОстатокОбщее; 
		КонецЕсли; 

		// списываем оборобование	
		Движение = Движения.НоменклатураПоСрокамГодности.ДобавитьРасход();
		Движение.Период = Период;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Количество = Списываем;

		// списываем сумму	
		Движение = Движения.СебестоимостьНоменклатуры.ДобавитьРасход(); 
		Движение.Период = Период;
		ЗаполнитьЗначенияСвойств(Движение, Выборка); 
		Движение.Количество = Списываем;
		Движение.Сумма = Себестоимость;

		// обновляем информацию, сколько всего осталось номенклатуры 
		// по количеству и сумме	
		ОсталосьКоличество = ОсталосьКоличество - Списываем;
		ОсталосьСумма = ОсталосьСумма - Движение.Сумма;
	КонецЦикла;
КонецЦикла;
```

Для оборудования в эксплуатации:

```1c
Выборка = РезультатЗапроса.Выбрать();
Пока Выборка.Следующий() Цикл 
	Движение = Движения.НоменклатураВЭксплуатации.ДобавитьРасход(); 
	Движение.Период = Период;
	ЗаполнитьЗначенияСвойств(Движение, Выборка); 
КонецЦикла;
```

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объекты блокировки (регистры со списанием): 
	- регистр накопления `НоменклатураПоСрокамГодности`
	- регистр накопления `СебестоимостьНоменклатуры`
	- регистр накопления `НоменклатураВЭксплуатации`
- отборы блокировки:
	- блокировка 1 (остатки по срокам):
		- срок годности; по диапазону, ДО конца для и раньше
	- блокировка 2 (себестоимость): весь регистр
	- блокировка 3 (остатки по эксплуатации):
		- срок годности; по диапазону, ДО конца для и раньше
		- срок годэксплуатации; по диапазону, ДО конца для и раньше

```1c
Блокировка = Новый БлокировкаДанных;
// БЛОКИРОВКА 1
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НоменклатураПоСрокамГодности");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(, КонецМесяца)); 
// БЛОКИРОВКА 2
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СебестоимостьНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// БЛОКИРОВКА 3
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.НоменклатураВЭксплуатации");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(, КонецМесяца)); 
ЭлементБлокировки.УстановитьЗначение("СрокЭксплуатации", Новый Диапазон(, КонецМесяца)); 
Блокировка.Заблокировать(); 
```

## Отчет

```1c
ВЫБРАТЬ
	НоменклатураВЭксплуатацииОстатки.Номенклатура КАК Оборудование,
	НоменклатураВЭксплуатацииОстатки.КоличествоОстаток КАК Количество,
	НоменклатураВЭксплуатацииОстатки.СуммаОстаток КАК Сумма,
	РАЗНОСТЬДАТ( // считаем, что период отчета раньше, чем кончается срок годности
		&Период, 
		НоменклатураВЭксплуатацииОстатки.СрокГодности, 
		ДЕНЬ
	) КАК ОставшийсяСрокГодности,
	РАЗНОСТЬДАТ( // аналогично для срока эксплуатации
		&Период, 
		НоменклатураВЭксплуатацииОстатки.СрокЭксплуатации, 
		ДЕНЬ
	) КАК ОставшийсяСрокЭксплуатации
ИЗ
	РегистрНакопления.НоменклатураВЭксплуатации.Остатки(&Период, ) КАК НоменклатураВЭксплуатацииОстатки
```
