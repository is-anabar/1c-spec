# Билет 10 (занятие 19)


Компания является заведением общественного питания. Возможна продажа как отдельных продуктов, так наборов этих продуктов и готовых блюд.

Набор представляет из себя перечень продуктов, хранящихся на складе. Например, можно продавать в виде набора пирожное и чай, а можно по отдельности - отдельно чай, отдельно пирожное.

В том случае, если из продуктов изготовлено блюдо, например из овощей сделан салат, то продаваться может только само блюдо, а входящие в его состав продукты проданы быть не могут.

Для каждой номенклатурной позиции (продукта, блюда) к ней добавляются любое количество характеристик. Набор характеристик является свойством. Например,Салат «огуречный, входит в комплексный обед, диетический» или томат «мелкий, красный, тепличный». «Огуречный», «входит в комплексный обед», «диетический» - это характеристики, а «Огуречный, входит в комплексный обед, диетический» - свойство.

Учет номенклатур, но не наборов, ведется в разрезе свойств. Механизм должен быть реализован с помощью плана видов характеристик. Одно и то же свойство может быть указано для различных номенклатурных позиций.

Закупка продуктов отражается документом «Приходная накладная», продажа - «Расходная накладная». Для отражения приготовления блюд служит документ «Комплектация». Документом комплектация пользователь делает движения в любой регистр накопления и правильность записей контролирует сам.

Учет номенклатуры в разрезе складов не ведется.

При продаже в одной табличной части указываются продукты, наборы и готовые блюда.
Со временем (не чаще, чем 1 раз в день) состав набора может изменяться. При продаже должен приниматься тот состав набора, который был актуальным на момент продажи.

Ведомость продуктов и блюд за период с 01.01.2010 по 31.03.2010

|Номенклатура	|Нач. ост.	|Приход	|Расход	|Кон. ост.|
|---------------|-----------|-------|-------|---------|
|Салат «Столичный»	|2	|4	|5	|1|
|Салат «Капрезе»	|1	|2		|3| |
|Булочки	|100	|200	|300|	|

Отчет строится по количеству для продуктов и блюд. 


## Виды номенклатуры

Настроить виды номенклатуры можно по [инструкции](оу%20общее.md#filter-items)

В нашем случае будут:
- Товар (продукт)
- Комплект
- Блюдо


## Свойства номенклатуры

Необходимо создать справочник `Свойства номенклатуры`. Здесь будут записи, вроде `салат овощной, диетический`.

Пример можно взять [здесь](/0%20-%20общее/03%20как%20добавить%20характеристики%20объектов.md). В качестве объекта для доп. свойств будет выступать `Свойства номенклатуры`.

> Если захочется сделать проверку, что для комплектов не задаются свойства, то это можно сделать в обработчике `ПриЗаписи`


## Состав комплектов

Для хранения данных создаем регистр `СоставНаборов`:
- Периодичность: день
- Измерения:
	- Номенклатура (комплект)
	- Товар (продукт)
- Ресурсы:
	- Количество

> Почему составная часть набора в измерениях? Если переместить ее в ресурс, то можно будет сделать ситуацию:
> - 01.01.2025 - Набор №1 - Помидор - 5
> - 01.01.2025 - Набор №1 - Помидор - 6
> 
> Здесь для обного и того же набора получится задать один и тот же состав, но с разным количеством.


## Настройка регистров накопления

Несмотря на мутную формулировку `Учет номенклатур, но не наборов, ведется в разрезе свойств`, здесь речь идет только про задание характеристик. На учет остатков это никак не влияет.

> Почему не влияет: при разложении набора на продукты по время списания мы не можем угадать, какие сройства выдать продуктам из состава.

В итоге работаем только с `ОстаткиНоменклатуры`:
- Измерения:
	- Номенклатура
- Ресурсы
	- Количество


## Комплектация

Документ похож на документ [Операция](/2%20-%20БУ/общее/04%20как%20создать%20документ%20Операция%20или%20ее%20аналоги.md).


## Приходная накладная


Для формирования движений можно воспользоваться конструктором движений. 



## Расходная накладная

Здесь вообще нет расчета себестоимости, поэтому воспользуемся `новой методикой`.

### Получение предварительных данных

```1c
// создаем менеджер временных таблиц
// при проверке отрицательных остатков нам пригодится 
// временная таблица из этого запроса
МВТ = Новый МенеджерВременныхТаблиц;
	
Запрос = Новый Запрос;
// устанавливаем менеджер временных таблиц
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	// группируем табличную часть
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
		// есть текущая номенклатура - комплект,
		// то получаем его составную часть
		// иначе - исходную номенклатуру
	|	ВЫБОР
	|		КОГДА втТовары.Номенклатура.ВидНоменклатуры = &Комплект
	|			ТОГДА СоставНаборовСрезПоследних.Товар
	|		ИНАЧЕ втТовары.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
		// есть текущая номенклатура - комплект,
		// то получаем количество с учетом составных частей
		// иначе - исходное количество
	|	ВЫБОР
	|		КОГДА втТовары.Номенклатура.ВидНоменклатуры = &Комплект
	|			ТОГДА втТовары.Количество * ЕСТЬNULL(СоставНаборовСрезПоследних.Количество, 0)
	|		ИНАЧЕ втТовары.Количество
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставНаборов.СрезПоследних(
	|				&МоментВремени,
	|				Комплект В
	|					(ВЫБРАТЬ
	|						втТовары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						втТовары КАК втТовары)) КАК СоставНаборовСрезПоследних
	|		ПО втТовары.Номенклатура = СоставНаборовСрезПоследних.Комплект";
```

> Здесь может быть подвох с регистров сведений, которых хранит состав наборов:
> - нет проверки, что состав комплекта заполнен
> - если возникнет ситуация, вроде
>	- 01.01.2025 - Набор №1 - Помидор - 5
>	- 01.02.2025 - Набор №1 - Укроп - 5
>	
>	то при взятии данных на 02.02 в состав набора попадет и помидор, и укроп, хотя должен быть только укроп.
>
> Мне кажется, эти ситуацию можно просто игнорировать, т.к. получение актуального состава сильно усложняет запрос при списании. Скорее всего, этот регистр нужен только для проверки понимания, что добавить в измерения, а что - в реквизиты, и сложности с получением актуального состава - побочный эффект, который не продумали заранее


### Формирование движений

Записываем движения по `новой методике`.

```1c
Пока Выборка.Следующий() Цикл
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Движение.Период = Дата;
	ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, Количество"); 
КонецЦикла; 
	
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
Движения.ОстаткиНоменклатуры.Записать();
```

### Проверка отрицательных остатков

```1c
Запрос = Новый Запрос;
// устанавливаем МВТ, чтобы пользоваться
// временной таблицей втТовары из прошлого запроса
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&Граница,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						втТовары.Номенклатура КАК Номенклатура
	|					ИЗ
	|						втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
	|		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) < 0";

// обязательно используем Новый Граница(..., ВидГраницы.Включая)
// так как мы списываем номенклатуру на МоментВремени(),
// а остатки проверяем ровно после МоментВремени()	
Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
РезультатЗапроса = Запрос.Выполнить();
// если есть хоть одна запись, то мы получили отрицательные остатки
Если НЕ РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
		
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
		Выборка.НоменклатураПредставление, -Выборка.КоличествоОстаток);
		Сообщение.Сообщить();	
	КонецЦикла;
КонецЕсли;
```


## Отчет

```1c
ВЫБРАТЬ
	ОстаткиНоменклатурыОстаткиИОбороты.Номенклатура КАК Номенклатура,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоНачальныйОстаток КАК НачальныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоПриход КАК Приход,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоРасход КАК Расход,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоКонечныйОстаток КАК КонечныйОстаток
ИЗ
	РегистрНакопления.ОстаткиНоменклатуры.ОстаткиИОбороты(
		&НачалоПериода, 
		&КонецПериода,
		 ,
		 , 
		Номенклатура.ВидНоменклатуры В (&Товар, &Блюдо)
	) КАК ОстаткиНоменклатурыОстаткиИОбороты
```





