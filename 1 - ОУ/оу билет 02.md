# Билет 2 (занятия 10, 11)

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа - «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Складской учет товаров не ведется.

В документе «Расходная накладная», в табличной части для каждого товара пользователь указывает партию, которую необходимо списать. В том случае, если товара по указанной партии не хватает, документ не проводится и выводится соответствующее сообщение о нехватке.

Необходимо построить отчет по анализу продаж товаров за период.

Продажи с 01.01.2010 по 31.03.2010

|Номенклатура	|Кол-во	|Себест-стъ	|Продажа	|Прибыль	|Интервал	|Срок|
|---------------|-------|-----------|-----------|-----------|-----------|----|
|Куртка замшевая	|3	|300	|620	|320	|10	|20|
|Портсигар	|3	|30	|50	|20	|Разовая	|50|
Доставка	|1		| |100	|100	|Разовая| |	

Прибыль рассчитывается как:
«Сумма продаж» - «Себестоимость»

Интервал - расчетный показатель средний интервал отгрузок (в днях). Он рассчитывается как:
(«Дата первой отгрузки» - «Дата последней отгрузки») / «количество отгрузок»

В том случае, когда отгрузка была только одна, то в колонке Интервал выводится «разовая».

Срок - расчетный показатель срок последней отгрузки (в днях), определяющий, как давно прошла последняя отгрузка. Он рассчитывается как:  «Конец периода отчета» - «Дата последнего документа отгрузки»

## Виды номенклатуры

Настроить виды номенклатуры можно по [инструкции](оу%20общее.md#filter-items)


## Регистры накопления

По условию задачи:
- Храним количество и себестоимость в разрезе партий. Только для товаров
- Отчет по продажам. Товары и услуги

В связи с этим будет 2 регистра накопления: один для остатков номенклатуры, второй - для хранения данных о продажах.

### Остатки номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
	- Партия
- Ресурсы:
	- Количество
	- Сумма

### Продажи

- Оборотный
- Измерения:
	- Номенклатура
- Ресурсы:
	- Количество
	- Сумма
	- Себестоимость (для более просто запроса в отчете)



## Приходная накладная

Для формирования движениями можно воспользоваться конструктором запроса.



## Расходная накладная

Добавляем в табличную часть колонку `Партия`

Схема с валютами похожа на обычное списание себестоимости.

Дополнительно нужно делать проверку:
- если товар, то проверяем остатки
- если услуга, то нет


### Установка флагов

```1c
// очищаем движения - записываем пустые
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Продажи.Записывать = Истина;
	
Движения.Записать();
	
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Продажи.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	// группируем табличную часть
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.Партия КАК Партия,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.Партия

ИНДЕКСИРОВАТЬ ПО
	Номенклатура,
	Партия
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.ВидНоменклатуры = &Товар КАК ЭтоТовар,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Партия КАК Партия,
	втТовары.Партия.Представление КАК ПартияПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// информация об остатках
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				(Номенклатура, Партия) В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура,
						втТовары.Партия КАК Партия
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
			И втТовары.Партия = ОстаткиНоменклатурыОстатки.Партия
```


### Движения

```1c
Пока Выборка.Следующий() Цикл 
	// если обрабатывается товар, то себестоимость изменится
	// для услуг себестоимости нет
	Себестоимость = 0;

	// проверка остатков и списание - для товаров	
	Если Выборка.ЭтоТовар Тогда
		// проверка на отрицательные остатки
		Разница = Выборка.Количество - Выборка.КоличествоОстаток;
		Если Разница > 0 Тогда 
			Отказ = Истина;
				
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 по партии %2 в количестве %3",
			Выборка.НоменклатураПредставление, Выборка.ПартияПредставление, Разница);
			Сообщение.Сообщить();
		КонецЕсли; 
			
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		// проверка, что списываем номенклатуру из партии полностью	
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе
			Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
		КонецЕсли;

		// записываем расходные движения	
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Сумма = Себестоимость;
		ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, Партия, Количество");		
	КонецЕсли;

	// запись продаж для всех видов номенклатуры	
	Движение = Движения.Продажи.Добавить();
	Движение.Период = Дата;
	Движение.Себестоимость = Себестоимость;
	ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, Количество, Сумма");
КонецЦикла;
```

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
- отборы блокировки:
	- номенклатура; можно взять из ТЧ
	- партии; можно взять из ТЧ

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Партия", "Партия");
Блокировка.Заблокировать(); 
```

## Отчет

```1c
ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	СУММА(ПродажиОбороты.КоличествоОборот) КАК Количество,
	СУММА(ПродажиОбороты.СебестоимостьОборот) КАК Себестоимость,
	СУММА(ПродажиОбороты.СуммаОборот) КАК Продажа,
	СУММА(ПродажиОбороты.СуммаОборот) - СУММА(ПродажиОбороты.СебестоимостьОборот) КАК Прибыль,
	ВЫБОР
		КОГДА КОЛИЧЕСТВО(ПродажиОбороты.Регистратор) = 1
			ТОГДА "Разовая"
		// минимум - первая отгрузка
		// максимум - последняя отгрузка
		// количество регистраторов (списаний) - количество отгрузок
		ИНАЧЕ РАЗНОСТЬДАТ(МИНИМУМ(ПродажиОбороты.Период), МАКСИМУМ(ПродажиОбороты.Период), ДЕНЬ) / КОЛИЧЕСТВО(ПродажиОбороты.Регистратор)
	КОНЕЦ КАК Интервал,
	РАЗНОСТЬДАТ(МАКСИМУМ(ПродажиОбороты.Период), &КонецПериода, ДЕНЬ) КАК Срок
ИЗ
	// выставляем третим параметров Регистратор или Авто
	// тогда можно будет достать периоды движений
	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК ПродажиОбороты

СГРУППИРОВАТЬ ПО
	ПродажиОбороты.Номенклатура
```


> Если не добавлять себетсоимость в регистр продаж, то придется делать соединение с регистром остатков. Запрос получается довольно сложный.
