# Билет 2 (дополнительный)

Покупка товаров осуществляется через комитентов, в документе Приходная накладная нужно указать комитента. Продажа - без указания комитента. При проведении расходной накладной нужно распределять списание по партиям - товар который раньше пришел - тот и списывается. С каждой продажи комитенту начисляется комиссия. Процент комиссии для каждого комитента свой и может меняться со временем не чаще 1 раза в месяц. Если списывается товар, который пришел от разных комитентов, то сумма вознаграждения рассчитывается пропорционально списанному количеству. 

Комиссию надо перечислять комитенту отдельным документом Расход денег. В документе Расход денег нужна табличная часть: Контрагент и Сумма. Выплачивать нельзя больше, чем должны.

Суммового учета товара нет, складской учет не ведется.
 
В отчете Взаиморасчеты следующие поля:
|Комитент + процент комиссии (в скобках) | Начальный остаток | Продажи | Выплачено комиссии | Конечный остаток|
|----------------------------------------|-------------------|---------|--------------------|-----------------|
|										|					|			|					|				|

В отчете процент комиссии - это процент на начало месяца

*Данная задача представлена на экзамене еще в одной вариации. Отличия от текущего условия следующие: суммовой учет по товарам ведется, вознаграждение комитенту считается от суммы прибыли (выручка – себестоимость), в отчете вместо колонки «Продажи» колонка «Начислено».


# Вариант 2 - суммовой учет ведется

## Настройка регистра сведений для процентов комиссии

Создаем регистр сведений для процентов комиссии:
- Периодичность: месяц
- Измерения:
	- Коминтент
- Ресурсы:
	- Процент

## Настройка регистров накопления

По условию у нас:
- учет номенклатуры с ценой
- данные по выплатам процентов

Для первого случая будет достаточно немного изменить РН `Остатки номенклатуры`, а для второго - добавить РН для хранения данных по начислениями и выплатам.

### РН Остатки номенлатуры

Добавляем:
- Измерения:
	- Партия
	- Коминетент
- Ресурсы:
	- Сумма

> Коминтента можно вытянуть из партии, но мне кажется, что тут ситуация как с валютами:
> - если валюта в документе, то она делается отдельным измерением
> - если валюта в зашита в контрагента из документа, то она не указывается, а данные берутся из контрагентов.

> Также наличие коминтента в измерениях будет удобно для соединения в запросе при получении процента.


### РН Выплаты коминтентам

Создаем регистр накопления для выплат коминетентам:
- Тип: остаточный
- Измерения:
	- Коминтент
- Ресурсы:
	- Сумма


## Приходная накладная

Добавляем реквизит `Коминтент`.

Для формирования движений можно воспользоваться конструктором движений.


## Расходная накладная

Для проведения необходимо проверить остатки номенклатуры. Так как при списании будет вестись расчет себестоимости, то будем проводить по старой методике.

На основе разницы между продажей и себестоимостью необходимо рассчитать комиссию коминетентам.

### Движения

Очищаем движения (если были) и устанавливаем флаг на запись:
```1c
// очищаем движения - записываем пустые
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.ВыплатыКоминтентам.Записывать = Истина;
Движения.Записать();
	
// устанавливаем флажок, чтобы новые движения записывались
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.ВыплатыКоминтентам.Записывать = Истина;
```

### Запрос

Получаем информацию об остатках и формируем данные для списания по партиям.

```1c
// группируем данные из документа
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	// данные из остатков
	ОстаткиНоменклатурыОстатки.Коминтент КАК Коминтент,
	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	// данные по проценту для коминента
	ЕСТЬNULL(ПроцентыКоминтентовСрезПоследних.Процент, 0) КАК Процент
ИЗ
	втТовары КАК втТовары
		// данные по остаткам
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				Номенклатура В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
			// проценты начислений для коминентов
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентыКоминтентов.СрезПоследних(&МоментВремени, ) КАК ПроцентыКоминтентовСрезПоследних
			ПО ОстаткиНоменклатурыОстатки.Коминтент = ПроцентыКоминтентовСрезПоследних.Коминтент
		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура

// порядок для списания
УПОРЯДОЧИТЬ ПО
	ОстаткиНоменклатурыОстатки.Партия.МоментВремени

// для проверки того, что данных в целом хватает, 
// а также для рассчета выручки (продажи за одну единицу)
ИТОГИ

	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма),
	СУММА(КоличествоОстаток)
ПО
	Номенклатура
```

### Формирование движений

Неободимо:
- проверить, что номенклатуры хватает вообще
- сделать списание по партиям с учетом выручки

```1c
Пока ВыборкаНоменклатура.Следующий() Цикл
	// проверка, что в целом хватает	
	Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в колчестве %2",
		ВыборкаНоменклатура.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;
		
	ТребуетсяСписать = ВыборкаНоменклатура.Количество; 
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// пока есть, что списывать
	Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
		// определяем, сколько требуется списать
		Списываем = Мин(ТребуетсяСписать, Выборка.КоличествоОстаток);
		// обновляем, сколько осталось списать
		ТребуетсяСписать = ТребуетсяСписать - Списываем;

		// проверка последнего списания	
		Если Списываем = Выборка.КоличествоОстаток Тогда
			Себестоимость = Выборка.СуммаОстаток;
		Иначе
			Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток; 
		КонецЕсли; 

		// расчет, за сколько продали текущую номенклатуру	
		Выручка = Списываем * Выборка.Сумма / Выборка.Количество;
			
		// списание остатков
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, Партия, Коминтент");
		Движение.Количество = Списываем;
		Движение.Сумма = Себестоимость;
			
		// начисление выплат
		Движение = Движения.ВыплатыКоминтентам.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Коминтент = Выборка.Коминтент;
		Движение.Сумма = (Выручка - Себестоимость) * (Выборка.Процент / 100); 
		КонецЦикла; 
	КонецЦикла;
```


### Устанавливаем блокировку

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
- отборы блокировки:
	- список номенклатуры; можно достать из документа


```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
Блокировка.Заблокировать();
```



## Расход денег

Необходимо определить, сколько осталось выплатить контрагенту. Если пытаемся начислить больше, то формируем отказ.

Так как нет никаких расчетов (как расчет себестоимости), то для проведение необходимо использовать новую методику.

### Движения

Очищаем движения (если были) и устанавливаем флаг на запись:
```1c
// очищаем движения - записываем пустые
Движения.ВыплатыКоминтентам.Записывать = Истина;
Движения.Записать();
	
// устанавливаем флажок, чтобы новые движения записывались
Движения.ВыплатыКоминтентам.Записывать = Истина;
```

### Записываем движения в регистр

Необходимо с помощтю запроса сгруппировать данные, записать (и запомнить с помощью менеджера временных таблиц).

```1c
// данные группировки ТЧ пригодятся для проверки отрицательных остатков
МВТ = Новый МенеджерВременныхТаблиц;
	
// запрос
Запрос = Новый Запрос; 
// указываем для запоминания временных таблиц
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходДенегОсновная.Коминтент КАК Коминтент,
	|	СУММА(РасходДенегОсновная.Сумма) КАК Сумма
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	Документ.РасходДенег.Основная КАК РасходДенегОсновная
	|ГДЕ
	|	РасходДенегОсновная.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходДенегОсновная.Коминтент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Коминтент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// выводим для формирования движений
	|ВЫБРАТЬ
	|	втДанные.Коминтент КАК Коминтент,
	|	втДанные.Сумма КАК Сумма
	|ИЗ
	|	втДанные КАК втДанные";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Выборка = РезультатЗапроса.Выбрать();
	
	// формируем движения
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВыплатыКоминтентам.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Коминтент = Выборка.Коминтент;
		Движение.Сумма = Выборка.Сумма;
	КонецЦикла;
	
	// записываем
	Движения.ВыплатыКоминтентам.Записывать = Истина;
	Движения.ВыплатыКоминтентам.БлокироватьДляИзменения = Истина;
	Движения.Записать();
```

### Проверка отрицательных остатков

```1c
// запрос
Запрос = Новый Запрос;
// необходимо для получения данных из прошлого запроса
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	втДанные.Коминтент КАК Коминтент,
	|	втДанные.Коминтент.Представление КАК КоминтентПредставление,
	|	ЕСТЬNULL(ВыплатыКоминтентамОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	|ИЗ
	|	втДанные КАК втДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыплатыКоминтентам.Остатки(
	|				&МоментВремени,
	|				Коминтент В
	|					(ВЫБРАТЬ
	|						втДанные.Коминтент КАК Коминтент
	|					ИЗ
	|						втДанные КАК втДанные)) КАК ВыплатыКоминтентамОстатки
	|		ПО втДанные.Коминтент = ВыплатыКоминтентамОстатки.Коминтент
	|ГДЕ
	|	ЕСТЬNULL(ВыплатыКоминтентамОстатки.СуммаОстаток, 0) < 0";

// обязательно использовать
// Новый Граница(МоментВремени(), ВидГраницы.Включая)
// так как мы списали данные на МоментВремени(),
// а проверяем остатки ровно после МоментВремени()	
Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));

// если результат не пустой, то есть отрицательные остатки
// значит, мы начислили больше, чем нужно	
РезультатЗапроса = Запрос.Выполнить();
Если НЕ РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
		
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Для коминтента %1 сумма превышает на %2",
		Выборка.КоминтентПредставление, -Выборка.СуммаОстаток);
		Сообщение.Сообщить();
	КонецЦикла;
КонецЕсли;
```

## Отчет

Получаем данные по остаткам и оборотам по выплатам.

```1c
ВЫБРАТЬ
	ВыплатыКоминтентамОстаткиИОбороты.Коминтент КАК Коминтент,
	ВыплатыКоминтентамОстаткиИОбороты.СуммаНачальныйОстаток КАК НачальныйОстаток,
	ВыплатыКоминтентамОстаткиИОбороты.СуммаПриход КАК Начислено,
	ВыплатыКоминтентамОстаткиИОбороты.СуммаРасход КАК Выплачено,
	ВыплатыКоминтентамОстаткиИОбороты.СуммаКонечныйОстаток КАК КонечныйОстаток,
	ПроцентыКоминтентовСрезПоследних.Процент КАК Процент
ИЗ
	РегистрНакопления.ВыплатыКоминтентам.ОстаткиИОбороты КАК ВыплатыКоминтентамОстаткиИОбороты
		// {} показывают, что надо взять значение именно из параметра с именем НачалоПериода
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентыКоминтентов.СрезПоследних({(&НачалоПериода)}, ) КАК ПроцентыКоминтентовСрезПоследних
		ПО ВыплатыКоминтентамОстаткиИОбороты.Коминтент = ПроцентыКоминтентовСрезПоследних.Коминтент
```


Чтобы вывести коминента и провент вместе, можно создать необходимо вычисляемое поле, и выбрать его в качестве группировки.

