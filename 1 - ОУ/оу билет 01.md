# Билет 1 (занятия 6, 7-9)

Компания занимается оптовой торговлей. Поступление товара отражается документом «Приходная накладная», продажа – «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Учет товаров ведется в разрезе складов. Поступление и продажа осуществляется с указанием склада (в шапке документа).

При проведении документа «Расходная накладная» необходимо производить списание товара со склада. В том случае, когда товара не хватает, документ проводиться не должен.

Списание себестоимости товаров должно быть организовано по партиям, в зависимости от метода списания (FIFO или LIFO), принятого в учетной политике. Значение учетной политики меняется не чаще одного раза в год. При проведении документа необходимо использовать метод, актуальный на момент проведения. 

Для расчета себестоимости при списании товара необходимо учитывать только момент поступления товара в компанию, вне зависимости от того, на какой склад он пришел. Предположим, для метода списания FIFO первое поступление портсигара произошло на склад «Основной» документом «Приходная накладная №1», а потом на склад «Транзитный» документом «Приходная накладная №2». В этом случае при продаже товара со склада «Транзитный» в первую очередь должна быть списана себестоимость портсигара по документу «Приходная накладная №1», так как она пришла раньше.

Необходимо построить отчет по остаткам товара на складах на указанную дату.

![report](/1%20-%20ОУ/media/ticket-1-report.png)


## Учетная политика

Инструкция по созданию учетной политики [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md)

## Склады

Создаем справочник `Склады`


## Номенклатура

Добавляем `Вид номенклатуры` и устанавливаем отбор для ТЧ `Приходная накладная`. Подробнее [здесь](оу%20общее.md#filter-items)


## Настройка регистров накопления

По условию:
- остатки - по складу; здесь учет цены не требуется
- себестоимость - по партиям

Так как разные разрезы для остатков и для себестоимости, то создаем 2 регистра сведений.

### Остатки номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
	- Склад
- Ресурсы (для хранения остатков):
	- Количество

 ### Себестоимость номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
	- Партия
- Ресурсы (для расчета себестоимости):
	- Количество
	- Сумма

	

## Приходная накладная

Добавляем реквизит `Склад`.

Для формирования движений можно воспользоваться конструктором движений. Необходимо сделать движения по обоим регистрам накопления.


## Расходная накладная

Списание для разных регистров будет проходить по-разному:
- Остатки номенклатуры:
	- Получаем, какую номенклатуру и в каком количестве нужно списать
	- Так как нет расчета себестоимости, то можем списать данные сразу - `новая методика`
	- После списания проверяем, что нет отрицательных остатков
- Себебстоимость номенклатуры:
	- Получаем, какую номенклатуру и в каком количестве нужно списать
	- Так как есть расчет себестоимости, то необходимо заранее проверить остатки - `старая методика`
	- Рассчитываем себестоимость

### Проверка учетной политики

Сначала проверяем, что на этот год задана учетная политика.

### Получение предварительных данных из документа

Получаем, сколько номенклатуры потребовали в документе, записываем во временную таблицу.

Далее эти данные пригодятся для формирования движений по списанию.

```1c	
// Необходимо для того, чтобы новые запросы знали
// о временных таблицах
МВТ = Новый МенеджерВременныхТаблиц;
	
Запрос = Новый Запрос; 
// Устанавливаем менеджер для "запоминания виртуальных таблиц"
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	// группируем для избавления от дублей
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|	// только товары проверяем на остатки и себестоимость
	|	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Товар
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Будет использовано для формирования списания остатков
	|// и блокировки при расчете себестоимости
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Количество КАК Количество
	|ИЗ
	|	втТовары КАК втТовары";

```

### Списание номенклатуры (остатки)

Списываем номенклатуру из `ОстаткиНоменклатуры` по `новой методике`.

```1c	
// РезультатЗапросаТЧ будет использовано позже,
// для блокировки при расчете себестоимости
РезультатЗапросаТЧ = Запрос.Выполнить();	
Выборка = РезультатЗапросаТЧ.Выбрать();

// флаг
Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
Пока Выборка.Следующий() Цикл
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Номенклатура = Выборка.Номенклатура;
	Движение.Склад = Склад;
	Движение.Количество = Выборка.Количество;
КонецЦикла; 

// запись остатков	
Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
Движения.ОстаткиНоменклатуры.Записать();
```


### Проверка на отрицательные остатки

После того, как списали номенклатуру, можно проверить остатки на отрицательные значения. Если они есть, то номенклатуры недостаточно, необходимо отменить проведение.

```1c
// Проверка, что нет отрицательных остатков
Запрос = Новый Запрос;
// Используем МВТ, чтобы запрос знал о втТовары
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) * -1 КАК ОстатокНаСкладе
	|ИЗ
	|	втТовары КАК втТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|		&Граница,
	|		Номенклатура В
	|			(ВЫБРАТЬ
	|				втТовары.Номенклатура КАК Номенклатура
	|			ИЗ
	|				втТовары КАК втТовары)
	|			И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
	|	ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) < 0";

// Обязательно использовать
// Новый Граница(МоментВремени(), ВидГраницы.Включая)
// так как списание сформировано на МоментВремени(),
// а остатки необходимо получить сразу после этого момента
Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
Запрос.УстановитьПараметр("Склад", Склад);

// Если есть хоть одна запись, то номенклатуры недостаточно
// Необходимо отменить проведение	
РезультатЗапроса = Запрос.Выполнить();
Если НЕ РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
	
	Выборка = РезультатЗапроса.Выбрать();	
	Пока Выборка.Следующий() Цикл
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
	Выборка.НоменклатураПредставление,
	Выборка.ОстатокНаСкладе);
	Сообщение.Сообщить();	
	КонецЦикла; 
	
	Возврат;
КонецЕсли;
```

### Расчет себестоимости

Для расчета себестоимости используется `старая методика`.


1. Устанавливаем флаги
	```1c
	// флаги
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	Движения.Записать();
		
	Движения.СебестоимостьНоменклатуры.Записывать = Истина;
	```

2. Запрос

	```1c
	// запрос
	Запрос = Новый Запрос;
	// Используем МВТ, чтобы запрос знал о втТовары
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	втТовары.Номенклатура КАК Номенклатура,
		|	втТовары.Количество КАК Количество,
		|	СебестоимостьНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(СебестоимостьНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(СебестоимостьНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	втТовары КАК втТовары
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьНоменклатуры.Остатки(
		|		&МоментВремени,
		|		Номенклатура В
		|			(ВЫБРАТЬ
		|				втТовары.Номенклатура КАК Номенклатура
		|			ИЗ
		|				втТовары КАК втТовары)) КАК СебестоимостьНоменклатурыОстатки
		|	ПО втТовары.Номенклатура = СебестоимостьНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СебестоимостьНоменклатурыОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество)
		|ПО
		|	Номенклатура";

	// Порядок документов меняется в зависимости от учетной политики	
	Если УчетнаяПолитика = Перечисления.УчетнаяПолитика.ЛИФО Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"СебестоимостьНоменклатурыОстатки.Партия.МоментВремени",
		"СебестоимостьНоменклатурыОстатки.Партия.МоментВремени УБЫВ");
	КонецЕсли;
		
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	```


3. Движения

	```1c
	Пока ВыборкаНоменклатура.Следующий() Цикл
		// запоминаем, сколько нужно списать по текущей номенклатуре
		ТребуетсяСписать = ВыборкаНоменклатура.Количество;
		
		Выборка = ВыборкаНоменклатура.Выбрать();
		// Пока есть, что списывать
		Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
			// Определяем, сколько можно списать номенклатуры у текущей строки
			Списываем = Мин(ТребуетсяСписать, Выборка.КоличествоОстаток);
			ТребуетсяСписать = ТребуетсяСписать - Списываем;
			
			// Проверка последнего списания
			Если Списываем = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СуммаОстаток;
			Иначе
				Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток ;
			КонецЕсли;
			
			// Формирование движений
			Движение = Движения.СебестоимостьНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Партия = Выборка.Партия;
			Движение.Количество = Списываем;
			Движение.Сумма = Себестоимость;
			
		КонецЦикла;
	КонецЦикла;
	```

4. Блокировка

	Блокировка устанавливается `ПЕРЕД` запросом. 

	На основе запроса делаем блокировку:
	- объект блокировки: регистр накопления `СебестоимостьНоменклатуры`
	- отборы блокировки:
		- список номенклатуры; можно достать из предварительного запроса в самом начале списания - `РезультатЗапросаТЧ`

	```1c
	// блокировка
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СебестоимостьНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	// Используем данные из предварительного запроса в самом начале списания
	ЭлементБлокировки.ИсточникДанных = РезультатЗапросаТЧ;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	```

