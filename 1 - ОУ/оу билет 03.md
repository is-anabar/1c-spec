# Билет 3 (занятия 12, 13)

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа - «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Складской учет товаров не ведется.

Списание себестоимости должно быть в зависимости от принятого на момент проведения документа значения учетной политики. Метод списания себестоимости либо по FIFO, либо по средней. Учетная политика может меняться каждый месяц, её изменение фиксируется соответствующим документом. Например:
- 01.01.2012 Учетная политика № 1 Списание по FIFO
	- 05.01.2012 Прих. накладная № 1 Куртка 1 шт. 1800р. (стоимость закупки)
	- 10.01.2012 Прих. накладная № 2 Куртка 1 шт. 1000р. (стоимость закупки)
	- 15.01.2012 Прих Накладная № 3 Куртка 1 шт. 2000р. (стоимость закупки)
	- 25.01.2012 Расх. накладная № 1 Куртка 1 шт. 1800р. (себестоимость)
- 01.02.2012 Учетная политика № 2 списание по средней 
	- 07.02.2012 Расх. накладная № 2 Куртка 1 шт. 1500р. (себестоимость)

Считается, что документы задним числом не вводятся, но старые документы могут неоперативно перепроводиться.

Необходимо построить отчет по движениям товаров за период.

![report](/1%20-%20ОУ/media/ticket-3-report.png)


## Виды номенклатуры

Настроить виды номенклатуры можно по [инструкции](оу%20общее.md#filter-items)


## Настройка регистров накопления

По условию:
- остатки и себестоимость - по партиям; только по товарам

Здесь будет достаточно одного регистра.


### Остатки номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
	- Партия
- Ресурсы:
	- Количество
	- Сумма


## Учетная политика

Инструкция по созданию учетной политики [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md), но будут отличия:
- появился регистратор - документ `Установка учетной политики`.

## Документ Установка учетной политики

Создаем документ `Установка учетной политики`.
- Реквизиты
	- Учетная политика

При проведении документа будет следующий алгоритм:
- Записываем данные об очетной политике 
- Если мы установили политику `По средней`, тогда:
	- списываем все остатки номенклатуры
	- записываем их на пустую партию

### Запись учетной политики

```1c
// установка флагов для записи движений
Движения.УчетнаяПолитика.Записывать = Истина;
// запись в регистр
Период = НачалоМесяца(Дата);
	
Запись = Движения.УчетнаяПолитика.Добавить();
Запись.Период = Период;
Запись.УчетнаяПолитика = УчетнаяПолитика;
```

### Перевод номенклатуры на среднюю

```1c
// установка флагов для записи движений
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.ОстаткиНоменклатуры.Записать();
Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
// блокировка
// так как будем списывать всю номенклатуру, то блоикруем весь регистр
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
Блокировка.Заблокировать(); 
	
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество,
	|	ОстаткиНоменклатурыОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&Период, ) КАК ОстаткиНоменклатурыОстатки
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(Сумма)
	|ПО
	|	Номенклатура";
	
Запрос.УстановитьПараметр("Период", Период);
	
РезультатЗапроса = Запрос.Выполнить();
ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
Пока ВыборкаНоменклатура.Следующий() Цикл
		
	Выборка = ВыборкаНоменклатура.Выбрать();
	// списываем номенклатуру с партий
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Период;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);	
	КонецЦикла;

	// записываем все на пустую	
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
	Движение.Период = Период;
	ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура, "Номенклатура, Количество, Сумма");
	Движение.Партия = Документы.ПриходнаяНакладная.ПустаяСсылка();
КонецЦикла;
```


## Приходная накладная

Добавляем реквизит `Курс`.

При проведнии:
- необходимо проверить наличие учетной политики
- для формирования движений можно воспользоваться конструктором движений
	- если учетная политика - по средней, то в партию записываем пустой документ
	- если по фифо, то записываем в партию ссылку на текущий дркумент


## Расходная накладная

Дополнительно нужно делать проверку:
- если товар, то проверяем остатки
- если услуга, то нет

### Установка флагов

```1c
// очищаем движения - записываем пустые
Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
Движения.Записать();
	
Движения.ОстаткиНоменклатуры.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	// группируем по номенклатуре
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
	// вспомогательное поле; для правильного списания номенклатуры
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуре = &Товар КАК ЭтоТовар
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуре = &Товар

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.ЭтоТовар КАК ЭтоТовар,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	// данные по остаткам
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				Номенклатура В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура

УПОРЯДОЧИТЬ ПО
	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
ИТОГИ
	МИНИМУМ(ЭтоТовар), // для проверки, нужно ли проверять остатки
	МАКСИМУМ(Количество),
	СУММА(КоличествоОстаток)
ПО
	Номенклатура
```

### Движения

```1c
Пока ВыборкаНоменклатура.Следующий() Цикл 
	// услуги не нужно проверять на остатки
	Если ВыборкаНоменклатура.ЭтоТовар Тогда
		// проверка, что хвататет номенклатуры	
		Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Разница > 0 Тогда
			Отказ = Истина; 
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			ВыборкаНоменклатура.НоменклатураПредставление, Разница);
			Сообщение.Сообщить();
		КонецЕсли;
			
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		// запоминаем, сколько нужно списать текущей номенклатуры	
		КоличествоКСписанию = ВыборкаНоменклатура.Количество; 
			
		Выборка = ВыборкаНоменклатура.Выбрать();
		// пока есть, что списывать
		Пока Выборка.Следующий() И КоличествоКСписанию > 0 Цикл
			Списываем = Мин(КоличествоКСписанию, Выборка.КоличествоОстаток);
			КоличествоКСписанию = КоличествоКСписанию - Списываем;

			// проверяем, списываем ли текущую строку полностью или нет	
			Если Списываем = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СуммаОстаток;
			Иначе
				Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
			КонецЕсли;

			// записываем движение	
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Количество = Списываем;
			Движение.Сумма = Себестоимость;
			ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, Партия");	
		КонецЦикла; 
			
	КонецЕсли;

	// записываем продажи
	// для всех видов номенклатуры	
	Движение = Движения.Продажи.Добавить();
	Движение.Период = Дата;
	Движение.СуммаВал = ВыборкаНоменклатура.Сумма;
	Движение.СуммаРуб = ВыборкаНоменклатура.Сумма * Курс;
	ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура, "Номенклатура, Количество");
КонецЦикла;
```

### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
- отборы блокировки:
	- номенклатура; можно взять из ТЧ

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
Блокировка.Заблокировать(); 
```

## Отчет

```1c
ВЫБРАТЬ
	ОстаткиНоменклатурыОстаткиИОбороты.Номенклатура КАК Номенклатура,
	ОстаткиНоменклатурыОстаткиИОбороты.Партия КАК Партия,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоНачальныйОстаток КАК КоличествоНачальныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоПриход КАК КоличествоПриход,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаПриход КАК СуммаПриход,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоРасход КАК КоличествоРасход,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаРасход КАК СуммаРасход,
	ОстаткиНоменклатурыОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоКонечныйОстаток,
	ОстаткиНоменклатурыОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток
ИЗ
	РегистрНакопления.ОстаткиНоменклатуры.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , ) КАК ОстаткиНоменклатурыОстаткиИОбороты
```


