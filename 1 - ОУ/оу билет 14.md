# Билет 14 (занятия 28-29)

Компания занимается оптовой торговлей. Отгрузка товаров покупателю отражается документом «Расходная накладная», причем каждая накладная может быть выставлена в своей валюте. Оплата от покупателя приходит в рублях и отражается документом «Приход денег». Одна оплата может относиться к нескольким накладным, в этом случае при проведении документа автоматически закрывается долг покупателя по отгрузке, начиная с самой ранней недоплаченной накладной и т.д. Курс валюты берется на дату оплаты.

В том случае, если сумма оплат превышает долг по отгрузке, то эта переплата засчитывается как аванс. При отгрузке товара необходимо проверять наличие аванса от покупателя. Если есть аванс, то он засчитывается как оплата накладной по курсу на дату накладной.

Складской учет товаров не ведется.

Необходимо построить отчеты по взаиморасчетам за период.

![report](/1%20-%20ОУ/media/ticket-14-report.png)


## Регистр накопления Взаиморасчеты

Создаем регистр накопления (остатки).

Измерения:
- Контрагент
- Накладная (пустая - для авансов)
- Валюта (т.к. для каждого документа своя)

Ресурсы:
- Сумма

## Расходная накладная

Добавляем реквизит `Контрагент`.

### Записываем контрагенту долг

```1c
// выше по коду...
СуммаПоДокументуВал = СуммаПоДокументу;

Движения.РасчетыПоНакладным.Записывать = Истина;
	
Движение = Движения.РасчетыПоНакладным.Добавить();
// чем больше сумму, тем выше долг
// отрицательная сумма - аванс
Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
Движение.Период = Дата;
Движение.Контрагент = Контрагент;
Движение.Накладная = Ссылка;
Движение.Валюта = Валюта;
Движение.Сумма = СуммаПоДокументуВал;
```

### Определяем наличие аванса

С помощью запроса находим аванс: контрагент + пустая накладная. Значение (Сумма) берём с минусом:
```1c
ВЫБРАТЬ
	-РасчетыПоНакладнымОстатки.СуммаОстаток КАК СуммаАвансРуб
ИЗ
	РегистрНакопления.РасчетыПоНакладным.Остатки(
			&МоментВремени,
			Контрагент = &Контрагент
				И Накладная = &ПустаяСсылка) КАК РасчетыПоНакладнымОстатки
```

Если запрос ничего не вернул, то аванса нет.

### Определяем, покрывает ли аванс хотя бы малую часть долга в валюте (евроцент/цент). Если нет, то завершаем проведение.

```1c
// Если сумма покрывает меньше 0.01, то просто оставляем "аванс" висеть дальше
СуммаАвансаВал = Окр(Выборка.СуммаАвансРуб / КурсВалюты, 2);
	
Если СуммаАвансаВал = 0 Тогда
	Возврат;
КонецЕсли;
```

### Определяем, какую часть суммы покрывает аванс: всю сумму или часть.

```1c
// если аванс больше или равен сумме по накладной,
// просто списываем весь долг по накладной в рублях и валюте
Если Выборка.СуммаАвансРуб >= СуммаПоДокументуРуб Тогда
	ПокрываетсяАвансомРуб = СуммаПоДокументуРуб;
	ПокрываетсяАвансомВал = СуммаПоДокументуВал;
Иначе 
	// тратим весь аванс в рублях и сумму по авансу в валюте
	ПокрываетсяАвансомРуб = Выборка.СуммаАвансРуб;
	ПокрываетсяАвансомВал = СуммаАвансаВал;
КонецЕсли;
```

### Берем деньги у накладной и переносим в счет оплаты.

```1c
// берем у аванса - пустой накладной
Движение = Движения.РасчетыПоНакладным.Добавить();
// авансы записываются с минусом, поэтому тут приход
Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
Движение.Период = Дата;
Движение.Контрагент = Контрагент;
Движение.Накладная = Документы.РасходнаяНакладная.ПустаяСсылка();
Движение.Валюта = Справочники.Валюты.BYN;
Движение.Сумма = ПокрываетсяАвансомРуб; 
	
// записываем сумму документу
Движение = Движения.РасчетыПоНакладным.Добавить();
// долг делает приход, поэтому для уменьшения долга снимаем деньги
Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
Движение.Период = Дата;
Движение.Контрагент = Контрагент;
Движение.Накладная = Ссылка;
Движение.Валюта = Валюта;
Движение.Сумма = ПокрываетсяАвансомВал;
```


## Приход денег

Создаем документ.

Добавляем реквизит `Контрагент`, `СуммаПоДокументуРуб`.

### Получаем долги по накладным

```1c
ВЫБРАТЬ
	РасчетыПоНакладнымОстатки.Накладная КАК Накладная,
	РасчетыПоНакладнымОстатки.Валюта КАК Валюта,
	РасчетыПоНакладнымОстатки.СуммаОстаток КАК СуммаОстаток,
	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс
ИЗ
	РегистрНакопления.РасчетыПоНакладным.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК РасчетыПоНакладнымОстатки
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних КАК КурсыВалютСрезПоследних
		ПО РасчетыПоНакладнымОстатки.Валюта = КурсыВалютСрезПоследних.Валюта

УПОРЯДОЧИТЬ ПО
	РасчетыПоНакладнымОстатки.Накладная.Дата
```

### Списываем долги

```1c
// По аналогии со списанием номенклатуры, заводим
// вспомогательную переменную для списания денег
ПриходДенегРуб = СуммаПоДокументуРуб;

// Если есть строки и еще хватает денег, то идем дальше	
Пока Выборка.Следующий() И ПриходДенегРуб > 0 Цикл
	// Определяем, какую сумму в рублях покрывает оплата
	СуммаДолгаВРублях = Выборка.СуммаОстаток * Выборка.Курс;
	ОплаченоПоНакладной = Мин(СуммаДолгаВРублях, ПриходДенегРуб);

	// Если покрывается вся сумма в рублях, то
	// списываем всю валюту и все рубли	
	Если СуммаДолгаВРублях = ОплаченоПоНакладной Тогда
		СуммаОплатыРуб = ОплаченоПоНакладной;
		СуммаОплатыВал = Выборка.СуммаОстаток;
	Иначе
		// Если нет, то рассчитываем, сколько в валюте покроет оплата
		СуммаОплатыРуб = ОплаченоПоНакладной;
		СуммаОплатыВал = ?(Выборка.Курс = 0, 0, Окр(ОплаченоПоНакладной / Выборка.Курс, 2));
	КонецЕсли;

	// Если оплата в валюте покрывает больше 0.01, записываем в аванс	
	Если СуммаОплатыВал > 0 Тогда
		Движение = Движения.РасчетыПоНакладным.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Выборка.Накладная;
		Движение.Валюта = Выборка.Валюта;
		Движение.Сумма = СуммаОплатыВал;    

		// Отнимает остаток суммы для погашения	
		ПриходДенегРуб = ПриходДенегРуб - СуммаОплатыРуб;
	КонецЕсли;		
КонецЦикла;
```

### Записываем аванс

Если что-то осталось в `ПриходДенегРуб`, то записываем в аванс.

```1c
Если ПриходДенегРуб > 0 Тогда
	Движение = Движения.РасчетыПоНакладным.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Накладная = Документы.РасходнаяНакладная.ПустаяСсылка();
	Движение.Валюта = Справочники.Валюты.BYN;
	Движение.Сумма = ПриходДенегРуб;
КонецЕсли;
```

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `РасчетыПоНакладным`
- отборы блокировки:
	- контрагент

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоНакладным");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по контрагенту
ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
Блокировка.Заблокировать();
```