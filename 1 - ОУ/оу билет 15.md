# Билет 15 (занятие 15)


Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Складской учет товаров не ведется.

Списание себестоимости товаров должно быть организовано по партиям, в зависимости метода списания (FIFO или LIFO), принятого в учетной политике. Значение учетной политики меняется не чаще одного раза в год. При проведении документа необходимо использовать метод, актуальный на момент проведения.

Дополнительные затраты при продаже вводятся отдельным документом «Дополнительные затраты» с указанием расходной накладной, к которой относятся эти затраты, номенклатуры и суммы затрат по каждой номенклатурной позиции.

Необходимо построить отчет по продажам товаров за период. В отчете себестоимость должна отражаться с учетом распределенных затрат, вне зависимости от того, в каком периоде был введен документ «Дополнительные затраты».

Продажи с 01.01.2010 по 31.03.2010

|Номенклатура	|Кол-во	|Себест-сть	|Продажа	|Прибыль|
|---------------|-------|-----------|-----------|-------|
|Куртка замшевая|	|3	|300	|620	|320|
|Портсигар	|3	|30	|50	|20|
|Доставка	|1	|	|100	|100|

Прибыль рассчитывается как: «Сумма продаж» - «Себестоимость».


## Виды номенклатуры

Настроить виды номенклатуры можно по [инструкции](оу%20общее.md#filter-items)


## Учетная политика

Инструкция по созданию учетной политики [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md)


## Настройка регистров накопления

По условию:
- остатки и себестоимость - по партиям; только по товарам
- продажи; товары и услуги

Один регистр будет отвечать за остатки и учет себестоимости, а второй - для фиксирования продаж.



### Остатки номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
	- Партия
- Ресурсы:
	- Количество
	- Сумма


### Продажи

- Оборотный
- Измерения:
	- Номенклатура
- Ресурсы:
	- Количество
	- Себестоимость (пригодится для хранения доп. затрат + для отчета)
	- Продажа


## Приходная накладная

Для формирования движений можно воспользоваться конструктором движений. Необходимо сделать движения по остаточному регистру накопления.


## Расходная накладная

Дополнительно нужно делать проверку:
- если товар, то проверяем остатки
- если услуга, то нет

### Проверка учетной политики

Сначала проверяем, что на этот год задана учетная политика.

### Установка флагов

```1c
// очищаем движения - записываем пустые
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Продажи.Записывать = Истина;
	
Движения.Записать();
	
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Продажи.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	// группируем ТЧ документа
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	// вспомогательное поле
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Товар КАК ЭтоТовар,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Товар

ИНДЕКСИРОВАТЬ ПО
	Номенклатура // индексруем, 
	// т.к. будет участвовать в отборе таблицы + левом соединении
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные из документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.ЭтоТовар КАК ЭтоТовар,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Продажа, // переименовано для удобства
	// данны по остаткам номенклатуры
	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				Номенклатура В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура

УПОРЯДОЧИТЬ ПО
	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
ИТОГИ
	МИНИМУМ(ЭтоТовар), 		// определяет, нужно ли проверять остатки
	МАКСИМУМ(Количество),
	МАКСИМУМ(Продажа),		// для движение по продаже
	СУММА(КоличествоОстаток)
ПО
	Номенклатура
```

Дополнительно корректируем запрос в зависимости от учетной политики:
```1c
Если УчетнаяПолитика = Перечисления.УчетнаяПолитика.ЛИФО Тогда
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"ОстаткиНоменклатурыОстатки.Партия.МоментВремени",
		"ОстаткиНоменклатурыОстатки.Партия.МоментВремени УБЫВ"
	);
КонецЕсли;
```

### Движения

```1c
Пока ВыборкаНоменклатура.Следующий() Цикл

	// вспомогательное поле.
	// результат будет записан в продажи	
	ИтоговаяСебестоимость = 0;

	// если товар, то проверяем остатки	
	Если ВыборкаНоменклатура.ЭтоТовар Тогда
		// проверяем, хватает ли товара
		Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Разница > 0 Тогда
			Отказ = Истина;
				
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("ОУ: Недостаточно товара %1 в количестве %2.",
			ВыборкаНоменклатура.НоменклатураПредставление, Разница);
			Сообщение.Сообщить();
		КонецЕсли;
			
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		// запоминаем, сколько требуется списать номенклатуры	
		ТребуетсяСписать = ВыборкаНоменклатура.Количество;
			
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ТребуетсяСписать > 0 Цикл
			// определяем, сколько спишем из текущей партии:
			// все до конца или только часть
			Списываем = Мин(ТребуетсяСписать, Выборка.КоличествоОстаток);
			// обновляем количество номенклатуры, которое осталось списать
			ТребуетсяСписать = ТребуетсяСписать - Списываем;

			// учет копеек, если списываем всю номенклатуру из партии	
			Если Списываем = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СуммаОстаток;
			Иначе 
				Себестоимость = Списываем * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
			КонецЕсли;

			// записываем расход	
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, Партия");
			Движение.Количество = Списываем;
			Движение.Сумма = Себестоимость; 

			// обновляем итоговую стоимость - для продаж
			// суммируем Движение.Сумма, т.к. автоматически рассчитывается округление	
			ИтоговаяСебестоимость = ИтоговаяСебестоимость + Движение.Сумма;
		КонецЦикла;
	КонецЕсли;
		
	// продажи
	Движение = Движения.Продажи.Добавить();
	Движение.Период = Дата;
	ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура);
	Движение.Себестоимость = ИтоговаяСебестоимость;
КонецЦикла;
```

### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
- отборы блокировки:
	- номенклатура; можно взять из ТЧ

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
Блокировка.Заблокировать(); 
```

## Отчет

```1c
ВЫБРАТЬ
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	ПродажиОбороты.КоличествоОборот КАК Количество,
	ПродажиОбороты.СебестоимостьОборот КАК Себестоимость,
	ПродажиОбороты.ПродажаОборот КАК Продажа,
	// рассчитываем прибыль
	ПродажиОбороты.ПродажаОборот - ПродажиОбороты.СебестоимостьОборот КАК Прибыль
ИЗ
	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ПродажиОбороты
```


