# Билет 12 (занятия 22, 23)

Компания занимается торговлей специализированного оборудования. Принят следующий порядок работы: поступает заказ покупателя (документ «Заказ покупателя»). На основании заказов покупателей производится закупка оборудования у поставщика (документ «Приходная накладная»), причем можно приобрести только то оборудование, которое заказано покупателем. После того, как оборудование поступило, оно может быть отгружено покупателю (документ «Расходная накладная»).

В документе «Приходная накладная» для каждой номенклатурной позиции указывается заказ покупателя, для которого приобретено это оборудование. Оборудование для одного заказа покупателя может поставляться несколькими документами. Закупать можно только заказанное покупателем оборудование.

Отгрузка оборудования по одному заказу покупателя может происходить несколькими документами «Расходная накладная». Следует считать, что оборудование по заказу будет отгружено полностью и отгрузка может происходить только на основании заказа. 

Себестоимость оборудования рассчитывается как средняя в рамках закупок под заказ покупателя.

Используя планы видов характеристик, необходимо предоставить возможность пользователю добавить произвольное количество дополнительных характеристик к каждому заказу. Например, для одного заказа могут быть указаны регион и срочность, а для другого заказа может быть указан регион и необходимость в дополнительном контроле поставки.

Необходимо создать отчет по анализу недоотгруженных заказов покупателей на выбранную дату в разрезе выбранной характеристики.

![отчет](/1%20-%20ОУ/media/ticket-12-report.png)


## Свойства заказа

Пример можно взять [здесь](/0%20-%20общее/03%20как%20добавить%20характеристики%20объектов.md). В качестве объекта для доп. свойств будет выступать `Заказ`.


## Хранения заказов покупателей

Для хранения того, сколько номенклатуры было заказано, создаем регистр накопления `ЗаказыПокупателей`.

Измерения:
- Номенклатура
- Заказ
Ресурсы:
- Количество

## Хранение остатков номенклатуры

Для хранения доступных остатков и стоимости номенклатуры изменяем регистр `ОстаткиНоменклатуры`.

Измерения:
- Номенклатура
- Заказ
Ресурсы:
- Количество
- Сумма

## Документ Заказ покупателя

Создаем документ.

Добавляем табличную часть `СписокНоменклатуры`.

Реквизиты ТЧ:
- номенклатура
- количество


Записываем приход в `ЗаказыПокупателей`. Можно воспользоваться конструктором движений.


## Документ Приходная накладная

В ТЧ `СписокНоменклатуры` добавляем поле `Заказ`.

### Поступление по заказу

Чтобы не закупить больше, чем заказали, проверяем остатки по `ЗаказыПокупателей`. Так как здесь важно только количество, а стоимости нет, используем `новую методику`.

```1c
// используем менеджер временных таблиц
МВТ = Новый МенеджерВременныхТаблиц;
	
Запрос = Новый Запрос;
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	// таблицу втТовары будем использовать после для 
	// проверки остатков по заказам 
	|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	ПриходнаяНакладнаяСписокНоменклатуры.Заказ КАК Заказ,
	|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	|	СУММА(ПриходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	|	ПриходнаяНакладнаяСписокНоменклатуры.Заказ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Выводим данные для формирования остатков по заказам
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Заказ КАК Заказ,
	|	втТовары.Количество КАК Количество,
	|	втТовары.Сумма КАК Сумма
	|ИЗ
	|	втТовары КАК втТовары";
	
Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
РезультатЗапросаТЧ = Запрос.Выполнить();
	
Выборка = РезультатЗапросаТЧ.Выбрать();	
Пока Выборка.Следующий() Цикл
	Движение = Движения.ЗаказыПокупателей.ДобавитьРасход();
	Движение.Период = Дата;
	ЗаполнитьЗначенияСвойств(Движение, Выборка);
КонецЦикла;

// сохраняем движения
Движения.ЗаказыПокупателей.Записывать = Истина;
Движения.ЗаказыПокупателей.БлокироватьДляИзменения = Истина;
Движения.ЗаказыПокупателей.Записать();
```

### Проверка отрицательных остатков

После записи движений проверяем, что нет отрицательных остатков. Если они есть, то мы списали больше, чем у нас потребовали по заказам.

```1c
// проверка отрицательных остатков 
Запрос = Новый Запрос;
// используем МВТ, чтобы получить доступ к втТовары
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	втТовары.Заказ КАК Заказ,
	|	втТовары.Заказ.Представление КАК ЗаказПредставление,
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПокупателей.Остатки(
	|				&Граница,
	|				(Номенклатура, Заказ) В
	|					(ВЫБРАТЬ
	|						втТовары.Номенклатура КАК Номенклатура,
	|						втТовары.Заказ КАК Заказ
	|					ИЗ
	|						втТовары КАК втТовары)) КАК ЗаказыПокупателейОстатки
	|		ПО втТовары.Номенклатура = ЗаказыПокупателейОстатки.Номенклатура
	|			И втТовары.Заказ = ЗаказыПокупателейОстатки.Заказ
	// проверка на отрицательные остатки
	|ГДЕ
	|	ЕСТЬNULL(ЗаказыПокупателейОстатки.КоличествоОстаток, 0) < 0";
	
Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
РезультатЗапроса = Запрос.Выполнить();

// если есть хоть одна запись, то мы закали больше, чем требуется	
Если НЕ РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
		
	Выборка = РезультатЗапроса.Выбрать();	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 по заказу %2 в количестве %3",
		Выборка.НоменклатураПредставление,
		Выборка.ЗаказПредставление,
		-Выборка.КоличествоОстаток);
		Сообщение.Сообщить();
	КонецЦикла;	
КонецЕсли;  
	
// записать приход номенклатуры
Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
Выборка = РезультатЗапросаТЧ.Выбрать();	
Пока Выборка.Следующий() Цикл
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
	Движение.Период = Дата; 
	ЗаполнитьЗначенияСвойств(Движение, Выборка);
КонецЦикла;
```


## Документ Расходная накладная

Добавлем реквизит `Заказ`

Так как есть расчет себестоимости, то используется `старая методика`.

### Установка флагов

Для записи движений устанавливаем флаги

```1c
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Записать();
Движения.ОстаткиНоменклатуры.Записывать = Истина;
```

### Запрос

Получаем остатки номенклатуры.

```1c
// группируем данные из таблицы
ВЫБРАТЬ
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.Количество КАК Количество,
	ОстаткиНоменклатурыОстатки.Заказ КАК Заказ,
	// получаем данные для проверки остатков и расчета себестоимости
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				Номенклатура В
						(ВЫБРАТЬ
							втТовары.Номенклатура КАК Номенклатура
						ИЗ
							втТовары КАК втТовары)
					И Заказ = &Заказ) КАК ОстаткиНоменклатурыОстатки
		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
```

### Движения

```1c
Пока Выборка.Следующий() Цикл 
	// проверка, что номенклатуры достаточно
	Разница = Выборка.Количество - Выборка.КоличествоОстаток;
	Если Разница > 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2 по указанному заказу.",
		Выборка.НоменклатураПредставление, Разница);
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;

	// проверка последнего списания	
	Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
		Себестоимость = Выборка.СуммаОстаток;
	Иначе
		Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
	КонецЕсли;

	// движения	
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Номенклатура = Выборка.Номенклатура;
	Движение.Заказ = Заказ;
	Движение.Количество = Выборка.Количество;
	Движение.Сумма = Себестоимость;	
КонецЦикла;
```

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
- отборы блокировки:
	- заказ
	- список номенклатуры; можно достать из документа

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по списку номенклатуры
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
// отбор по заказу из реквизитов
ЭлементБлокировки.УстановитьЗначение("Заказ", Заказ);
Блокировка.Заблокировать(); 
```


