# Билет 6 (занятие 17, 38)


Компания занимается оптовой торговлей складских стеллажей и их комплектующих. Закупка комплектующих отражается документом «Приходная Накладная», продажа – «Расходная накладная».

Каждый стеллаж представляет собой некоторый фиксированный набор комплектующих (например, 4 стойки, 5 полок и 20 болтов). Необходимо обеспечить уникальность деталей, т.е. одна и та же деталь не может относиться к разным стеллажам. 

Учет остатков ведется в разрезе складов. В документах «Приходная накладная» и «Расходная накладная» склад только один (склад – реквизит шапки).

Возможна продажа как отдельных комплектующих, так и целых стеллажей, причем и стеллажи и их комплектующие указываются в одной табличной части. В случае продажи стеллажа осуществляется списание со склада соответствующего количества комплектующих.

В том случае, если каких-либо комплектующих на складе не хватает, документ проводиться не должен. Учет себестоимости деталей вести не требуется. 

Используя планы видов характеристик, необходимо предоставить возможность пользователю добавить произвольное количество характеристик к каждому стеллажу (материал, страна). 

Создать отчет, который в разрезе складов будет показывать количество стеллажей. Например, если стеллаж состоит из 4 стоек, 5 полок и 20 болтов, а на складе есть 8 стоек, 15 полок и 30 болтов, то целый стеллаж только один.

Также в отчете вывести информацию о стране стеллажа.

![report](/1%20-%20ОУ/media/ticket-6-report.png)



## Свойства номенклатуры

Пример можно взять [здесь](/0%20-%20общее/03%20как%20добавить%20характеристики%20объектов.md). В качестве объекта для доп. свойств будет выступать `Номенклатура`.


## Склады

Создаем справочник `Склады`


## Номенклатура

Добавляем `Вид номенклатуры` и устанавливаем отбор для ТЧ `Приходная накладная`. Подробнее [здесь](оу%20общее.md#filter-items)


## Состав стеллажей

Создаем регистр сведений для хранения состава стеллажей:
- Периодичность: непериодический
- Измерения:
	- Деталь (Номенклатура)
- Реквизиты:
	- Комплект (Номенклатура)
	- Количество

Чтобы разделить детали и комплекты, необходимо выставить параметры отбора как в документе `Приходная накладная`.

> Наличие только детали в измерениях автоматически проверяет, что деталь записана только в один комплект.


## Настройка регистров накопления

По условию:
- остатки - по складу; здесь учет цены не требуется

### Остатки номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
	- Склад
- Ресурсы (для хранения остатков):
	- Количество


## Приходная накладная

Добавляем реквизит `Склад`.

Для формирования движений можно воспользоваться конструктором движений. 


## Расходная накладная

Списание для разных регистров будет проходить по-разному:
- Остатки номенклатуры:
	- Получаем, какую номенклатуру и в каком количестве нужно списать
	- Так как нет расчета себестоимости, то можем списать данные сразу - `новая методика`
	- После списания проверяем, что нет отрицательных остатков


### Запрос

Необходимо разложить номенклатуру на детали.

```1c
// моздаем менеджер временных таблиц, чтобы
// заполнить данные из текущего запроса
МВТ = Новый МенеджерВременныхТаблиц;
	
Запрос = Новый Запрос;
// устанавливаем МВТ
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
		// группируем табличную часть документа
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Деталь КАК ЭтоДеталь,
	|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	|ГДЕ
	|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &Деталь
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	// здесь выводим информацию:
	// если номенклатура - деталь, то выводим исходную номенклатуру
	// если комплект - выводим составляющую комплекта
	|	ВЫБОР
	|		КОГДА втТовары.ЭтоДеталь
	|			ТОГДА втТовары.Номенклатура
	|		ИНАЧЕ СоставСтеллажей.Деталь
	|	КОНЕЦ КАК Номенклатура,
	// здесь выводим информацию:
	// если номенклатура - деталь, то выводим исходное количество
	// если комплект - выводим количество (комплектов) * количество (составных деталей)
	|	СУММА(ВЫБОР
	|			КОГДА втТовары.ЭтоДеталь
	|				ТОГДА втТовары.Количество
	|			ИНАЧЕ втТовары.Количество * ЕСТЬNULL(СоставСтеллажей.Количество, 0)
	|		КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ втДетали
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
	|		ПО втТовары.Номенклатура = СоставСтеллажей.Комплект
	|
	// группируем по номеклатуре, чтобы избавиться о дублей
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА втТовары.ЭтоДеталь
	|			ТОГДА втТовары.Номенклатура
	|		ИНАЧЕ СоставСтеллажей.Деталь
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// выводим для формирования движений
	|ВЫБРАТЬ
	|	втДетали.Номенклатура КАК Номенклатура,
	|	втДетали.Количество КАК Количество
	|ИЗ
	|	втДетали КАК втДетали";
```

### Формирование движений

Списываем номенклатуру из `ОстаткиНоменклатуры`.

```1c	
Пока Выборка.Следующий() Цикл
	Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
	Движение.Период = Дата;
	ЗаполнитьЗначенияСвойств(Движение, Выборка);
	Движение.Склад = Склад;
КонецЦикла;

// устанавливаем флаг, что движения нужно записывать
Движения.ОстаткиНоменклатуры.Записывать = Истина;
// блокировка для новой методики
Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
Движения.ОстаткиНоменклатуры.Записать();
```


### Проверка на отрицательные остатки

После того, как списали номенклатуру, можно проверить остатки на отрицательные значения. Если они есть, то номенклатуры недостаточно, необходимо отменить проведение.

```1c
Запрос = Новый Запрос;
// Используем МВТ, чтобы запрос знал о втТовары и втДетали
Запрос.МенеджерВременныхТаблиц = МВТ;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	втДетали.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ИЗ
	|	втДетали КАК втДетали
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|				&Граница,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							втДетали.Номенклатура КАК Номенклатура
	|						ИЗ
	|							втДетали КАК втДетали)
	|					И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
	|		ПО втДетали.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) < 0";

// Обязательно использовать
// Новый Граница(МоментВремени(), ВидГраницы.Включая)
// так как списание сформировано на МоментВремени(),
// а остатки необходимо получить сразу после этого момента	
Запрос.УстановитьПараметр("Граница", Новый Граница(МоментВремени(), ВидГраницы.Включая));
Запрос.УстановитьПараметр("Склад", Склад);

// Если есть хоть одна запись, то номенклатуры недостаточно
// Необходимо отменить проведение		
РезультатЗапроса = Запрос.Выполнить();
Если НЕ РезультатЗапроса.Пустой() Тогда
	Отказ = Истина;
		
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("ОУ: Недостаточно товара %1 в количестве %2",
			Выборка.НоменклатураПредставление, -Выборка.КоличествоОстаток);
		Сообщение.Сообщить();
	КонецЦикла;
КонецЕсли;
```


## Отчет

```1c
ВЫБРАТЬ
	// выводим склад
	ОстаткиНоменклатурыОстатки.Склад КАК Склад,
	// выводим стеллаж
	СоставСтеллажей.Комплект КАК Стеллаж,
	// берем минимальное число доступных стеллажей
	МИНИМУМ(
		// берем целую часть от количества стеллажей
		// здесь нет проверки на ноль, т.к. в составе не может быть нулевое количество деталей
		ЦЕЛ(ОстаткиНоменклатурыОстатки.КоличествоОстаток / ЕСТЬNULL(СоставСтеллажей.Количество, 0))
	) КАК Количество,
	СвойстваКомплектов.ЗначениеХарактеристики КАК ЗначениеХарактеристики
ИЗ
	РегистрСведений.СоставСтеллажей КАК СоставСтеллажей
		// берем остатки для деталей
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(&Период, Номенклатура.ВидНоменклатуры = &Деталь) КАК ОстаткиНоменклатурыОстатки
		ПО СоставСтеллажей.Деталь = ОстаткиНоменклатурыОстатки.Номенклатура
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СвойстваКомплектов КАК СвойстваКомплектов
		// соединяемся в условии ПО,
		// чтобы не превратить ЛЕВОЕ соединение во ВНУТРЕНЕЕ
		ПО СоставСтеллажей.Комплект = СвойстваКомплектов.Комплект
			И (СвойстваКомплектов.Характеристика = &Характеристика)

СГРУППИРОВАТЬ ПО
	ОстаткиНоменклатурыОстатки.Склад,
	СоставСтеллажей.Комплект,
	СвойстваКомплектов.ЗначениеХарактеристики
```

	
