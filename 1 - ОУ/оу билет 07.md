# Билет 7 (занятие 16)

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». Помимо продажи товара, могут оказываться дополнительные услуги, например по доставке. И услуги и товары указываются в одной табличной части.

Весь учет ведется одновременно в 2-х валютах: рубли и доллары. При проведении документов курс указывается непосредственно в самом документе. Возникновение курсовых разниц на себестоимость при продаже не предполагается.

Складской учет товаров не ведется.

Списание себестоимости товаров должно быть организовано по партиям, в зависимости метода списания (FIFO или LIFO), принятого в учетной политике. Значение учетной политики меняется не чаще одного раза в год. При проведении документа необходимо использовать метод, актуальный на момент проведения.

Необходимо построить отчет по продажам товаров за период.

![report](/1%20-%20ОУ/media/ticket-7-report.png)

Прибыль по каждой валюте рассчитывается как:
«Сумма продаж» - «Себестоимость»


## Учетная политика

Инструкция по созданию учетной политики [здесь](/0%20-%20общее/02%20как%20добавить%20регистр%20для%20учетной%20политики.md)


## Номенклатура

Добавляем `Вид номенклатуры` и устанавливаем отбор для ТЧ `Приходная накладная`. Подробнее [здесь](оу%20общее.md#filter-items)


## Настройка регистров накопления

По условию:
- остатки и себестоимость - по партиям; только по товарам
- продажи; товары и услуги

Один регистр будет отвечать за остатки и учет себестоимости, а второй - для фиксирования продаж.


### Остатки номенклатуры

- Остаточный
- Измерения:
	- Номенклатура
	- Партия
- Ресурсы:
	- Количество
	- СуммаВал
	- СуммаРуб


### Продажи

- Оборотный
- Измерения:
	- Номенклатура
- Ресурсы:
	- Количество
	- СуммаВал
	- СуммаРуб


## Приходная накладная

Добавляем реквизит `Курс`.

Для формирования движений можно воспользоваться конструктором движений. Необходимо сделать движения по остаточному регистру накопления.


## Расходная накладная

Схема с валютами похожа на обычное списание себестоимости.

Дополнительно нужно делать проверку:
- если товар, то проверяем остатки
- если услуга, то нет

### Проверка учетной политики

Сначала проверяем, что на этот год задана учетная политика.

### Установка флагов

```1c
// очищаем движения - записываем пустые
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Продажи.Записывать = Истина;
	
Движения.Записать();
	
Движения.ОстаткиНоменклатуры.Записывать = Истина;
Движения.Продажи.Записывать = Истина;
```

### Запрос

```1c
ВЫБРАТЬ
	// группируем по номенклатуре
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
	// вспомогательное поле; для правильного списания номенклатуры
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуре = &Товар КАК ЭтоТовар
ПОМЕСТИТЬ втТовары
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуре = &Товар

ИНДЕКСИРОВАТЬ ПО
	Номенклатура
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	// данные документа
	втТовары.Номенклатура КАК Номенклатура,
	втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	втТовары.ЭтоТовар КАК ЭтоТовар,
	втТовары.Количество КАК Количество,
	втТовары.Сумма КАК Сумма,
	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
	// данные по остаткам
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаРубОстаток, 0) КАК СуммаРубОстаток
ИЗ
	втТовары КАК втТовары
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
				&МоментВремени,
				Номенклатура В
					(ВЫБРАТЬ
						втТовары.Номенклатура КАК Номенклатура
					ИЗ
						втТовары КАК втТовары)) КАК ОстаткиНоменклатурыОстатки
		ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура

УПОРЯДОЧИТЬ ПО
	ОстаткиНоменклатурыОстатки.Партия.МоментВремени
ИТОГИ
	МИНИМУМ(ЭтоТовар), // для проверки, нужно ли проверять остатки
	МАКСИМУМ(Количество),
	МАКСИМУМ(Сумма),
	СУММА(КоличествоОстаток)
ПО
	Номенклатура
```

### Движения

```1c
Пока ВыборкаНоменклатура.Следующий() Цикл 
	// услуги не нужно проверять на остатки
	Если ВыборкаНоменклатура.ЭтоТовар Тогда
		// проверка, что хвататет номенклатуры	
		Разница = ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток;
		Если Разница > 0 Тогда
			Отказ = Истина; 
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			ВыборкаНоменклатура.НоменклатураПредставление, Разница);
			Сообщение.Сообщить();
		КонецЕсли;
			
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		// запоминаем, сколько нужно списать текущей номенклатуры	
		КоличествоКСписанию = ВыборкаНоменклатура.Количество; 
			
		Выборка = ВыборкаНоменклатура.Выбрать();
		// пока есть, что списывать
		Пока Выборка.Следующий() И КоличествоКСписанию > 0 Цикл
			Списываем = Мин(КоличествоКСписанию, Выборка.КоличествоОстаток);
			КоличествоКСписанию = КоличествоКСписанию - Списываем;

			// проверяем, списываем ли текущую строку полностью или нет	
			Если Списываем = Выборка.КоличествоОстаток Тогда
				СебестоимостьРуб = Выборка.СуммаРубОстаток;
				СебестоимостьВал = Выборка.СуммаВалОстаток;
			Иначе
				СебестоимостьРуб = Списываем * (Выборка.СуммаРубОстаток / Выборка.КоличествоОстаток);
				СебестоимостьВал = Списываем * (Выборка.СуммаВалОстаток / Выборка.КоличествоОстаток);
			КонецЕсли;

			// записываем движение	
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Количество = Списываем;
			Движение.СуммаВал = СебестоимостьВал;
			Движение.СуммаРуб = СебестоимостьРуб;
			ЗаполнитьЗначенияСвойств(Движение, Выборка, "Номенклатура, Партия");	
		КонецЦикла; 
			
	КонецЕсли;

	// записываем продажи
	// для всех видов номенклатуры	
	Движение = Движения.Продажи.Добавить();
	Движение.Период = Дата;
	Движение.СуммаВал = ВыборкаНоменклатура.Сумма;
	Движение.СуммаРуб = ВыборкаНоменклатура.Сумма * Курс;
	ЗаполнитьЗначенияСвойств(Движение, ВыборкаНоменклатура, "Номенклатура, Количество");
КонецЦикла;
```

### Блокировка


Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
- отборы блокировки:
	- номенклатура; можно взять из ТЧ

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
Блокировка.Заблокировать(); 
```

## Отчет

```1c
ВЫБРАТЬ
	// будем показывать только номенклатуру, которую продали
	ПродажиОбороты.Номенклатура КАК Номенклатура,
	// обрамляем в ISNULL(), чтобы сделать вычисляемые поля для выручки
	ISNULL(ОстаткиНоменклатурыОбороты.СуммаВалРасход, 0) КАК СебестоимостьВал,
	ISNULL(ОстаткиНоменклатурыОбороты.СуммаРубРасход, 0) КАК СебестоимостьРуб,
	ISNULL(ПродажиОбороты.СуммаВалОборот, 0) КАК ПродажаВал,
	ISNULL(ПродажиОбороты.СуммаРубОборот, 0) КАК ПродажаРуб,
	ПродажиОбороты.КоличествоОборот КАК Количество
ИЗ
	РегистрНакопления.Продажи.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ПродажиОбороты
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ОстаткиНоменклатурыОбороты
		ПО ПродажиОбороты.Номенклатура = ОстаткиНоменклатурыОбороты.Номенклатура
```

Для отчеты выручку можно сделать с помощью вычисляемых полей.


