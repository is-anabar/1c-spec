# Билет 1 (дополнительный)

Трансфертные цен, продажа с торговых точек с перемещением товара.

Компания занимается оптовой торговлей. Поступление товаров отражается документом «Приходная накладная», продажа – «Расходная накладная». 

Закупка товара может производиться в любой отдел, а непосредственно продажа происходит в торговых точках. Считается, что большая часть закупается отделом закупок, откуда потом передается торговым точкам. Перемещение товара происходит автоматически по трансфертной цене при продаже в торговой точке. Товар может перемещаться только из отдела закупок в торговую точку. Трансфертная цена определяется как себестоимость перемещаемого товара, увеличенная на процент наценки. Процент наценки задается пользователем для каждой номенклатурной позиции.

При продаже товара необходимо в первую очередь контролировать хватает ли товара в данной торговой точке. Если нет – необходимо не создавая документ для перемещения товаров добавить соответствующие движения в регистре для передачи товара по трансфертной цене. В том случае, когда и в отделе закупок товара не хватает, продажа не производится (документ не проводится).

Себестоимость товаров рассчитывается как средняя по отделу.

Необходимо построить отчет по движениям товаров за период по количеству и сумме.

![ticket extra 1 report](/1%20-%20ОУ/media/ticket-extra-1.png)


## Настрока справочника Номенклатура

Добавляем реквизит `ПроцентНадбавки`

## Настройка регистра накопления ОстаткиНоменклатуры

Добавляем реквизит `Сумма`.

## Приходная накладная

Добавляем реквизит `Подразделение`.

Для формирования движений можно воспользоваться конструктором движений.

## Перемещение

Создаем документ `Перемещение`.

Реквизиты:
- ПодразделениеОтправитель
- ПодразделениеПолучатель

Создаем ТЧ `СписокНоменулатуры`. Реквизиты:
- Номенклатура
- Количество

### Проведение

1. Очищаем движения (если были) и устанавливаем флаг на запись:
	```1c
	// очищаем движения - записываем пустые
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	// устанавливаем флажок, чтобы новые движения записывались
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	```

2. Формируем запрос
	Алгоритм запроса будет примерно следующий:
	- группировка табличной части по номенклатуре - избавляемся от дублей строк - `втТовары`
	- получение информации об остатках:
		- достаем информацию о том, какую номенклатуру (+ предстваление, процент надбавки) и сколько потребовали в документе
		- достаем из `ОстаткиНоменклатуры.Остатки` остаток, сумму товаров, соединяясь по:
			- отбор в виртуальной таблице: по списку номенклатуры + подразделению-отправителю
			- левое соединение по номенклатуре

	Итоговый запрос примерно такой: 
	```1c
	// группировка ТЧ
	ВЫБРАТЬ
		ПеремещениеСписокНоменклатуры.Номенклатура КАК Номенклатура,
		СУММА(ПеремещениеСписокНоменклатуры.Количество) КАК Количество
	ПОМЕСТИТЬ втТовары
	ИЗ
		Документ.Перемещение.СписокНоменклатуры КАК ПеремещениеСписокНоменклатуры
	ГДЕ
		ПеремещениеСписокНоменклатуры.Ссылка = &Ссылка

	СГРУППИРОВАТЬ ПО
		ПеремещениеСписокНоменклатуры.Номенклатура

	ИНДЕКСИРОВАТЬ ПО
		Номенклатура
	;

	////////////////////////////////////////////////////////////////////////////////
	ВЫБРАТЬ
		// информаци о номенлктаре
		втТовары.Номенклатура КАК Номенклатура,
		втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		втТовары.Номенклатура.ПроцентНадбавки КАК ПроцентНадбавки,
		// информация о том, сколько потребовали в документе
		втТовары.Количество КАК Количество,
		// информация о том, сколько номенклатуры есть у подразделения-отправителя
		ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
	ИЗ
		втТовары КАК втТовары
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
					&МоментВремени,
					Номенклатура В
							(ВЫБРАТЬ
								втТовары.Номенклатура КАК Номенклатура
							ИЗ
								втТовары КАК втТовары)
						И Подразделение = &ПодразделениеОтправитель) КАК ОстаткиНоменклатурыОстатки
			ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
	```

3. Формируем движения

	Необходимо проверить наличие товара в подразделении.

	Если товара достаточно, то:
	- списать товар у подразделения-отправителя
	- добавить товар подразделению-получателю; цена должна быть уже с наценкой

	```1c
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2",
			Выборка.НоменклатураПредставление, 
			Выборка.Количество - Выборка.КоличествоОстаток);
			Сообщение.Сообщить();
		КОнецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
				
		Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
		
		// списываем у отправителя
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Подразделение = ПодразделениеОтправитель;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Себестоимость;
		
		// добавляем получателю
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Подразделение = ПодразделениеПолучатель;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Себестоимость * (1 + Выборка.ПроцентНадбавки / 100);
	КонецЦикла;
	```

4. Устанавливаем блокировку

	Блокировка устанавливается `ПЕРЕД` запросом. 

	На основе запроса делаем блокировку:
	- объект блокировки: регистр накопления `ОстаткиНоменклатуры`
	- отборы блокировки:
		- список номенклатуры; можно достать из документа
		- подразделение-отправитель

	```1c
	Блокировка = Новый БлокировкаДанных;
	// объект блокировки
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	// отбор по складу-отправителю
	ЭлементБлокировки.УстановитьЗначение("Подразделение", ПодразделениеОтправитель);
	// отбор по списку номенклатуры
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	```

## Расходная накладная

Добавляем реквизит `Подразделение`

### Проведение

1. Очищаем движения (если были) и устанавливаем флаг на запись:
	```1c
	// очищаем движения - записываем пустые
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Записать();
	
	// устанавливаем флажок, чтобы новые движения записывались
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	```

2. Формируем запрос
	Алгоритм запроса будет примерно следующий:
	- группировка табличной части по номенклатуре - избавляемся от дублей строк - `втТовары`
	- получение информации об остатках **в текущем подразделении и в "Отдел закупок"**:
		- достаем информацию о том, какую номенклатуру (+ предстваление, процент надбавки) и сколько потребовали в документе
		- достаем из `ОстаткиНоменклатуры.Остатки` **для текущего подразделения** остаток, сумму товаров, соединяясь по:
			- отбор в виртуальной таблице: по списку номенклатуры + подразделению
			- левое соединение по номенклатуре
		- достаем из `ОстаткиНоменклатуры.Остатки` **для Отдела закупок** остаток, сумму товаров, соединяясь по:
			- отбор в виртуальной таблице: по списку номенклатуры + подразделению `Отдел закупок`
			- левое соединение по номенклатуре

	Примерный код запроса:
	```1c
	// группировка ТЧ
	ВЫБРАТЬ
		РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
	ПОМЕСТИТЬ втТовары
	ИЗ
		Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	ГДЕ
		РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка

	СГРУППИРОВАТЬ ПО
		РасходнаяНакладнаяСписокНоменклатуры.Номенклатура

	ИНДЕКСИРОВАТЬ ПО
		Номенклатура
	;

	////////////////////////////////////////////////////////////////////////////////
	ВЫБРАТЬ
		// информация о запрашиваемой номенклатуре из документа
		втТовары.Номенклатура КАК Номенклатура,
		втТовары.Номенклатура.Представление КАК НоменклатураПредставление,
		// процент надбавки достается на случай, если придется
		// делать движения Отдел закупок - текущее подразделение
		втТовары.Номенклатура.ПроцентНадбавки КАК ПроцентНадбавки,
		втТовары.Количество КАК Количество,
		// остатки в текущем подразделении
		ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		// остатки в отделе закупок
		ЕСТЬNULL(ОстаткиНоменклатурыОстаткиОтделЗакупок.КоличествоОстаток, 0) КАК КоличествоОстатокОтделЗакупок,
		ЕСТЬNULL(ОстаткиНоменклатурыОстаткиОтделЗакупок.СуммаОстаток, 0) КАК СуммаОстатокОтделЗакупок
	ИЗ
		втТовары КАК втТовары
			// получение остатков в текущем подразделении
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
					&МоментВремени,
					Номенклатура В
							(ВЫБРАТЬ
								втТовары.Номенклатура КАК Номенклатура
							ИЗ
								втТовары КАК втТовары)
						И Подразделение = &Подразделение) КАК ОстаткиНоменклатурыОстатки
			ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
			// получение остатков отдела продаж
			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
					&МоментВремени,
					Номенклатура В
							(ВЫБРАТЬ
								втТовары.Номенклатура КАК Номенклатура
							ИЗ
								втТовары КАК втТовары)
						И Подразделение = &ОтделЗакупок) КАК ОстаткиНоменклатурыОстаткиОтделЗакупок
			ПО втТовары.Номенклатура = ОстаткиНоменклатурыОстаткиОтделЗакупок.Номенклатура
	```

3. Формируем движения

	Необходимо проверить, хватает ли всего товаров. Если не хватает, то отказ

	Далее проверяем, нужно ли взять товары у отдела закупок. Если нужно, то:
	- формируем списание у отдела закупок
	- формируем приход для текущего подразделения
	- заполминаем, сколько взяли товаров и на какую сумму

	Формируем списание у текущего подразделения: берем доступное количество в текущем подразделении + количество, которое взяли у отдела закупок (если не брали, то + 0). аналогично для суммы.

	Примерный код списания:
	```1c
	Пока Выборка.Следующий() Цикл
		// определяем, достаточно ли товара в компании
		Разница = Выборка.Количество - (Выборка.КоличествоОстаток + Выборка.КоличествоОстатокОтделЗакупок);
		Если Разница > 0 Тогда
			Отказ = Истина;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2", 
			Выборка.НоменклатураПредставление, Разница);
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоСписаноОтделЗакупок = 0;
		СуммаСписаноОтделЗакупок = 0;
		
		НедостачаВТорговойТочке = Выборка.Количество - Выборка.КоличествоОстаток;
		Если НедостачаВТорговойТочке > 0 Тогда
			СебестоимостьОтделЗакупок = НедостачаВТорговойТочке * Выборка.СуммаОстатокОтделЗакупок / Выборка.КоличествоОстатокОтделЗакупок;
		
			// списываем у отправителя
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Подразделение = Справочники.Подразделения.ОтделЗакупок;
			Движение.Количество = Выборка.Количество;
			Движение.Сумма = СебестоимостьОтделЗакупок;
			
			// добавляем получателю
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Подразделение = Подразделение;
			Движение.Количество = Выборка.Количество;
			Движение.Сумма = СебестоимостьОтделЗакупок * (1 + Выборка.ПроцентНадбавки / 100);
			
			// запоминаем, сколько взяли у отдела закупок и на какую сумму
			КоличествоСписаноОтделЗакупок = НедостачаВТорговойТочке;
			СуммаСписаноОтделЗакупок = СебестоимостьОтделЗакупок * (1 + Выборка.ПроцентНадбавки / 100);
		КонецЕсли;
		
		// учитываем количество, которое есть в текущем подразделении
		// + количество, которое взяли у отдела закупок. аналогично для суммы
		КоличествоСписано = Выборка.Количество + КоличествоСписаноОтделЗакупок;
		// здесь есть проверка на 0, т.к. в текущем подразделении может не быть товара вовсе,
		// а мы весь взяли у отдела закупок
		СуммаСписано = ?(Выборка.КоличествоОстаток = 0, 0, Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток) + СуммаСписаноОтделЗакупок;
		
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Подразделение = Подразделение;
		Движение.Количество = КоличествоСписано;
		Движение.Сумма = СуммаСписано;
	КонецЦикла;
	```

4. Устанавливаем блокировку

	Блокировка устанавливается `ПЕРЕД` запросом. 

	На основе запроса делаем **две** блокировки:
	1. объект блокировки: регистр накопления `ОстаткиНоменклатуры`
		- отборы блокировки:
			- список номенклатуры; можно достать из документа
			- текущее подразделение
	2. объект блокировки: регистр накопления `ОстаткиНоменклатуры`
		- отборы блокировки:
			- список номенклатуры; можно достать из документа
			- отдел закупок

	```1c
	// блокировка
	Блокировка = Новый БлокировкаДанных;
	// блокировка по подразделению
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Подразделение", Подразделение);
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура"); 
	// блокировка отдела закупок
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Подразделение", Справочники.Подразделения.ОтделЗакупок);
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	```