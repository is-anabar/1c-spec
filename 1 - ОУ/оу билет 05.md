# Билет 5 (занятия 31, 32)

Компания занимается оптовой торговлей. Взаиморасчеты с покупателями ведутся в разрезе проектов. Поступления денежных средств от покупателя отражается документом «Приход денег», отгрузка товаров документом «Расходная накладная».

Весь учет ведется в рублях и дополнительно в разных валютах в зависимости от проекта. Валюта указывается непосредственно в самом проекте. Для каждого проекта валюта может быть указана только одна.

Возникновение курсовых разниц при поставке и оплате не предполагается.

При проведении документа курс валюты должен быть взять на дату этого документа.

В документе «Приход денег» указывается контрагент и сумма платежа. Задолженности погашаются по проектам в порядке их даты оплаты (дата оплаты указывается в проекте). В случае, когда сумма платежа больше всех долгов по отгрузке, оставшаяся сумма в рублях зачитывается как аванс.

В документе «Расходная накладная» может быть указан только один проект (проект в реквизите шапки). При проведении документа «Расходная накладная» необходимо производить проверку авансов. В том случае, если аванс есть, необходимо его погасить. Оставшаяся сумма должна быть учтена как долг по проекту по отгрузке.
Учет остатков номенклатуры не ведется.

Необходимо создать отчет по состоянию взаиморасчетов на дату.

![report](/1%20-%20ОУ/media/ticket-5-report.png)


## Примечание

Билет похож на [билет №14](/1%20-%20ОУ/оу%20билет%2014.md)


## Проекты

Создаем справочник с реквизитами:
- Контрагент
- Валюта


## Регистр накопления Взаиморасчеты

Создаем регистр накопления (остатки).

Измерения:
- Контрагент
- Проект (пустой - для авансов)
- Валюта (т.к. для каждого проекта своя и нет гарантии, что ее нельзя изменить в справочнике)

Ресурсы:
- Сумма


## Расходная накладная

Добавляем реквизит `Проект`.


### Делаем проверку проектов

Необходимо проверить, что заполнены валюта и контрагент


Далее будет встречаться переменная `Данные`. Это структура, которая хранит информацию о контрагенте и валюте проекта.


### Записываем контрагенту долг

```1c
// записываем движение по оплате
СуммаРуб = Окр(СуммаВал * Курс, 2);  
	
Движения.Оплаты.Записывать = Истина;
Движение = Движения.Оплаты.Добавить();
Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
Движение.Период = Дата;
Движение.Проект = Проект; 
Движение.СуммаВал = СуммаВал;
// контрагент и валюта
ЗаполнитьЗначенияСвойств(Движение, Данные);
```


### Получаем авансы

```1c
// устанавливаем блокировку на нужного контрагента и пустой проект (авансы)
// заполнить блокировку удобно после написания запроса
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.Оплаты");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// блокировка по контрагенту 
ЭлементБлокировки.УстановитьЗначение("Контрагент", Данные.Контрагент);
// блокировка по пустому проекту
ЭлементБлокировки.УстановитьЗначение("Проект", Справочники.Проекты.ПустаяСсылка());
Блокировка.Заблокировать();
	
// авансы	
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОплатыОстатки.Контрагент КАК Контрагент,
	|	ОплатыОстатки.Проект КАК Проект,
	|	ОплатыОстатки.Валюта КАК Валюта,
	|	-ОплатыОстатки.СуммаВалОстаток КАК СуммаРубОстаток
	|ИЗ
	|	РегистрНакопления.Оплаты.Остатки(
	|			&МоментВремени,
	|			Контрагент = &Контрагент
	|				И Проект = &ПустойПроект) КАК ОплатыОстатки";
	
Запрос.УстановитьПараметр("Контрагент", Данные.Контрагент);
Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
// только для авансов - пустой проект
Запрос.УстановитьПараметр("ПустойПроект", Справочники.Проекты.ПустаяСсылка());

// если нет авансов, то прекращаем расчет	
РезультатЗапроса = Запрос.Выполнить();
Если РезультатЗапроса.Пустой() Тогда
	Возврат;
КонецЕсли;
```

### Расчет покрытия аванса

```1c
Выборка = РезультатЗапроса.Выбрать();	
Выборка.Следующий();

// если долг по документу строго больше аванса, то
Если СуммаРуб > Выборка.СуммаРубОстаток Тогда
    // списываем весь аванс
    // рассчитываем, сколько из аванса покроется в валюте
	СписываемРуб = Выборка.СуммаРубОстаток;
	СписываемВал = Окр(Выборка.СуммаРубОстаток / Курс, 2); // обязательно с округлением
Иначе
    // если аванс полность покрывает долг, то
    // списываем весь долг по документу в рублях и валюте
	СписываемРуб = СуммаРуб;	
	СписываемВал = СуммаВал;
КонецЕсли;

// если аванс в валюте покрывает хотя бы 0.01	
Если СписываемВал > 0 Тогда 
	// уменьшаем аванс
	Движение = Движения.Оплаты.ДобавитьПриход();
	Движение.Период = Дата;
	ЗаполнитьЗначенияСвойств(Движение, Выборка);
	Движение.СуммаВал = СписываемРуб;

	// списываем долг по накладной	
	Движение = Движения.Оплаты.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Проект = Проект;
	Движение.СуммаВал = СписываемВал;
	ЗаполнитьЗначенияСвойств(Движение, Данные);
КонецЕсли;
```


## Приход денег

Создаем документ.

Добавляем реквизит `Контрагент`, `СуммаРуб`.

### Получаем долги по накладным

```1c
ВЫБРАТЬ
	ОплатыОстатки.Контрагент КАК Контрагент,
	ОплатыОстатки.Проект КАК Проект,
	ОплатыОстатки.Валюта КАК Валюта,
	ОплатыОстатки.СуммаВалОстаток КАК СуммаОстатокВал,
	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс,
	ВЫРАЗИТЬ(ОплатыОстатки.СуммаВалОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК ЧИСЛО(12, 2)) КАК СуммаОстатокРуб,
	КурсыВалютСрезПоследних.Валюта.Представление КАК ВалютаПредставление
ИЗ
	РегистрНакопления.Оплаты.Остатки(
			&МоментВремени,
			Контрагент = &Контрагент
				// только по активным проектам
				И НЕ Проект = &ПустойПроект) КАК ОплатыОстатки
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСрезПоследних
		ПО ОплатыОстатки.Валюта = КурсыВалютСрезПоследних.Валюта

УПОРЯДОЧИТЬ ПО
	ОплатыОстатки.Проект.ДатаОплаты
```

### Списываем долги

```1c
ОсталосьИзОплаты = СуммаРуб;
	
Пока Выборка.Следующий() И ОсталосьИзОплаты > 0 Цикл 
	// не обязательно, но полезно
	Если Выборка.Курс = 0 Тогда
		Отказ = Истина;
			
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задан курс для валюты " + Выборка.ВалютаПредставление;
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Продолжить;
	КонецЕсли;
			
	// определяем, сколько можем оплатить по текущему проекту	
	СписываемРуб = Мин(Выборка.СуммаОстатокРуб, ОсталосьИзОплаты);

	// если можем оплатить всю сумму по проекту, то списываем все долги в валюте	
	Если СписываемРуб = Выборка.СуммаОстатокРуб Тогда 
		СписываемВал = Выборка.СуммаОстатокВал;	
	Иначе
		// иначе рассчитываем долг в валюте, который можем покрыть
		СписываемВал = Окр(СписываемРуб / Выборка.Курс, 2);
	КонецЕсли; 

	// если модем покрыть минимум один цент	
	Если СписываемВал > 0 Тогда 
		Движение = Движения.Оплаты.ДобавитьРасход();
		Движение.Период = Дата;
		// контрагент и валюта
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.СуммаВал = СписываемВал;

		// обновляем доступную для списания сумму	
		ОсталосьИзОплаты = ОсталосьИзОплаты - СписываемРуб;
	КонецЕсли;
КонецЦикла; 
```

### Записываем аванс

Если что-то осталось в `ПриходДенегРуб`, то записываем в аванс.

```1c
Если ОсталосьИзОплаты > 0 Тогда
	// аванс
	Движение = Движения.Оплаты.ДобавитьРасход();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Проект = Справочники.Проекты.ПустаяСсылка();
	Движение.Валюта = Справочники.Валюты.РоссийскийРубль;
	Движение.СуммаВал = ОсталосьИзОплаты;
КонецЕсли;
```

### Блокировка

Блокировка устанавливается `ПЕРЕД` запросом. 

На основе запроса делаем блокировку:
- объект блокировки: регистр накопления `РасчетыПоНакладным`
- отборы блокировки:
	- контрагент

```1c
Блокировка = Новый БлокировкаДанных;
ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоНакладным");
ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
// отбор по контрагенту
ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
Блокировка.Заблокировать();
```

## Отчет

```1c
ВЫБРАТЬ
	ОплатыОстатки.Контрагент КАК Контрагент,
	ВЫБОР
		КОГДА ОплатыОстатки.Проект = &ПустойПроект
			ТОГДА "Аванс"
		ИНАЧЕ ОплатыОстатки.Проект
	КОНЕЦ КАК Проект,
	ОплатыОстатки.Валюта КАК Валюта,
	ВЫБОР
		КОГДА ОплатыОстатки.Проект = &ПустойПроект
			ТОГДА -ОплатыОстатки.СуммаВалОстаток
		ИНАЧЕ ОплатыОстатки.СуммаВалОстаток
	КОНЕЦ КАК СуммаВал,
	ВЫБОР
		КОГДА ОплатыОстатки.Проект = &ПустойПроект
			ТОГДА 0
		ИНАЧЕ ОплатыОстатки.СуммаВалОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)
	КОНЕЦ КАК СуммаРуб
ИЗ
	РегистрНакопления.Оплаты.Остатки(&Период, ) КАК ОплатыОстатки
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсыВалютСрезПоследних
		ПО ОплатыОстатки.Валюта = КурсыВалютСрезПоследних.Валюта
```

Чтобы оформить отчет, делаем следующее:
- Добавляем в реквизиты:
	- СуммаВал
	- СуммаРуб
- Создаем таблицу:
	- Строки:
		- Контрагент
	- Колонки:
		- Проект, Валюта (в одной группировке)

> Если сделать колонку без Валюты, то при задании макета группировки для колонок будет ошибка

