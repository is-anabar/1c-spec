## Билет 7 (занятие 121)

В специальной форме подбора, открываемой из формы документа «Расходная накладная», пользователь должен иметь возможность выбрать произвольное количество номенклатурных позиций, которые в результате отображаются в таблице «Отобранные товары», с указанием самого товара и его количества.

Дополнительно должна поддерживаться возможность перетаскивания мышкой выбранного товара из списка товаров в таблицу «Отобранные товары».

После окончания подбора в табличной части документа должны появиться строки со всеми выбранными товарами в указанном количестве.

Далее будет представлен примерный вид формы.

На форме две таблицы, в первой отображается «Список товаров», она состоит из двух колонок – «Наименование» и «Код».
Вторая таблица – «Отобранные товары». В ней две колонки – «Номенклатура» и «Количество».
Ниже второй таблицы кнопка «Ок».



### Форма подбора номенклатуры

1. Создаем произвольную форму номенклатуры.

	Открываем форму. Выбираем
	- **Режим открытия окна** - Блокировать окно владельца (чтобы форма существовала только при открытом документе, а не сама по себе)


2. Добавляем реквизиты формы
	2.1 **СписокНоменклатуры** - Динамический список.

	Выставляем `Основная таблица` - Номенклатура.

	Вытаскиваем на форму. 
	
	Дополнительно:
	- убираем для командуню панель
	- флаг `РазрешитьНачалоПеретаскивания` (из текущей в другой элемент) - да
	- флаг `РазрешитьПеретаскивание` (из другого элемента в текущую) - нет


	2.2. **ВыбранныеТовары** - Таблица значений.

	Добавляем колонки:
	- Номенклатура
	- Количество

	Дополнительно:
	- убираем для командуню панель
	- флаг `РазрешитьНачалоПеретаскивания` (из текущей в другой элемент) - нет
	- флаг `РазрешитьПеретаскивание` (из другого элемента в текущую) - да
	- выставить высоту таблиц (~8)


3. Добавляем команду `ПеренестиВДокумент`.

	Можно сделать кнопкой по умолчанию.

4. Перенос номенклатуры по клику 

	Для **СписокНоменклатуры** создаем обработчик `Выбор`:
	- отключаем стандартную обработку
	- создаем процедуру, которая переносит номенклатуру: проверяем наличие номенклатуры в таблице значений (НайтиСтроки)
		- если нет, то добавляем строку
		- если есть, то обновляем количество 

		```1c
		Отбор = Новый Структура;
		Отбор.Вставить("Номенклатура", Номенклатура);
		Строки = ВыбранныеТовары.НайтиСтроки(Отбор);
		
		// добавляем впервые
		Если Строки.Количество() = 0 Тогда
			Строка = ВыбранныеТовары.Добавить();
			Строка.Номенклатура = Номенклатура;
			Строка.Количество = 1;
		Иначе // уже есть, обновляем количество
			Строка = Строки[0];
			Строка.Количество = Строка.Количество + 1;
		КонецЕсли;
		```

5. Перенос номенклатуры по перетаскиванию

	Добавляем для **ВыбранныеТовары** обработчик `Перетаскивание`

	ПараметрыПеретаскивания.Значение - Список выбранных строки из СписокНоменклатуры. Пробегаемся по каждой номенклатуре и обновляем **ВыбранныеТовары** аналогично предыдущему пункту:

	```1c
	Для Каждого Номенклатура Из ПараметрыПеретаскивания.Значение Цикл
		// метод из п.4
		ПеренестиНоменклатуруВТЗ(Номенклатура);
	КонецЦикла;
	```

6. Перенос в документ

	Для команды переноса в документ формируем данные для переноса:
	- помещаем ТЗ во временное хранилище (делать нужно через ВыбранныеТовары.Выгрузить() на сервере)
	- оповещаем документ
	- закрываем форму

	```1c
	&НаКлиенте
	Процедура ПеренестиВДокумент(Команда)
		Адрес = ПоместитьТоварыВоВХ();
		Оповестить("ФормаПодбораНоменклатура", Адрес, ЭтаФорма);
		Закрыть();
	КонецПроцедуры 

	&НаСервере
	Функция ПоместитьТоварыВоВХ()
		Возврат ПоместитьВоВременноеХранилище(ВыбранныеТовары.Выгрузить());
	КонецФункции
	```


### Расходная накладная


1. Открытие формы подбора

	Для команды пишем код по открытию формы подбора:

	```1c
	// 3ий параметр нужен, чтобы оповещалась только Расходная накладная
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаПодбора", , ЭтаФорма);
	```

2. Добавляем обработку оповещения

	(Необязательно) можно проверить, что пришла обработка от подбора номенклатуры и был передан адрес временного ханилища:

	```1c
	Если НЕ (ИмяСобытия = "ФормаПодбораНоменклатура" И ЭтоАдресВременногоХранилища(Параметр)) Тогда
		Возврат;
	КонецЕсли;
	```

	Переносим данные из ВХ:
	- получает таблицу значений
	- перебираем строки; если в ТЧ нет такой номенклатуры, то добавляем, если есть - обновляем количество:

	```1c
	ВыбранныеТовары = ПолучитьИзВременногоХранилища(Адрес);

	Для Каждого СтрокаПодбора Из ВыбранныеТовары Цикл
		Отбор = Новый Структура("Номенклатура", СтрокаПодбора.Номенклатура);
		Строки = Объект.СписокНоменклатуры.НайтиСтроки(Отбор);

		Если Строки.Количество() = 0 Тогда
			Строка = Объект.СписокНоменклатуры.Добавить();
			Строка.Номенклатура = СтрокаПодбора.Номенклатура;
			Строка.Количество = СтрокаПодбора.Количество;
		Иначе
			Строка = Строки[0];
			Строка.Количество = Строка.Количество + СтрокаПодбора.Количество;
		КонецЕсли;
	КонецЦикла;
	```







