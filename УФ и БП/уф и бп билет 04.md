## Билет 4 (занятия 122 - 124)

Для контроля правильности формирования движений по документу «Расходная накладная», необходимо создать управляемые основные форму документа и форму списка, чтобы пользователь мог иметь возможность просмотра движений этого документа по всем регистрам, в которые документ делает записи. Доступ к данным регистров должен осуществляться в форме документа из панели навигации, а в форме списка из командной панели формы.

В целях ведения кадрового учета необходимо предоставить пользователю возможность для каждого сотрудника хранить его фотографию. Выбор и просмотр фотографии необходимо осуществлять из формы соответствующего элемента справочника «Сотрудники». Сама фотография должна храниться либо в информационной базе, либо в виде отдельного файла, в зависимости от желания пользователя. Запись картинки в базу должна происходить только при записи самого элемента справочника, а отображение на форме сразу же после выбора, то есть, до записи.


### Панель движений

1. Форма документа
	Создаем форму документа, открываем.

	Открываем `Командный интерфейс` (рядом со вкладкой Элементы). На группе `Панель навигации -> Перейти` ставим флажки `Видимость` (первая колонка).
	> Если нет плюсика рядом с `Перейти`, то стоит проверить правильность регистров: включены в подсистемы и пр.

2. Форма списка
	Создаем форму списка, открываем.

	Переходим `Команды -> Глобальные команды`. В части `Параметризуемые` выносим на форму необходимые регистры.


### Фотографии сотрудников


1. Настройка физ.лиц
	Создаем реквизиты:
	|Реквизит |Тип |Назначение |
	|:-- |:-- |:-- |
	|Фотография |Временное хранилище |Хранение фото в БД |
	|ПутьКФотографии |Строка (неогранич.) |Хранение путь к фото на компьютере|

2. Вывод картинки на форму
	Создаем реквизит формы - АдресКартинки (строка), выносим на форму.
	|Реквизит |Тип |Назначение |
	|:-- |:-- |:-- |
	|АдресКартинки |Строка (неогранич.) |Для вывода изображения пользователю |
	|ЗаписыватьКартинкуВБазу |Булево |Определяем при записи, нужно ли записывать картинку или ничего не изменилось|
	
	Настраиваем `АдресКартинки`:
	- Вид: `Поле картинки`
	- выставляем флажок `Гиперссылка`

3. Открытие и запись элемента
	При открытии:
	- если заполнен `Путь к фотографии`, то через попытку пробуем прочитать и отобразить на форме
	```1c
	&НаКлиенте
	Процедура ПриОткрытии(Отказ)
		Попытка
			Если ЗначениеЗаполнено(Объект.ПутьКФотографии) Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьФайлПоПутиЗавершение", ЭтаФорма);
				// обязательно ставим УникальныйИдентификатор
				НачатьПомещениеФайлаНаСервер(ОписаниеОповещения, , , , Объект.ПутьКФотографии, УникальныйИдентификатор);	
			КонецЕсли;
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение.Сообщить();
		КонецПопытки;
	КонецПроцедуры

	&НаКлиенте
	Процедура ЗагрузитьФайлПоПутиЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
		Если ОписаниеПомещенногоФайла = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// показываем картинку пользователю
		АдресКартинки = ОписаниеПомещенногоФайла.Адрес;
	КонецПроцедуры
	```

	При чтении (на сервере):
	- устанавливаем флажок `ЗаписыватьКартинкуВБазу` - Ложь
	- если мы читаем картинку из БД (не заполнен `ПутьКФотографии`), то получаем данные из временного хранилища и записываем в `АдресКартинки`:
	```1c
	&НаСервере
	Процедура ПриЧтенииНаСервере(ТекущийОбъект)
		ЗаписыватьКартинкуВБазу = Ложь;
		
		Если НЕ ЗначениеЗаполнено(Объект.ПутьКФотографии) Тогда
			АдресКартинки = ПоместитьВоВременноеХранилище(ТекущийОбъект.Фотография.Получить());
		КонецЕсли;
	КонецПроцедуры
	```

	Перед записью (на сервере):
	- Если установлен флаг `ЗаписыватьКартинкуВБазу`, то
		- устанавливаем флаг - Ложь
		- создаем из адреса временного хранилище и записываем в реквизит `Фотография`
	```1c
	&НаСервере
	Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		Если ЗаписыватьКартинкуВБазу Тогда 
			ЗаписыватьКартинкуВБазу = Ложь;
			ТекущийОбъект.Фотография = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресКартинки));
		КонецЕсли;
	КонецПроцедуры
	```

4. Сохранение картинки

	```1c
	// выбор картинки
	&НаКлиенте
	Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
		СтандартнаяОбработка = Ложь;   
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтаФорма);
		// обязательно ставим УникальныйИдентификатор
		НачатьПомещениеФайлаНаСервер(ОписаниеОповещения, , , , , УникальныйИдентификатор);
	КонецПроцедуры


	// обязательно экспортная
	&НаКлиенте
	Процедура ВыборФайлаЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
		Если ОписаниеПомещенногоФайла = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// вопрос про сохранение в БД или оставить на компьютере
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОписаниеПомещенногоФайла", ОписаниеПомещенногоФайла);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросСохранениеЗавершение", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, "Вы хотите записать картинку в базу данных?", РежимДиалогаВопрос.ДаНетОтмена);
	КонецПроцедуры  

	// обязательно экспортная
	&НаКлиенте
	Процедура ВопросСохранениеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
		// обработка ответов пользователя
		Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
			// хранить в БД. удаляем старый путь к фотографии, чтобы открывалось тольо из БД
			ЗаписыватьКартинкуВБазу = Истина;
			Объект.ПутьКФотографии = "";
		ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
			// записываем путь к фото
			Объект.ПутьКФотографии = ДополнительныеПараметры.ОписаниеПомещенногоФайла.СсылкаНаФайл.Файл.ПолноеИмя;
		Иначе 
			Возврат;
		КонецЕсли; 
		
		// показываем пользователю фото вне зависимости от выбора сохранения
		АдресКартинки = ДополнительныеПараметры.ОписаниеПомещенногоФайла.Адрес;
	КонецПроцедуры
	```
