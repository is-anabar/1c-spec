# Билет 3 (занятия 116 - 118)

В справочнике «Контрагенты» необходимо создать управляемую основную форму элемента, в которой пользователь сможет увидеть все движения с участием этого контрагента по регистру бухгалтерии. Доступ к этой информации должен осуществляться из панели навигации.

В форме документа «Приходная накладная» необходимо предоставить пользователю возможность вводить произвольный текстовый комментарий. Текст комментария может содержать навигационную ссылку (не более одной) на документ оплаты. Переход по навигационной ссылке должен осуществляться при нажатии кнопки открытия, созданной у данного элемента управления формы. 

При проведении документа «Расходная накладная» в случае нехватки товара должно появляться сообщение с указанием реального количества. Данное сообщение должно быть привязано к полю «Количество» соответствующей строки табличной части документа. В случае наличия дублей строк сообщение должно быть связано с первой строкой, содержащей проблемную номенклатуру.


## Предварительная подготовка

При необходимости добавить Контрагента в проводки по бухгалтерии.


## Движения контрагента

### Настройка формы

Создать произвольную форму.

Добавить на форму реквизит `Движения` (динамический список). Выставить флаги:
- произвольный запрос

### Получение списка движений

Открыть запрос для `Движения`:
- формируем запрос по `ДвиженияССубконто`; для отбора по контрагенту выставляем в условия виртуальной таблицы условие:
```1c
Субконто1 = &Контрагент,
// Субконто2 и Субконто3 аналогично - при наличии
```
- выбираем (под текстом запроса) `Основная таблица`
- выставляем (там же) флаг `Динамическое считывание данных`


При открытии формы устанавливаем отбор по параметру:
```1c
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// если при открытии формы пришёл контрагент,
	// то переменная заполняется переданным значением
	Контрагент = Неопределено;
	Параметры.Свойство("Контрагент", Контрагент);
	
	// будут передаваться из параметров открытия формы,
	// об этом в следующем пункте
	Движения.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
КонецПроцедуры
```

### Отображение движений 

Создаём в справочнике `Контрагенты` (рядом с реквизитами и табличными частями) команду для открытия движений:
- группа - `Панель навигации формы.Важное`
- тип параметра команды - `СправочникСсылка.Контрагенты`
- заполняем код команды
```1c
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	// передаём при открытии формы
	ПараметрыФормы = Новый Структура("Контрагент", ПараметрКоманды);
	// внимательно следим, что открывается нужная форма
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаДвижений", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
КонецПроцедуры
```

## Комментарий с ссылкой

### Вывод на форму

Создаём необходимый реквизит

Создаём форму (если не было). Для поля `Комментарий` выбираем:
- кнопка открытия - да
- автомаксимальная ширина - снимаем флажок

### Открытие ссылки

3В обработке `Открытие` поля `Комментарий` код по открытию ссылки:
```1c
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	// отключаем стандартную обработку
	СтандартнаяОбработка = Ложь;
	
	Текст = Объект.Комментарий;
	Слова = СтрРазделить(Текст, " "); 
		
	// здесь вставить нужный фрагмент ссылки на документ
	СсылкаНаРасходную = "e1cib/data/Документ.РасходнаяНакладная?ref=";
	
	Для Каждого Слово Из Слова Цикл
		Если НЕ СтрНачинаетсяС(Слово, СсылкаНаРасходную) Тогда
			Продолжить;
		КонецЕсли; 
			
		Попытка
			// переход по ссылке
			ПерейтиПоНавигационнойСсылке(Слово);
		Исключение 
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение.Сообщить();
		КонецПопытки;  
			
		Возврат;
	КонецЦикла;
КонецПроцедуры
```


## Сообщение с привязкой к полю

### Получение иномера строки

В запросе на суммирование списка номенклатуры в ТЧ, помимо суммирования количества, берём минимум из номера строки. Примерно:
```1c
ВЫБРАТЬ
	МИНИМУМ(РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки) КАК НомерСтроки,
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
ИЗ
	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
ГДЕ
	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
СГРУППИРОВАТЬ ПО
	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
```

### Вывод сообщения

При выводе сообщения о недостаче выводим с привязкой к полю:
```1c
Сообщение = Новый СообщениеПользователю;
// текст сообщения...
ИндексСтроки = Выборка.НомерСтроки - 1;
Сообщение.Поле = СтрШаблон("Объект.СписокНоменклатуры[%1].Количество", ИндексСтроки);
Сообщение.Сообщить();
```
