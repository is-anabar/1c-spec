## Билет 2 (занятия 125 - 126)

С помощью регламентного задания необходимо по расписанию каждый день формировать задачи сотрудникам на создание отчет о проделанной работе. Задача должна порождаться для тех сотрудников, которые числятся в регистре сведений «Сведения о сотрудниках» на момент формирования задачи. По мере возникновения этих задач у пользователя должна автоматически открываться форма каждой невыполненной его задачи.

Затем в задании будет представлен скриншот внешнего вида формы задачи.

На форме следующие поля:
- Номер: 000000001, Дата: 21.01.2014 20:25:38
- Наименование: Отчет  о проделанной работ
- Исполнитель: Иванюхин

Перечень работ (многострочное поле):
1.	Работал работу на работе
2.	Еще немного работал
3.	Посещал столовую


На рабочем столе необходимо отобразить список невыполненных задач текущего пользователя. Кроме того, пользователь должен иметь возможность видеть полный список задач.


### Пользователи и параметры сеанса

Стандартное заполнение, прочитать можно [здесь](/УФ%20и%20БП/уф%20общее.md)


### Задачи

Создаем задачу с реквизитами:
- `Исполнитель` - Физлица
- `Описание` - строка (неогран.), многострочный режим

Можно увеличить длину наименования до 150.


### Список задач, которые нужно показать пользователю

Создаем общий модуль (сервер, вызов сервера) для получения задач текущего сотрудника.

```1c
Функция ПолучитьСписокЗадачПользователя() Экспорт
	
	ФизЛицо = ПараметрыСеанса.ТекущийПользователь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачиИсполнителям.Ссылка КАК Ссылка
		|ИЗ
		|	Задача.ЗадачиИсполнителям КАК ЗадачиИсполнителям
		|ГДЕ
		|	ЗадачиИсполнителям.Исполнитель = &ФизЛицо
		|	И НЕ ЗадачиИсполнителям.Выполнена
		|	И НЕ ЗадачиИсполнителям.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	СписокЗадач = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		СписокЗадач.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат СписокЗадач;

КонецФункции
```

В модуле приложения (Корень конфигурации - клик ПКМ) заполняем метод `ПриНачалеРаботыСистемы`.
```1c
Процедура ПриНачалеРаботыСистемы()
	// процедура
	// инвервал
	// показывать однократно
	ПодключитьОбработчикОжидания("ПоказатьФормуЗадач", 5, Истина);
КонецПроцедуры 

Процедура ПоказатьФормуЗадач() Экспорт
	СписокЗадач = ОбщегоНазначенияВызовСервера.ПолучитьСписокЗадачПользователя();
	
	Для Каждого Задача Из СписокЗадач Цикл 
		// Ключ необходим для того, чтобы открывалась форма конкретной задачи
		ПараметрыФормы = Новый Структура("Ключ", Задача);
		ОткрытьФорму("Задача.ЗадачиИсполнителям.ФормаОбъекта", ПараметрыФормы);
	КонецЦикла;
КонецПроцедуры

```


### Список задач, которые нужно создать

Создаем общий модуль (сервер) для получения списка сотрудников.

Чтобы получить список пользователей, для которых нужно создать задачи:
- берем данные по пользователям с задачи за сегодняшний день (на начало дня); задачи не должны быть выполнены
- берем данные из `СведенияОСотрудниках`, где сотрудники не попадают в список из предыдущего пункта

```1c
// ОбщегоНазначенияСервер
Процедура СформироватьСписокЗадач() Экспорт
	НачалоДня = НачалоДня(ТекущаяДата());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗадачиИсполнителям.Дата КАК Дата,
		|	ЗадачиИсполнителям.Исполнитель КАК Исполнитель
		|ПОМЕСТИТЬ втЗадачиНаДень
		|ИЗ
		|	Задача.ЗадачиИсполнителям КАК ЗадачиИсполнителям
		|ГДЕ
		|	ЗадачиИсполнителям.Дата = &НачалоДня
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Исполнитель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОСотрудникахСрезПоследних.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|			&НачалоДня,
		|			НЕ Сотрудник В
		|					(ВЫБРАТЬ
		|						втЗадачиНаДень.Исполнитель КАК Исполнитель
		|					ИЗ
		|						втЗадачиНаДень КАК втЗадачиНаДень)) КАК СведенияОСотрудникахСрезПоследних";
	
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Задача = Задачи.ЗадачиИсполнителям.СоздатьЗадачу();
		Задача.Дата = НачалоДня;
		Задача.Наименование = "Отчет о проделанной работе";
		Задача.Исполнитель = Выборка.Сотрудник;
		Задача.Записать();
	КонецЦикла;
	
КонецПроцедуры
```


### Регламентное задание

Создаем регламентное задание. В качестве имени метода выбираем `ОбщегоНазначенияСервер.СформироватьСписокЗадач`.


Для запуска регламентного задания создаем обработку.

Создаем форму обработки, для удобства создаем реквизит формы `ВремяНачала` (тип Дата - Время).

При нажатии на кнопку должны выполняться действия:
- удаляем предыдущие задачи
- создаем новую с заданным расписанием

```1c
&НаСервере
Процедура КомандаЗапуститьРЗНаСервере() 
	// удаление существующих задач
	Задания = РегламентныеЗадания.ПолучитьРегламентныеЗадания();
	Для Каждого Задание Из Задания Цикл
		Задание.Удалить();
	КонецЦикла;
	
	// создание новой задачи
	МетаданныеЗадачи = Метаданные.РегламентныеЗадания.СоздатьЗадачиПользователям;
	РЗ = РегламентныеЗадания.СоздатьРегламентноеЗадание(МетаданныеЗадачи);
	
	// один раз в день
	РЗ.Расписание.ПериодПовтораДней = 1;
	// начиная с указанного времени
	РЗ.Расписание.ВремяНачала = ВремяНачала;
	
	РЗ.Записать();
	
КонецПроцедуры

```





