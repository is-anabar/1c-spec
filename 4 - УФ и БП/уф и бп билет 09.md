# Билет 9

Фотография в справочнике «Сотрудники» - выбирать фотку из файла и сохранять в самой базе. 


## Примечание

Билет похож на №4


## Настройка физических лиц

### Реквизиты

Создаем реквизиты:
- ФотографияХЗ: Хранилище значений

### Форма документа

Создаем форму документа

Добавляем реквизит формы:
- АдресКартинки: строка (неогранич.)

Выносим реквизит `АдресКартинки` на форму и ставим:
- Вид: поле картинки
- Гиперссылка: да

### Код

Необходимо сделать:
- Код при чтении на сервере (для отображения картинки из базы)
- Код при записи на сервере (для сохранения картинки)
- Код при выборе картинки

```1c
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.ФотографияХЗ) Тогда
		Возврат;
	КонецЕсли;
	
    // получаем данные из хверенного хранилища из базы
	Фотография = ТекущийОбъект.ФотографияХЗ.Получить();
    // отображаем данные на форме
	АдресКартинки = ПоместитьВоВременноеХранилище(Фотография);
КонецПроцедуры
```

```1c
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    // получаем фотографию в виде двоичных данных
	Фотография = ПолучитьИзВременногоХранилища(АдресКартинки);
    // записываем во временное хранилище
	ХЗ = Новый ХранилищеЗначения(Фотография);
    // сохраняем в базе
	ТекущийОбъект.ФотографияХЗ = ХЗ;
КонецПроцедуры
```

```1c
&НаКлиенте
Процедура АдресКартинкиНажатие(Элемент, СтандартнаяОбработка)
    // отключаем стандартную обработку
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтаФорма);
    // указываем уникальный идентификатор
	НачатьПомещениеФайлаНаСервер(Описание, , , , ,УникальныйИдентификатор);
КонецПроцедуры 

// обязательно экспортная процедура
&НаКлиенте
Процедура ВыборФайлаЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
    // показываем картинку пользователю
	АдресКартинки = ОписаниеПомещенногоФайла.Адрес;
КонецПроцедуры

```
