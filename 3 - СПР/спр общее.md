# Общие моменты по СПР

## Оглавление
- [Совместительство](#part-time)
- [Графики работы](#shifts)
- [Стандартное заполнение планов видов расчета](#account-plan)
- [Стандартное заполнение регистров расчета](#account-register)
- [Стандартный алгоритм расчета ЗП](#salary)
- [Стандартный алгоритм расчета премии](#bonus)
- [Сторно](#storno)
- [Когда дни/часы заносятся в ресурсы](#days-as-resource)
- [Перерасчеты](#recount)


## <a name="part-time"></a> Совместительство

Если в билете допускается совместительство, то `Подразделение` нужно добавить:
- в документ `НачислениеЗарплаты` (в нужные табличные части)
- в регистр сведений `СведенияОСотрудниках` в качестве измерения
- в регистры расчета `ОсновныеНачисления / ДополнительныеНачисления`


## <a name="shifts"></a> Графики работы

Если в билете есть указание, что графики работы существуют, то:
- создаем справочник `ГрафикиРаботы`
- добавляем график: 
	- в документ `НачислениеЗарплаты` (в нужные табличные части)
	- в регистр сведений `ГрафикиРаботы` в качестве измерения
	- в регистры расчета `ОсновныеНачисления / ДополнительныеНачисления` как реквизит


## <a name="account-plan"></a> Стандартное заполнение планов видов расчета


Если выплата имеет временной промежуток с периодом начала и конца (например, зарплата, больничный, командировка), то на вкладке `Расчеты` выставляем флаг `Использует период действия`.

Если выплата имеет какую-то основу для расчета (например, премия зависит от оклада за прошлый месяц, премия начальнику бригады - от премии остальных в бригаде за этот месяц), то на вкладке `Расчеты` выставляем флаг `Зависит по периоду действия` и выбираем необходимые планы видов расчета.
> Сама по себе премия при этом не имеет срока действия - она выплачивается раз в месяц, а не с 01.06 по 30.06, например. Поэтому для премий не нужно использовать период действия.


## <a name="account-register"></a> Стандартное заполнение регистров расчета

### Настройки регистра

Необходимо сделать настройку регистра в зависимости от плана видов расчета (ПВР):
- если у ПВР установлен флаг `Использует период действия`, то в регистре расчета устанавливаем `Период действия`- да, заполняем поля по регистру сведений `ГрафикиРаботы`
- если у ПВР установлен флаг `Зависит по периоду действия`, то в регистре расчета устанавливаем `Базовый период` - да

### Данные регистра

Базовым набором данных будет:
- измерение `Сотрудник`
- ресурс `Результат`

Если есть совместительство, то добавляется измерение `Подразделение`.

Если есть графики работы, то добавляется реквизит `ГрафикРаботы`.

Остальные реквизиты добавляются по необходимости.



## <a name="salary"></a> Стандартный алгоритм расчета ЗП

### Предварительно записываем данные из ТЧ в движения

Переносим данные из ТЧ в движения по регистру расчета. На данном этапе заполняются только измерения, некоторые ресурсы (например, график работы). Заполнение часов, которые человек должен отработать и которые он на самом деле отработал, результат расчета будет рассчитываться далее.

```1c
Движения.ОсновныеНачисления.Записывать = Истина;
Для Каждого Строка Из ОсновныеНачисления Цикл
	Движение = Движения.ОсновныеНачисления.Добавить();
	ЗаполнитьЗначенияСвойств(Движение, Строка);
		
	Движение.ПериодРегистрации = ПериодРегистрации;
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	// приводим к концу дня
	Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
КонецЦикла;
```
> Приводить к концу дня `ПериодДействияКонец` нужно везде, кроме билета с 15-минутками

### Получаем отработанные и плановые часы/дни

Данные по отработанному и плановому времени хранятся в виртуальной таблице `ДанныеГрафика`. Регистр расчета сам определяет результат на основе регистра сведений `ГрафикиРаботы`.
```1c
ВЫБРАТЬ
	// здесь хранится номер строки в ТЧ документа
	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	// часы, которые должен был отработать человек
	// берем с ЕСТЬNULL()
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК ЧасовПлан,
	// часы, которые человек на самом деле отработал
	// берем с ЕСТЬNULL()
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ЧасовФакт
ИЗ
	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		Регистратор = &Ссылка И ВидРасчета В (&ВидыРасчета))
```
 Название `ЧасовПериодДействия` взялось от соединения `Часов` (название рерусра в регистра сведений **ГрафикиРаботы**) и `ПериодДействия`. Если ресурс будет называться `Значение`, то будет `ЗначениеПериодДействия`. Аналогично для `ЧасовФактическийПериодДействия`.

`ВидыРасчета` - массив с необходимыми видами расчета из плана видов расчета. 

### Расчет ЗП

Чаще всего данные для оклада нужно брать из регистров сведений (например, `СведенияОСотруднике` или `ТарифнаяСтавка`). Поэтому:
- в первом подзапросе получаем данные по отработанным и плановым часам/дням + сотрудник + подразделение (если есть совместительство); помещаем во временную таблицу, например, `втДанныеГрафика`
- во втором подзапросе соединяемся с необходимым регистром сведений для получения оклада / тарифной ставки; не забываем делать в таблицах `СрезПоследних` делать отбор по дате и измерениям (сотрудник + подразделение при наличии)
 
Итоговый запрос будет выглядеть примерно так:
```1c
ВЫБРАТЬ
	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ЧасовФакт,
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК ЧасовПлан
ПОМЕСТИТЬ втДанныеГрафика
ИЗ
	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика

ИНДЕКСИРОВАТЬ ПО
	Сотрудник
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
	втДанныеГрафика.ЧасовФакт КАК ЧасовФакт,
	втДанныеГрафика.ЧасовПлан КАК ЧасовПлан,
	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад
ИЗ
	втДанныеГрафика КАК втДанныеГрафика
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
				&ПериодРегистрации,
				Сотрудник В
					(ВЫБРАТЬ
						втДанныеГрафика.Сотрудник КАК Сотрудник
					ИЗ
						втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
```

После этого мы перебираем таблицу с движениями по основным начислениям, заполняем данными из запроса и выполняем рассчет ЗП.

```1c
Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
	// Поиск по списку всех движений
	// Становимся в самое начало результата запроса
	Выборка.Сбросить();

	// Бывает, что в движениях есть записи по двум и более видам расчета,
	// а нам нужно рассчитывать только один из них.
	// В качестве дополнительной проверки добавляем поиск по выборке.
	// Если данных нет, то продолжаем перебирать движения
	Если НЕ Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
		Продолжить;
	КонецЕсли;

	// Здесь обычно заполняются реквизиты Оклад, ЧасовФакт и пр.	
	ЗаполнитьЗначенияСвойств(Движение, Выборка);

	// Расчет ЗП
	Движение.Результат = ?(Выборка.ЧасовПлан = 0, 0,
		Выборка.ЧасовФакт / Выборка.ЧасовПлан * Выборка.Оклад);
		
КонецЦикла;
```

### Запись обновленных данных

После рассчета ЗП необходимо обновить запись о движениях с помощью кода 

```1c
Движения.ОсновныеНачисления.Записать(, Истина);
```
> Обязательно нужно указывать вторым параметром Истина, иначе отнимут баллы. Распространяется на планы видов расчета, у которых стоит флаг `Использует период действия`. Чаще всего это виды расчета, которые имеют `срок действия` - например, Оклад

> Если при перезаписи движений возникает ошибка, то, скорее всего, пытаемся перезаписать измерения (например, сотрудник) или ключевые значения (например, вид расчета)


## <a name="bonus"></a> Стандартный алгоритм расчета премии

Здесь под премией подразумевается какая-то выплата, которая:
- сама по себе не имеет периода действия (т.е. выплачивается один раз, а не в период с 01.05 по 31.05)
- рассчитывается в зависимости от другой выплаты, например, зарплаты за этот месяц (зарплата * процент)

В плане видов расчета будет стоять:
- `Зависит по периоду действия` - ОсновныеНачисления и (или) ДополнительныеНачисления


<!-- Про премию, которая на основе часов
из данных графика берем ЧасовБазовыйПериод -->

### Предварительно записываем данные из ТЧ в движения

Переносим данные из ТЧ в движения по регистру расчета. На данном этапе заполняются только измерения, некоторые ресурсы (например, график работы). Заполнение часов, которые человек должен отработать и которые он на самом деле отработал, результат расчета будет рассчитываться далее.

Чаще всего дополнительные начисления будут рассчитываться в зависимости от других выплат (например, от зарплаты за прошлый месяц или процентом от премии за текущий месяц). Чтобы показать, за какой период нужно брать выплаты, указываем `БазовыйПериодНачало` и `БазовыйПериодКонец`

```1c
Движения.ДополнительныеНачисления.Записывать = Истина;
Для Каждого Строка Из ДополнительныеНачисления Цикл
	Движение = Движения.ДополнительныеНачисления.Добавить();
	ЗаполнитьЗначенияСвойств(Движение, Строка);
		
	Движение.ПериодРегистрации = ПериодРегистрации;

	// Для определения, по какому периоду брать выплаты
	Движение.БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -1);
	Движение.БазовыйПериодКонец = ПериодРегистрации - 1;
КонецЦикла;
```

### Получаем базу по выплатам

После этого можно получать данные для расчета. Чтобы получить базу (основу для начислений, например, зарплату за прошлый месяц), необходимо обратиться к виртуальной таблице `БазаОсновныеНачисления`.

В ней будут доступны поля `[Имя ресурса регистра расчета] + База`. Например, в регистре расчета зарплата хранится в ресурсе `Результат`. В `БазаОсновныеНачисления` поле `РезультатБаза` будет хранить в себе зарплату за базовый период (например, за прошлый месяц).

> В ресурсах можно хранить не только выплату (результат), но и другие данные: например, дней [в командировке]. Зачем это использовать, рассказано [здесь](#days-as-requisite).

Для получения основы для начисления необходимо взять данные по `РезультатБаза`. 

Также для виртуальной таблицы требуется заполнение параметров - `измерений`. Чаще всего они будут совпадать с измерениями регистра расчета. Для передачи в запрос измерения передаются как массив строк, где строки содержат наименование измерения:
```1c
Измерения = Новый Массив;
Измерения.Добавить("Сотрудник");
```

> В некоторых билетах требуется получить премию по подразделению, но с разделением по сотрудникам. Тогда в качестве измерений будет только `Подразделение` (вместо подразделение + сотрудник для обычной премии) + понадобятся разрезы. Подробнее можно прочитать [здесь](#dimensions-and-sections).

Итоговый запрос будет выглядеть примерно следующим образом:
```1c
ВЫБРАТЬ
	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	// итог по базовому периоду для ресурса Результат
	ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК ОсноваДляРасчетаПремии
ИЗ
	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
			&Измерения,
			&Измерения,
			,
			Регистратор = &Ссылка
				И ВидРасчета.ВидРасчета В (&ВидыРасчета)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
```

После этого мы перебираем таблицу с движениями по дополнительным начислениям, заполняем данными из запроса и выполняем рассчет выплаты.

```1c
Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
	// Поиск по списку всех движений
	// Становимся в самое начало результата запроса
	Выборка.Сбросить();

	// Бывает, что в движениях есть записи по двум и более видам расчета,
	// а нам нужно рассчитывать только один из них.
	// В качестве дополнительной проверки добавляем поиск по выборке.
	// Если данных нет, то продолжаем перебирать движения
	Если НЕ Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
		Продолжить;
	КонецЕсли;

	// Здесь обычно заполняются реквизиты Оклад, ЧасовФакт и пр.	
	ЗаполнитьЗначенияСвойств(Движение, Выборка);

	// Расчет ЗП
	Движение.Результат = Выборка.ОсноваДляРасчетаПремии * Движение.ПроцентПремии / 100;
		
КонецЦикла;
```


<!-- TO DO: обновить информацию о том, как получить часы/дни базового периода
Дополнить расчет премии: если нужно получить часы, то тянем из данных графика
Например, ОсновныеНачисленияДанныеГрафика.ДнейБазовыйПериод

Не работает для табеля, т.к. фактические часы хранятся в регистре накопления, а не в данных графика
 -->

### Запись обновленных данных

После рассчета выплат необходимо обновить запись о движениях с помощью кода 

```1c
Движения.ДополнительныеНачисления.Записать();
```
> Если план видов расчета не `Использует период действия`, то второй параметр (Истина) можно не заполнять.

> Если при перезаписи движений возникает ошибка, то, скорее всего, пытаемся перезаписать измерения (например, сотрудник) или ключевые значения (например, вид расчета)


## <a name="storno"></a> Ввод задним числом (сторно)

Рассмотрим ситуацию, когда есть оклад и больничный. 

В апреле мы записываем, что человек проработал с 01.04 по 30.04.

В мае мы вносим, что человек работал с 01.05 по 31.05, а также у него больничный с 02.04 по 10.04 (т.е. данные за прошлый месяц).

Здесь нужно убрать рабочие часы и оклад с 02.04 по 10.04 (период больничного), вместо этого сделать выплату по больничным.


### Вытеснение
Чтобы больничный автоматически "убирал" оклад, необходимо в плане видов расчета для оклада на вкладке `Вытесняющие` выбрать больничный.

### Записи для сторнирования

С помощью вытеснения система сама может определить, что какие-то записи за прошлый период нужно изменить. Получить их можно с помощью следующего кода:

```1c
ТаблицаДополнений = Движения.ОсновныеНачисления.ПолучитьДополнение();
```

Далее создаем отменяющие движениядля прошлого периода:
- обязательно указываем флаг `Сторно` для каждого движения
- заполняем ПериодРегистрации, ПериодДействияНачало, ПериодДействияКонец с помощью `ПериодРегистрацииСторно, ПериодДействияНачалоСторно, ПериодДействияКонецСторно`
- заполняем свойства; тут стоит брать не все подряд, а только ключевые + оклад

```1c
Для Каждого Строка Из ТаблицаДополнений Цикл 
	Движение = Движения.ОсновныеНачисления.Добавить();
	// обязательно указываем, что это отменяющие записи
	Движение.Сторно = Истина;
	// заполняем период регистрации и период действия отменяющих записей
	Движение.ПериодРегистрации = Строка.ПериодРегистрацииСторно;
	Движение.ПериодДействияНачало = Строка.ПериодДействияНачалоСторно;
	Движение.ПериодДействияКонец = Строка.ПериодДействияКонецСторно;
		
	// лучше не заполнять все свойства, а только ключевые, т.е.
	// ВидРасчета, Сотрудник, Подразделение, ГрафикРаботы
	//
	// также стоит взять Оклад, он пригодится для расчета,
	// сколько выплат нужно отнять при отмене рабочих часов
	ЗаполнитьЗначенияСвойств(Движение, Строка, "ВидРасчета, Сотрудник, Подразделение, ГрафикРаботы, Оклад");
КонецЦикла;
```

### Расчет оклада

Алгоритм расчета ЗП здесь почти не отличается от обычного расчета. Единственное, что стоит изменить, это определение оклада:
```1c
ВЫБОР
	// если сторно, то берем оклад из периода сторно
	КОГДА втДанныеГрафика.Сторно
		ТОГДА втДанныеГрафика.Оклад
	ИНАЧЕ ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0)
КОНЕЦ КАК Оклад
```

> Что будет, если брать текущий оклад?
> 
> Допустим, у Бельдыева с 01.04.2025 был оклад 3000 рублей. При расчете зарплаты за апрель он получил 3000 рублей.
>
> С 01.05.2025 ему подняли зарплату до 100000 рублей. При расчете выплат ему внесли записи: оклад (01.05 - 31.05) и больничный (02.04 - 10.04). Если использовать текущий оклад для расчета сторно, то Бельдыеву отнимут 8 [рабочий дней, которые он проболел] * 5000 [примерная дневная ставка], т.е. 40000 рублей, хотя за прошлый месяц он получил только 3000.
>
> Чтобы избежать такой ситуации, оклад при сторнировании берется из того периода, который мы отнимаем (т.е. оклад с апреля). 

Стоит также учитывать, что при сторнировании мы должны не прибавлять, а отнимать зарплату + убирать рабочие часы / дни, которые были заполнены с помощью `ЗаполнитьЗначениеСвойств()`

```1c
Если Движение.Сторно Тогда
	Движение.Результат = -1 * Движение.Результат;
	Движение.ДнейФакт = 0;
КонецЕсли;
```



## <a name="days-as-resource"></a> Про случаи, когда дни/часы нужно добавить в ресурсы


### Для расчета оклада используются часы, для других начислений - дни

<!-- 
Ситуация:
Оклад - по часам, больничный - по дням. Так как оба вида расчета находятся в одном вида расчета - Основные начисления - и имеют период действия, то данные о больничном (дней больничного и дней работы по плану) можно вытянуть из ДанныеГрафика. В этом случае добавлять дни в ресурсы НЕ НУЖНО

Ситуация
Оклад - по часам, премия - считается по отработанным дням. Так как оклад - Основные начисления - имеет период действия, а премия - Дополнительные начисления - не имеет, то из ДанныеГрафика вытятянуть информацию о дней факт и план для премии нельзя (премия и окалд в разных планах расчета), то ДнейФакт и(или) ДнеяПлан НУЖНО добавить в ресурсы.
 -->

В регистре рассчета на вкладке **Основные**, при установленном флаге **Использует период действия** можно заполнить поля для графика, значения графика и даты графика.

Обычно в значении графика указывают **Часов**, поэтому для них будет автоматически рассчитываться `ЧасовФактБаза` для базовых начислений.

`Дней` же нигде не указывается, поэтому и автоматического рассчета `ДнейФактБаза` не будет. Чтобы это исправить, `ДнейФакт` надо добавить в ресурсы.

Здесь может быть такая ситуация:
- Оклад рассчитывается по часам
- Премия/компенсация - по дням

> `ДнейФакт` можно оставить в реквизитах, если оклад и премия:
> - в одном плане видов характеристик (в том числе используется один регистр рассчета)
> - они рассчитываются за один и тот же период (например, месяц)


### Используется табель

Из-за того, что табель хранит фактические часы не в регистре расчета, а в регистре накопления, база отработанных часов - `ЧасовБазовыйПериод` - не рассчитывается. Чтобы это исправить, `ЧасовФакт` надо добавить в ресурсы.


## <a name="dimensions-and-sections"></a> Про измерения и разрезы по базе начислений



## <a name="time-sheet"></a> Табель

Для расчета данных по табелю необходимо 2 вещи: регистр накопления и документ

### Регистр накопления (оборотный) ДанныеТабеля

Добавляем:
- измерения `Сотрудник, ВидРасчета`, если есть совместительство - `Подразделение`
- ресурсы `Часов`


### Документ **Табель**

**Запрещаем** оперативное проведение.

Заводим табличную часть, где в колонках будет:
- Сотрудник
- День1 ... День31 - для месяца, День1 ... День7 - для недели

При проведении документа:
- если табель для месяца: определяем дней в месяце - с помощью `День(<КонецМесяца>)`
- по виду записи в `День...` определяем вид начисления и записываем строку в РН `ДанныеТабеля`


### Документ **НачислениеЗарплаты**

Для расчета ЗП необходимо:
- получить данные графика для оклада:
	- `ЧасовПлан` (он рассчитывается исходя из указанного графика)
	- `ПериодДействияНачало, ПериодДействияКонец` (для получение фактических часов из РН)
- соединиться с:
	- данными табеля (по сотруднику (и подразделению), виду часов, периоду - между периодом действия начало и конец)
- получаем сумму часов, максимум из оклада и часов план (они будут дублироваться для каждой строки с `ЧасовФакт`)

Пример запроса:
```1c
ВЫБРАТЬ
	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	// часов план рассчитывается с помощью графика работы
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК ЧасовПлан,
	// будем использовать для соединения с РН по часам
	ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
	ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец
ПОМЕСТИТЬ втЧасовПлан
ИЗ
	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
			Регистратор = &Ссылка
				И ВидРасчета В (&ВидыРасчета)) КАК ОсновныеНачисленияДанныеГрафика
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втЧасовПлан.НомерСтроки КАК НомерСтроки,
	МАКСИМУМ(втЧасовПлан.ЧасовПлан) КАК ЧасовПлан,
	СУММА(ДанныеТабеля.Часов) КАК ЧасовФакт
ИЗ
	втЧасовПлан КАК втЧасовПлан
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеТабеля КАК ДанныеТабеля
		ПО втЧасовПлан.Сотрудник = ДанныеТабеля.Сотрудник
			И втЧасовПлан.Подразделение = ДанныеТабеля.Подразделение
			И (ДанныеТабеля.ВидЧасов = &ВидЧасовОклад)
			// обязательно соединяемся в условии ПО,
			// а не ГДЕ; иначе система превратит соединение
			// во внутреннее
			И (ДанныеТабеля.Период МЕЖДУ втЧасовПлан.ПериодДействияНачало И втЧасовПлан.ПериодДействияКонец)

СГРУППИРОВАТЬ ПО
	втЧасовПлан.НомерСтроки
```



## <a name="recount"></a>Перерасчеты

Перерасчеты зависят от задачи. Рассмотрим на следующем примере:
- Основные начисления - `Оклад`:
	- Сотрудник
	- Подразделение
- Дополнительные начисления - `Премия`- зависит от ЗП за прошлый месяц
	- Сотрудник
	- Подразделение

Т.к. дополнительные начисления `зависят от` основных начислений, то:

### Устанавливаем ведущие виды расчета

Для зависимого расчета указываем те, на основе которых он считается.

В примере премия зависит от оклада, поэтому для `Премия` на вкладке `Ведущие` устанавливаем `Оклад`.

### Создание перерасчетов

В Дополнительных начислениях создаем перерасчет.

Измерения перерасчета нужно делать такие же, как у регистра расчета. В нашем случае это
- Сотрудник
- Подразделение

Для каждого измерения указываем:
- Измерение регистра: измерение из текущего вида расчета
- Данные ведущих регистров: такие же измерения из всех регистров, на основе которых считается выплата.

Для нашего примера - измерение `Подразделение`:
- Измерение регистра: `Подразделение`
- Данные ведущих регистров: `Подразделение` (из основных начислений)

Аналогично для сотрудника.

