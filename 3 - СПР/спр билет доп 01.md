# Билет 1 (дополнительный)


Начисление зарплаты сотрудникам предприятия осуществляется ежемесячно. Каждый сотрудник может работать только в одном подразделении компании, то есть без совместительства.

Все сотрудники работают по индивидуальному графику. 

Начисление фактически отработанных часов осуществляется документом "Учет рабочего времени".  Данные заносятся за неделю. Сформировать форму документа как на картинке (в шапке указывается сотрудник и недельный период - дата начала и дата окончания, в ТЧ – колонки дата, количество часов), сделать обработчик кнопки "Заполнить", при нажатии на которую будут формировать строки на неделю.

Сотрудники предприятия получают оплату по часовому тарифу. Сумма начисления по тарифу определяется как тарифная ставка, умноженная на количество фактически отработанных часов. В течение расчетного периода тарифная ставка может изменяться не чаще, чем один раз в день, но берется на начало расчетного периода. В информационной базе необходимо хранить историю его изменения.
Также сотрудникам может быть начислено поощрение в виде произвольной суммы.

Один календарный месяц в году* сотрудникам представляется оплачиваемый отпуск, размер которого определяется как количество календарных дней отпуска, умноженное на среднюю дневную ставку. Средняя дневная ставка определяется как сумма начислений по тарифу за последний год, поделенная на количество отработанных дней. Следует учесть, что данные об отпуске не могут вводиться в систему задним числом.

Механизм перерасчетов в рамках данной задачи использовать не надо.

Для анализа сделанных сотрудникам предприятия начислений в конфигурации необходимо предусмотреть отчет следующего вида:

|Подразделение  |Сотрудник  |Вид расчета  |Период 1  |Период 2  |...	  |Итого:   |
|---------------|-----------|-------------|----------|----------|---------|---------|



## Графики работы
Так как все работает по 5/2, то создание справочника не требуется.


## Регистр сведений Графики работы

- Измерения:
	- Дата
- Ресурсы:
	- Часов (для оклада)
	- Дней (для отпуска)


### Обработка Заполнение графика

Без изменений.


## Учет рабочего времени

Здесь учет рабочего времени аналогичен табелю.

### Регистр накопления Учет рабочего времени

- Измерения:
	- Сотрудник
- Ресурсы:
	- Часов (для оклада)
	- Дней (сколько отработали - для отпуска)

### Документ Учет рабочего времени

Создаем документ:
- Реквизиты:
	- Сотрудник
	- Начало недели
	- Конец недели
- Табличные части:
	- Основная:
		- Дата
		- Часов

Для заполнения графика по кнопке получаем данные из регистра сведений `Графики работы` за указанный период:
```1c
// форма документа
// кнопка Заполнить, на сервере

Запрос = Новый Запрос;
// получаем данные по периоду
Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГрафикиРаботы.Дата КАК Дата,
	|	ГрафикиРаботы.Часов КАК Часов
	|ИЗ
	|	РегистрСведений.ГрафикиРаботы КАК ГрафикиРаботы
	|ГДЕ
	|	ГрафикиРаботы.Дата МЕЖДУ &НачалоНедели И &КонецНедели";
	
Запрос.УстановитьПараметр("КонецНедели", Объект.КонецНедели);
Запрос.УстановитьПараметр("НачалоНедели", Объект.НачалоНедели);

// проверяем, что они есть	
РезультатЗапроса = Запрос.Выполнить();
Если РезультатЗапроса.Пустой() Тогда
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Не задан график работы на указанный период.";
	Сообщение.Сообщить(); 
		
	Возврат;
КонецЕсли;

// очищаем таблицу от старых данных, чтобы не было задвоений	
Объект.Основная.Очистить();

// заполняем табличную часть	
Выборка = РезультатЗапроса.Выбрать();
Пока Выборка.Следующий() Цикл
	Строка = Объект.Основная.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, Выборка);
КонецЦикла;
```


При проведении проверяем, чтобы был заполнен реквизит `Часов`.
```1c
Движения.УчетРабочегоВремени.Записывать = Истина;
Для Каждого Строка Из Основная Цикл 
	Если Строка.Часов = 0 Тогда
		Продолжить;
	КонецЕсли;
		
	Движение = Движения.УчетРабочегоВремени.Добавить();
	Движение.Период = Строка.Дата;
	Движение.Сотрудник = Сотрудник;
	Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад;
	Движение.Часов = Строка.Часов;
	// автоматически записыыаем отработанный день
	Движение.Дней = 1;
КонецЦикла;
```


## Планы видов расчета

### ПВР Основные начисления

- Вкладка `Расчеты`:
	- Использует период действия: да
	- Зависит по периоду действия
		- Основные начисления (Отпуск на основе данных Оклада)
- Элементы
	- Оклад
	- Отпуск
		- Базовые:
			- Оклад


## Регистры расчета

### РР Основные начисления

- Использует период действия: да, заполняем поля
- Базовый период: да
- Измерения:
	- Сотрудник
- Ресурсы:
	- Результат
	- ДнейФакт (для определения, сколько дней отработал человек)
- Реквизиты:
	- ЧасовФакт
	- Ставка
	- ДнейОтпуска
	- ДнейОтработано (количество отработанных дней за год)
	- ОсноваДляНачислений (сумма по всем окладам за предыдущий год)


## Документ Начисление зарплаты

### Предварительная запись движений

Переносим в движения все данные из таблицы `ОсновныеНачисления`. Для `Отпуск` заполняем еще и базовый период

```1c
ПериодРегистрации = НачалоМесяца(Дата);
БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -12);
БазовыйПериодКонец = ПериодРегистрации - 1;
	
Движения.ОсновныеНачисления.Записывать = Истина;
	
Для Каждого Строка Из ОсновныеНачисления Цикл
	Движение = Движения.ОсновныеНачисления.Добавить();
	ЗаполнитьЗначенияСвойств(Движение, Строка);
		
	Движение.ПериодРегистрации = ПериодРегистрации;
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
		
	Если Строка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Отпуск Тогда
		Движение.БазовыйПериодНачало = БазовыйПериодНачало;
		Движение.БазовыйПериодКонец = БазовыйПериодКонец;
	КонецЕсли;
КонецЦикла;
	
Движения.Записать();
```


### Расчет начислений

```1c
ПериодРегистрации = НачалоМесяца(Дата);
Основные = ПланыВидовРасчета.ОсновныеНачисления;
	
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	|	ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
	|	ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец,
	|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ДнейОтпуска
	|ПОМЕСТИТЬ втДанныеГрафика
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ОсновныеНачисленияДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	СУММА(ЕСТЬNULL(УчетРабочегоВремени.Часов, 0)) КАК ЧасовФакт,
	|	МАКСИМУМ(ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Ставка, 0)) КАК Ставка,
	|	СУММА(ЕСТЬNULL(УчетРабочегоВремени.Дней, 0)) КАК ДнейФакт, // берем дни по регистру накопления
	|	МАКСИМУМ(втДанныеГрафика.ДнейОтпуска) КАК ДнейОтпуска,
	|	МАКСИМУМ(ОсновныеНачисленияБазаОсновныеНачисления.ДнейФактБаза) КАК ДнейОтработано,
	|	МАКСИМУМ(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза) КАК ОсноваДляНачислений
	|ИЗ
	|	втДанныеГрафика КАК втДанныеГрафика
			// данные по отработанным дням и часам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.УчетРабочегоВремени КАК УчетРабочегоВремени
	|		ПО втДанныеГрафика.Сотрудник = УчетРабочегоВремени.Сотрудник
				// пишем условие в секции ПО, что левое соединение не стало внутренним
	|			И (УчетРабочегоВремени.Период МЕЖДУ втДанныеГрафика.ПериодДействияНачало И втДанныеГрафика.ПериодДействияКонец)
			// данные о ставке
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
	|				&ПериодРегистрации,
	|				Сотрудник В
	|					(ВЫБРАТЬ
	|						втДанныеГрафика.Сотрудник КАК Сотрудник
	|					ИЗ
	|						втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
	|		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
			// данные по отработанным дням и сумме окладов за год
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , ) КАК ОсновныеНачисленияБазаОсновныеНачисления
	|		ПО втДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанныеГрафика.НомерСтроки";
	
Измерения = Новый Массив;
Измерения.Добавить("Сотрудник");
	
Запрос.УстановитьПараметр("Измерения", Измерения);
Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
РезультатЗапроса = Запрос.Выполнить();
Выборка = РезультатЗапроса.Выбрать();
	
Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
	Выборка.Сбросить();
	Если НЕ Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
		Продолжить;
	КонецЕсли;
		
	Если Движение.ВидРасчета = Основные.Оклад Тогда
		// необходимо записывать ДнейФакт, чтобы потом посчитатлось, 
		// сколько дней было отработано за прошлый год 
		Реквизиты = "ЧасовФакт, Ставка, ДнейФакт";
		ЗаполнитьЗначенияСвойств(Движение, Выборка, Реквизиты);
			
		Движение.Результат = Выборка.ЧасовФакт * Выборка.Ставка; 
	ИначеЕсли Движение.ВидРасчета = Основные.Отпуск Тогда
		Реквизиты = "ДнейОтпуска, ДнейОтработано, ОсноваДляНачислений";
		ЗаполнитьЗначенияСвойств(Движение, Выборка, Реквизиты);
			
		Движение.Результат = ?(Выборка.ДнейОтработано = 0, 0,
			Выборка.ДнейОтпуска * Выборка.ОсноваДляНачислений / Выборка.ДнейОтработано);
	КонецЕсли;
КонецЦикла;
	
Движения.ОсновныеНачисления.Записать(, Истина); // обязательно со вторым параметром
```


## Отчет

```1c
ВЫБРАТЬ
	ОсновныеНачисленияФактическийПериодДействия.ПериодРегистрации КАК Период,
	ОсновныеНачисленияФактическийПериодДействия.Сотрудник КАК Сотрудник,
	ОсновныеНачисленияФактическийПериодДействия.ВидРасчета КАК ВидРасчета,
	ОсновныеНачисленияФактическийПериодДействия.Результат КАК Результат
ИЗ
	РегистрРасчета.ОсновныеНачисления.ФактическийПериодДействия КАК ОсновныеНачисленияФактическийПериодДействия
ГДЕ
	ОсновныеНачисленияФактическийПериодДействия.ПериодРегистрации МЕЖДУ &ДатаНачала И &ДатаОкончания
```


Для вывода отчета:
- На вкладке реквизиты переносим:
	- Результат
- Таблица
	- Строки 
		- (группировка) Сотрудник
			- (группировка) Вид расчета
	- Колонки
		- (группировка) Период
- Выбранные поля:
	- Результат