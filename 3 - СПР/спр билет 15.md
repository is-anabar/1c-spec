# Билет 15 (занятия 100-102)


Начисление зарплаты сотрудникам предприятия осуществляется ежемесячно с использованием метода отклонений. Каждый сотрудник может работать одновременно только в одном подразделении компании, то есть совместительство не допускается.
Сотрудники компании работают в 3 смены по графику сутки через двое.

Сотрудники предприятия получают оплату по окладу пропорционально отработанному времени в часах. Часовая ставка рассчитывается как начальное значение оклада, деленное на количество рабочих часов в том же периоде, что и фактически отработанные часы. Первоначальное значение оклада может изменяться не чаще, чем один раз в день, но берется на начало расчетного периода. В информационной базе необходимо хранить историю его изменения.

Сотрудники могут находиться на больничном, размер которого определяется как количество дней болезни, умноженное на среднюю дневную ставку. Дни болезни рассчитываются по пятидневному графику. Средняя дневная ставка определяется как сумма всех начислений за предыдущий месяц, поделенная на количество рабочих дней в предыдущем месяце. Следует учесть, что данные об отпуске не могут вводиться в систему задним числом.

В любой момент времени каждому из сотрудников может быть начислено поощрение в виде произвольной суммы денег.

Механизм перерасчетов в рамках данной задачи использовать не надо.

Ввод всех начислений происходит документом «Начисления зарплаты». Документ в расчетном периоде может быть только один (сразу для всех видов расчета), а может быть несколько (по одному для каждого отдельного вида расчета). Считать, что все данные вводятся только в пределах одного месяца, например, можно указать начисление оклада с 10.01 по 31.01, а запись оклад: с 10.01 по 03.02 вводить нельзя. В одном документе могут быть данные только за текущий расчетный период.

С помощью диаграммы Ганта показать фактический период действия записей с разбивкой по сотрудникам, и для каждого сотрудника – по видам расчета. Отчет может быть построен за любой расчетный период.



## Подразделения

Совместительства нет, поэтому убираем подразделения там, где они есть.


## Графики работы
Создаем справочник `ГрафикиРаботы`. Добавляем предопределенные элементы `Пятидневка` (обязательно), `Смена 1, Смена 2, Смена 3` (необязательно) .


## Регистр сведений Графики работы

- добавляем измерение `График работы`
- переименовываем `Значение` в `Часов`, добавляем `Дней`


## Обработка Заполнение графика

- добавляем флажок `ЭтоСменныйГрафик`;
- при заполнении учитываем флажок:
	- если сменный, то выходные считаются в зависимости от номера текущего дня; количество часов - 24;
	- если по пятидневке, то выходные считаются от номера дня недели


## План видов расчета Основные начисления

- на вкладе **Расчеты** ставим:
	- `Использует период действия` - да
	- `Зависит по периоду действия` - основные и дополнительные начисления
- добавляем `Больничный`. На вкладке `Базовые` выбираем все виды расчета (+ добавится `Произвольная сумма денег`)
- для `Оклад` на вкладке `Вытесняющие` выбираем "Больничный"


## План видов расчета Дополнительные начисления

- убираем все флаги с вкладки `Расчеты` убираем
- добавляем `Произвольная сумма денег`; не забываем ее выставить базой для `Больничный`


## Регистр расчета Основные начисления

- устанавливаем `Период действия`- да, заполняем поля
- устанавливаем `Базовый период` - да
- измерения:
	- `Сотрудник`
- ресурсы: 
	- `Результат`
	- `ДнейФакт` (для расчета количества рабочих дней в прошлом месяца)
- добавляем реквизиты:
	- `ГрафикРаботы, ЧасовФакт, ЧасовПлан, Оклад` (для оклада)
		- если при подсчете `ЧасовФакт и ЧасовПлан` будут показываться слишком большие цифры, то нужно установить связь реквизита `График работы` с измерением регистра `Графики работы`
	- `ДнейБольничного, ДнейБазовыйПериод, НачисленияБазовыйПериод` (для больничного)


## Регистр расчета Дополнительные начисления

- стандартные измерение `Сотрудник` и ресурс `Результат`


## Документ Начисление зарплаты

Общий алгоритм расчета аналогичен [билету 9](/СПР/спр%20билет%209.md).

- `Оперативное проведение` - **Запретить**
- в ТЧ `ОсновныеНачисления` добавляем поле `ГрафикРаботы`
- добавляем ТЧ для дополнительных начислений 

### Запись движений

Записываем основные начисления + сторно и дополнительные начисления.

```1c
БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -1);
БазовыйПериодКонец = ПериодРегистрации - 1;
	
Основные = ПланыВидовРасчета.ОсновныеНачисления;
	
Движения.ОсновныеНачисления.Записывать = Истина;
	
Для Каждого Строка Из ОсновныеНачисления Цикл
	Движение = Движения.ОсновныеНачисления.Добавить();
	ЗаполнитьЗначенияСвойств(Движение, Строка); 
		
	Движение.ПериодРегистрации = ПериодРегистрации;
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
		
	Если Строка.ВидРасчета = Основные.Больничный Тогда
		Движение.ГрафикРаботы = Справочники.ГрафикиРаботы.Пятидневка;

		Движение.БазовыйПериодНачало = БазовыйПериодНачало;
		Движение.БазовыйПериодКонец = БазовыйПериодКонец;
	КонецЕсли;
КонецЦикла;


// запись сторно
ТаблицаДополнений = Движения.ОсновныеНачисления.ПолучитьДополнение();
	
Для Каждого Строка Из ТаблицаДополнений Цикл
	Движение = Движения.ОсновныеНачисления.Добавить(); 
	Движение.Сторно = Истина;
	Движение.ПериодРегистрации = Строка.ПериодРегистрацииСторно;
	Движение.ПериодДействияНачало = Строка.ПериодДействияНачалоСторно;
	Движение.ПериодДействияКонец = Строка.ПериодДействияКонецСторно; 

	// заполняем оклад для расчета сторно
	// если брать оклад на период регистрации, а не на вериод больничного,
	// то может выйти неверная сумма	
	Реквизиты = "ВидРасчета, Сотрудник, ГрафикРаботы, Оклад";
	ЗаполнитьЗначенияСвойств(Движение, Строка, Реквизиты);
КонецЦикла;

// произвольная сумма
Движения.ДополнительныеНачисления.Записывать = Истина;
	
Для Каждого Строка Из ДополнительныеНачисления Цикл
	Движение = Движения.ДополнительныеНачисления.Добавить();
	ЗаполнитьЗначенияСвойств(Движение, Строка); 
		
	Движение.ПериодРегистрации = ПериодРегистрации; 
	Движение.Результат = Строка.Размер;
КонецЦикла;
			
Движения.Записать();
```

### Начисление оклада

Из `ДанныеГрафика` получаем отработанные часы план и факт, дни факт (пригодится для больничного), а также оклад с учетом сторно:

```1c
ВЫБРАТЬ
	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	ОсновныеНачисленияДанныеГрафика.Сторно КАК Сторно,
	ОсновныеНачисленияДанныеГрафика.Оклад КАК Оклад,
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ЧасовФакт,
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК ЧасовПлан,
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ДнейФакт
ПОМЕСТИТЬ втДанныеГрафика
ИЗ
	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
			Регистратор = &Ссылка
				И ВидРасчета В (&ВидыРасчетаОклад)) КАК ОсновныеНачисленияДанныеГрафика

ИНДЕКСИРОВАТЬ ПО
	Сотрудник
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
	втДанныеГрафика.ЧасовФакт КАК ЧасовФакт,
	втДанныеГрафика.ЧасовПлан КАК ЧасовПлан,
	втДанныеГрафика.ДнейФакт КАК ДнейФакт,
	// если движение - сторно, то оклад нужно брать за тот месяц,
	// в котором был больничный
	ВЫБОР
		КОГДА втДанныеГрафика.Сторно
			ТОГДА втДанныеГрафика.Оклад
		ИНАЧЕ ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0)
	КОНЕЦ КАК Оклад
ИЗ
	втДанныеГрафика КАК втДанныеГрафика
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
				&ПериодРегистрации,
				Сотрудник В
					(ВЫБРАТЬ
						втДанныеГрафика.Сотрудник КАК Сотрудник
					ИЗ
						втДанныеГрафика КАК втДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
```

Заполняем движения. Для сторно-записей очищаем часы и дни факт (чтобы не прибавлялись часы и дни, которые человек не отработал)

```1c
Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
	Выборка.Сбросить();
	Если НЕ Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
		Продолжить;
	КонецЕсли;
		
	ЗаполнитьЗначенияСвойств(Движение, Выборка); 
		
	Движение.Результат = ?(Выборка.ЧасовПлан = 0,0,
	Выборка.Оклад * Выборка.ЧасовФакт / Выборка.ЧасовПлан);

	// очищаем, чтобы часы и дни болезни не прибавились к рабочим	
	Если Движение.Сторно Тогда
		Движение.Результат = -1 * Движение.Результат;
		Движение.ЧасовФакт = 0;
		Движение.ДнейФакт = 0;
	КонецЕсли;
КонецЦикла;
```

## Расчет больничного


Получаем:
- из `ДанныеГрафика`: 
	- `ДнейФактическийПериодДействия` (дни больничного)
- из `БазаОсновныеНачисления`:
	- `ДнейФактБаза` (отработанные дни за прошлый месяц)
	- `РезультатБаза` (все полученные деньги от основных начислений)
- из `БазаДополнительныеНачисления`:
	- `РезультатБаза` (все полученные деньги от дополнительных начислений)

```1c
ВЫБРАТЬ
	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ДнейБольничного,
	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.ДнейФактБаза, 0) КАК ДнейБазовыйПериод,
	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(ОсновныеНачисленияБазаДополнительныеНачисления.РезультатБаза, 0) КАК НачисленияБазовыйПериод
ИЗ
	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
			Регистратор = &Ссылка
				И ВидРасчета В (&ВидыРасчетаБольничный)) КАК ОсновныеНачисленияДанныеГрафика
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
				&Измерения,
				&Измерения,
				,
				Регистратор = &Ссылка
					И ВидРасчета В (&ВидыРасчетаБольничный)) КАК ОсновныеНачисленияБазаОсновныеНачисления
		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаДополнительныеНачисления(
				&Измерения,
				&Измерения,
				,
				Регистратор = &Ссылка
					И ВидРасчета В (&ВидыРасчетаБольничный)) КАК ОсновныеНачисленияБазаДополнительныеНачисления
		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаДополнительныеНачисления.НомерСтроки
```


## Отчет Диаграмма Ганта

- создаем отчет + произвольную форму
- добавляем `Диаграмма Ганта, Период` +  кнопка `Сформировать`
- с помощью запроса получаем данные `ФактическийПериодДействия` с отбором по `ПериодДействия`
- в коде формируем диаграмму:
```1c
// обязательно очищаем, иначе будет неверное отображение
ДиаграммаГанта.Обновление = Ложь;
ДиаграммаГанта.Очистить();

// ...
	
Пока Выборка.Следующий() Цикл
	Точка = ДиаграммаГанта.УстановитьТочку(Выборка.Сотрудник); 
	Точка.Текст = Выборка.Сотрудник;
		
	Серия = ДиаграммаГанта.УстановитьСерию(Выборка.ВидРасчета);
	Серия.Текст = Выборка.ВидРасчета;
		
	ЗначениеДГ = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
		
	Интервал = ЗначениеДГ.Добавить();
	Интервал.Начало = Выборка.ПериодДействияНачало;
	Интервал.Конец = Выборка.ПериодДействияКонец;
КонецЦикла; 
```
